mapscripts FennilahlTown_MapScripts {}

script FennilahlTown_EventScript_MartEmployeeRoadBlock {
    lockall
    faceplayer
    msgbox(format("I'm the chauffeur for the detectives in charge of the Tenebris disappearance. They're in Carabrue Town right now speaking to the lab staff."))
    releaseall
}

script FennilahlTown_EventScript_Man {
    lockall
    faceplayer
    warp(MAP_MIRROH_PEAK, 19, 17)

    msgbox(format("I was watching the news the other day. They say that the Pokemon Psychologist has gone missing. Who? Professor Tenebris of course!"))
    releaseall
}

script FennilahlTown_EventScript_Woman {
    lockall
    faceplayer
    if (flag(FLAG_FENNILAHL_CUTEMONGIFT)){
        msgbox(format("If your {PKMN} hold a SOOTHE BELL, they'll become attached to you faster!"))
    } else {
        msgbox(format("My, that's such a cute little {PKMN} you have with you! Here, use this and treat it with care, okay?"))
        giveitem(ITEM_SOOTHE_BELL)
        setflag(FLAG_FENNILAHL_CUTEMONGIFT)
    }   
    releaseall
}

script FennilahlTown_EventScript_HikerTunnel {
    lockall
    faceplayer

    goto_if_quest_active(QUEST_FENNILAHL_HIKER, Fennilahl_Breech_QuestInProgress)
    goto_if_quest_inactive(QUEST_FENNILAHL_HIKER, Fennilahl_Breech_StartQuest)

    releaseall
    end
}

script Fennilahl_Breech_StartQuest{

    msgbox(format("I found a STRANGE ROCK in the caves south of town, but a small PINK {PKMN} took it and ran away! And I don't have any {PKMN} to help me get it back! I don't want to battle - I can't deal with conflict.\pI don't know what to do! Will you help me out?"), MSGBOX_YESNO)

    if (var(VAR_RESULT) == 1){
        msgbox(format("Amazing! You've answered my prayers. I saw the pink {PKMN} run down south, towards ROUTE 2. I really liked that STRANGE ROCK! Please bring it back."))
        //activate quest
        startquest(QUEST_FENNILAHL_HIKER)
    } else {
        msgbox(format("Oh, what will I do!"))
    }

    releaseall
    end
}

script Fennilahl_Breech_QuestInProgress{

    if (flag(FLAG_FENNILAHL_HIKER_GEAR_GET)){
        msgbox(format("You brought my STRANGE ROCK? Amazing! How can I thank you for this?"))
        msgbox(format("A CLEFAIRY was keeping it? This is the CLEFAIRY? And it's been following you around because it can't let go of the rock?"))
        release
        delay(16)
        playmoncry(SPECIES_CLEFAIRY, CRY_MODE_NORMAL)
        msgbox(format("CLEFAIRY: clee!!!"))
        msgbox(format("Ouch! CLEFAIRY stop! Haha! Alright alright, you come along with me then. But you're not my {PKMN} okay? I don't like battling. You'll be my hiking partner!"))        
        msgbox(format("Thank you trainer! My name is BREECH. I'm just a simple man who loves collecting rocks. Even with CLEFAIRY by my side, I won't battle. CLEFAIRY and I are heading off to explore the mountains now. See you again soon!"))
        release
        applymovement(5, moves(walk_up))
        waitmovement(0)
        removeobject(5)
        setflag(FLAG_FENNILAHL_HIKER_BLOCK)
        removeitem(ITEM_STRANGE_ROCK)

        //complete quest
        completequest(QUEST_FENNILAHL_HIKER) 
    } else {
        msgbox(format("Why do bad things happen to me?"))        
    }
    releaseall
    end
}













script FennilahlTown_Sign_Entrance {
    msgbox(format("FENNILAHL TOWN\nConnecting Mt. Ceram and Vine River."), MSGBOX_SIGN)
    end
}

script FennilahlTown_Sign_Route2 {
    msgbox(format("{DOWN_ARROW}ROUTE 2\nConnecting to Vine River."), MSGBOX_SIGN)
    end
}

script FennilahlTown_Sign_Route3{
    msgbox(format("{UP_ARROW} UNDERGROUND TUNNEL\nTo Route 3 and GASTREE CITY."), MSGBOX_SIGN)
    end
}


//FLAG_FENNILAHL_BUDEW_GET
script FennilahlTown_Budew{
    lockall

    if (var(VAR_FENNILAHL_BUDEW) == 2){
        goto(FennilahlTown_Budew_ThirdBerry)        
    }

    if (var(VAR_FENNILAHL_BUDEW) == 1){
        goto(FennilahlTown_Budew_SecondBerry)        
    }

    if (var(VAR_FENNILAHL_BUDEW) == 0){
        goto(FennilahlTown_Budew_FirstBerry)
    }


    releaseall
    end
}

script FennilahlTown_Budew_FirstBerry{

    playmoncry(SPECIES_BUDEW, CRY_MODE_NORMAL)
    applymovement(8, moves(jump_in_place_up * 3))
    waitmovement(0)
    msgbox(format("BUDEW seems to want something..."))
    release

    checkitem(ITEM_ORAN_BERRY)
    if (var(VAR_RESULT) == 1){
        msgbox(format("Give BUDEW an ORAN BERRY?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            removeitem(ITEM_ORAN_BERRY)
            faceplayer  
            waitmovement(0)
            delay(12)             
            msgbox(format("{PLAYER} gave BUDEW an ORAN BERRY."), MSGBOX_SIGN)
            release
            applymovement(8, moves(jump_in_place_down * 3))   
            waitmovement(0)       
            msgbox(format("BUDEW seems happy!"))
            release

            setvar(VAR_FENNILAHL_BUDEW, 1)

        } else {
        msgbox(format("BUDEW seems disappointed..."))
        release
        }
    } else {
        release
    }

    releaseall
    end
}

script FennilahlTown_Budew_SecondBerry{

    playmoncry(SPECIES_BUDEW, CRY_MODE_NORMAL)
    applymovement(8, moves(jump_in_place_up * 2))
    waitmovement(0)
    msgbox(format("BUDEW seems to want something again...but not an ORAN BERRY."))
    release

    checkitem(ITEM_PECHA_BERRY)
    if (var(VAR_RESULT) == 1){
        msgbox(format("Give BUDEW a PECHA BERRY?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            removeitem(ITEM_PECHA_BERRY)
            faceplayer 
            waitmovement(0)
            delay(12)             
            msgbox(format("{PLAYER} gave BUDEW a PECHA BERRY."), MSGBOX_SIGN)
            release
            applymovement(8, moves(jump_in_place_down * 2))   
            waitmovement(0)       
            msgbox(format("BUDEW seems happy!"))
            release

            setvar(VAR_FENNILAHL_BUDEW, 2)

        } else {
        msgbox(format("BUDEW seems disappointed..."))
        release
        }
    } else {
        release
    }

    releaseall
    end
}

script FennilahlTown_Budew_ThirdBerry{

    playmoncry(SPECIES_BUDEW, CRY_MODE_NORMAL)
    applymovement(8, moves(jump_in_place_up))
    waitmovement(0)
    msgbox(format("What could BUDEW want now?"))
    release

    checkitem(ITEM_RAWST_BERRY)
    if (var(VAR_RESULT) == 1){
        msgbox(format("Give BUDEW a RAWST BERRY?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            removeitem(ITEM_RAWST_BERRY)
            faceplayer 
            waitmovement(0)
            delay(12)             
            msgbox(format("{PLAYER} gave BUDEW a RAWST BERRY."), MSGBOX_SIGN)
            release
            applymovement(8, moves(jump_in_place_down))   
            waitmovement(0)       
            msgbox(format("BUDEW seems happy!"))
            release

            setvar(VAR_FENNILAHL_BUDEW, 3)

            goto(FennilahlTown_Budew_JoinParty)

        } else {
        msgbox(format("BUDEW seems disappointed..."))
        release
        }
    } else {
        release
    }

    releaseall
    end
}

script FennilahlTown_Budew_JoinParty{
    applymovement(8, moves(emote_question_mark))
    waitmovement(0)
    delay(16)
    msgbox(format("BUDEW is looking at the Pokeballs on your belt curiously..."))
    release
    delay(16)
    msgbox(format("BUDEW nudges the Pokeballs on your belt. It seems to want to join your party! Will you become BUDEW's trainer?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == 1){
        removeobject(8)
        givemon(SPECIES_BUDEW, 5)
        playfanfare(MUS_OBTAIN_ITEM)
        msgbox(format("The BUDEW has joined your party!\pWelcome, BUDEW! Let's get along well together!"))
    } else {
        applymovement(8, moves(walk_fast_right walk_fast_down * 3 walk_fast_right * 2 walk_fast_down * 4))
        removeobject(8)
        msgbox(format("BUDEW ran off..."))
        release
    }

    setflag(FLAG_FENNILAHL_BUDEW_GET)
    setvar(VAR_FENNILAHL_BUDEW, 4)
    releaseall
    end   
}







