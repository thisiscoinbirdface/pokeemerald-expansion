mapscripts AncientMirroh_Caves_MapScripts{
    MAP_SCRIPT_ON_RESUME: AncientMirroh_Caves_1F_SetHoleWarp
}

script AncientMirroh_Caves_1F_SetHoleWarp{
    setstepcallback(STEP_CB_CRACKED_FLOOR)
	setholewarp(MAP_ANCIENT_MIRROH_CAVES_B1F)
	end
}

//prof sighting
script AncientMirroh_Tenebris_Caves_Trigger{
    if (flag(FLAG_ANCIENT_MIRROH_PROF_TENEBRIS_INSIDE)) {end}

    lockall
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 5))
    waitmovement(0)
    special(RemoveCameraObject)

    applymovement(2, moves(walk_down * 4))
    waitmovement(0)

    removeobject(2)

    setflag(FLAG_ANCIENT_MIRROH_PROF_TENEBRIS_INSIDE)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 5))
    waitmovement(0)
    special(RemoveCameraObject)

    releaseall
    end
}

//aerodactyl
script AncientMirroh_Aerodactyl{
    lockall
    msgbox(format("It's an ancient Pokémon!\nIt's roosting. Wake it up?"), MSGBOX_YESNO)

    if (var(VAR_RESULT) == 0) {end}

    playmoncry(SPECIES_AERODACTYL, CRY_MODE_NORMAL)

    setwildbattle(SPECIES_AERODACTYL, 35)
    dowildbattle

    //result
    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) == B_OUTCOME_WON){
        setflag(FLAG_ANCIENT_MIRROH_AERODACTYL)
        removeobject(1)
    }
    if (var(VAR_RESULT) == B_OUTCOME_CAUGHT){
        setflag(FLAG_ANCIENT_MIRROH_AERODACTYL)
        removeobject(1)
    }            
    if (var(VAR_RESULT) == B_OUTCOME_RAN){
        msgbox(format("The Pokémon settles back into its sleep..."))
    }

    releaseall
    end
}





//tenebris item
script AncientMirroh_Tenebris_Item{
    lockall
    removeobject(4)
    giveitem(ITEM_RARE_SHARD)
    delay(16)
    msgbox(format("Was this left behind by that lady?"))

    setflag(FLAG_ITEM_ANCIENTMIRROH_F)
    releaseall
    end
}