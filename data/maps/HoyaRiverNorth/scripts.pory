mapscripts HoyaRiverNorth_MapScripts{}




script Route6_Transition_EventScript_SharpedoBlocker{
    msgbox(format("There's a dangerous Pok√©mon in the Hoya River - it smashed into the riverbank and threw these boulders up!\pWhat is it?\pDon't be shocked when I tell you - \nit's a wild Mega Sharpedo!"), MSGBOX_NPC)
    end
}






script Route6_Transition_Weather_StormTrigger{
    if (flag(FLAG_ROUTE6_KOHLA_BATTLE)){
        end
    } else {
        setweather(WEATHER_RAIN_THUNDERSTORM)
        doweather
        releaseall
        end
    }
}
script HoyaRiverNorth_SaveGameTrigger{
    if (flag(FLAG_ROUTE6_KOHLA_BATTLE)){end}   

    setvar(VAR_TEMP_C, 1)

    call(Common_EventScript_SaveGameBeforeSomber)
    releaseall

    end
}


script Route6_Transition_KohlaBattle_Trigger0{
    if (flag(FLAG_ROUTE6_KOHLA_BATTLE)) {end}

    lockall
    fadeoutbgm(0)
    delay(30)
    //Move camera
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 4))
    waitmovement(0)

    //Kohla waiting
    speakername("Kohla")
    msgbox(format("I've been waiting for you."))
    release  

    //move myself
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down * 4))
    waitmovement(0)
    special(RemoveCameraObject)  

    call(Route6_Transition_EventScript_KohlaBattle)
}

script Route6_Transition_KohlaBattle_Trigger1{
    if (flag(FLAG_ROUTE6_KOHLA_BATTLE)) {end}

    lockall
    fadeoutbgm(0)
    delay(30)
    //Move camera
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left walk_down * 4))
    waitmovement(0)

    //Kohla waiting
    speakername("Kohla")
    msgbox(format("I've been waiting for you."))
    release  

    //move myself
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left walk_down * 4))
    waitmovement(0)
    special(RemoveCameraObject)  

    call(Route6_Transition_EventScript_KohlaBattle)
}

script Route6_Transition_EventScript_KohlaBattle{

    //Dialogue
    speakername("Kohla")
    msgbox(format("Once again you save the day while I run off chasing dead leads. Your stupid Professor Rue's lead was bogus!\pIt's like the world is telling me that\nI'm not as good as you, {PLAYER}.\lBut you and I know the truth.\lYou lucked your way into this journey, while I worked hard all my life...\p...to get nothing.\p...\pI want you to know that I don't like you. And even if nobody else knows,\lI want you to know that I will NEVER\lbe inferior to you!"))
    release

    //Battle
    trainerbattle_no_intro(TRAINER_ROUTE6_KOHLA, "...again...")

    applymovement(1, moves(emote_exclamation_mark))
    waitmovement(0)
    speakername("Kohla")
    msgbox(format("No!! Not again!"))
    applymovement(1, moves(face_right))
    delay(24)
    speakername("Kohla")
    msgbox(format("Boltund, you're too weak!\nYou just lost me the battle!\lYou need to push yourself harder!"))
    release
    applymovement(1, moves(face_up))
    delay(24)
    speakername("Kohla")
    msgbox(format("{PLAYER}, this isn't over.\pMark my words!"))
    release

    applymovement(1, moves(walk_down * 7))
    waitmovement(0)
    removeobject(1)    

    //End
    setflag(FLAG_ROUTE6_KOHLA_BATTLE)
    releaseall
    end
}






















