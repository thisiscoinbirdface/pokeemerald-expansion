mapscripts MirrohBaseCampGym_MapScripts{
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_MIRROHBASECAMPGYM_STATE, 0: MirrohBaseCampGym_EventScript_Main
    ]
}

script MirrohBaseCampGym_EventScript_Main{
    lockall
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 4))
    waitmovement(0)

    special(RemoveCameraObject)

    setvar(VAR_MIRROHBASECAMPGYM_STATE, 1)
    releaseall
    end
}





script MirrohBaseCampGym_Leader{
    lockall
    faceplayer
    
    if (flag(FLAG_MIRROHBASECAMP_LEADER)){
        speakername("Carona")
        msgbox(format("Use your power wisely. As for me, it's time to move on."))
    } else {
        //Intro
        speakername("???")
        msgbox(format("Hmm? A trainer? Welcome to the Mirroh Base Camp Gym...\p...\p...is what I would say if there was a Gym left."))
        release
        delay(16)
        speakername("Carona")
        msgbox(format("My name is Carona, trainer. I am the former Leader of the Mirroh Base Camp Gym. The Pokémon League recently decomissioned this gym."))
        release
        delay(60)
        speakername("Carona")
        msgbox(format("They took the entrance statues out first. Then they removed all the gym puzzle equipment.\p...\pFinally, one by one, all my trainers left."))
        release
        delay(30)
        speakername("Carona")
        msgbox(format("And now it's just me in this empty warehouse. Like a ghost. Just like the rest of this little town of ours."))
        release
        applymovement(1, moves(emote_exclamation_mark))
        waitmovement(0)
        delay(12)
        speakername("Carona")
        msgbox(format("But I was a Gym Leader once! You understand? I was powerful and strong. I was something, someone! Countless trainers paid me their respects in battle, and to the chosen few destined for the top, I would award the coveted lapel pin known as the Ironfist Badge!"))
        release
        delay(60)
        speakername("Carona")
        msgbox(format("...but that's all in the past. Next week the construction crew will come to demolish this building. You were lucky you found me. I just dropped in to...just to be here again, and just...\p...but something's missing.\pYou're a trainer, aren't you? Would you care to indulge me a little? I'd like to be challenged by a trainer. It would mean everything if I could ride that thrill just once more."), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            speakername("Carona")
            msgbox(format("You are too kind, trainer. Come then, let us battle. Let these four empty walls be our spectators, this crooked floor our stadium, and the silence our cheers!"))
            fadescreen(FADE_TO_BLACK)
            release
            setobjectxy(OBJ_EVENT_ID_PLAYER, 1, 5)

            special(SpawnCameraObject)
            applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left * 2))
            waitmovement(0)
            special(RemoveCameraObject)
            applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
            applymovement(1, moves(face_left))
            fadescreen(FADE_FROM_BLACK)
            speakername("Carona")
            msgbox(format("Now, trainer, feel the force of these iron fists!"))
            release

            //Battle
            trainerbattle_no_intro(TRAINER_MIRROHBASECAMP_LEADER, "A glorious battle!")

            //Post battle
            //Receive the badge
            speakername("Carona")
            msgbox(format("What a beautiful display! What a thrill! This is why we do what we do, trainer!\pNow come to me."))
            release

            special(SpawnCameraObject)
            applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left * 2))
            applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 6))
            waitmovement(0)
            special(RemoveCameraObject)


            // fadescreen(FADE_TO_BLACK)
            // setobjectxy(OBJ_EVENT_ID_PLAYER, 7, 5)
            // applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
            // applymovement(1, moves(face_left))
            // fadescreen(FADE_FROM_BLACK)

            speakername("Carona")
            msgbox(format("Unfortunately I don't have a badge to award you any more.\pBut I have something else.\pI see the closure of this gym as destiny's way of telling Lucario and me to lay down our fists. It's time we explored new things. So I won't need this any more, and as my final opponent, I want you to have it."))
            setflag(FLAG_MIRROHBASECAMP_LEADER)
            giveitem(ITEM_MEGA_RING)
            //Play gym badge fanfare
            call(Common_EventScript_PlayGymBadgeFanfare)

            speakername("Carona")
            msgbox(format("The Mega Ring will allow you to Mega Evolve specific Pokémon as long as they hold their unique Mega Stone.\pPlease take these with you as well.")) 
            giveitem(ITEM_RARE_SHARD, 2)
            speakername("Carona")
            msgbox(format("I feel satisfied. A fitting end - to have been defeated by a trainer of your caliber!\pCarry on our legacy, {PLAYER}! Show the world your iron fists!"))
            release

        } else {
            speakername("Carona")
            msgbox(format("Haha! A fitting finale in a way."))
            release
        }
    }
    releaseall

}




