raw`
    .set LOCALID_DR_FR, 7
    .set LOCALID_DR_SC, 6
    .set LOCALID_DR_TH, 2   
    .set LOCALID_LAP, 1
`
//note on raw: lapras and dreepy third are on different maps


mapscripts IvyRiver_MapScripts{}


//fisherman quest
//deliver mail in a chain from the first to the third fisherman

//lapras quest
//not a quest, but dreepy tells you to follow
//inside a cave, you see a wounded lapras


script LilyPond_Dreepy_Trigger_First{
    if (flag(FLAG_LILYPOND_DREEPY_FIRST)) {end}

    lockall
    delay(24)
    applymovement(LOCALID_DR_FR, moves(face_left emote_exclamation_mark))
    delay(24)

    playmoncry(SPECIES_DREEPY, CRY_MODE_NORMAL)
    applymovement(LOCALID_DR_FR, moves(walk_fast_left * 3 jump_in_place_left emote_exclamation_mark jump_in_place_left jump_in_place_left ))
    waitmovement(0)
    delay(40)

    applymovement(LOCALID_DR_FR, moves(walk_fast_right * 3 walk_fast_down * 6))
    waitmovement(0)
    removeobject(LOCALID_DR_FR)

    setflag(FLAG_LILYPOND_DREEPY_FIRST)

}

script LilyPond_Dreepy_Trigger_Second{
    if (flag(FLAG_LILYPOND_DREEPY_SECOND)) {end}

    lockall
    delay(24)
    applymovement(LOCALID_DR_SC, moves(face_down emote_exclamation_mark))
    delay(24)

    playmoncry(SPECIES_DREEPY, CRY_MODE_NORMAL)
    applymovement(LOCALID_DR_SC, moves(walk_fast_down *3 jump_in_place_left emote_exclamation_mark jump_in_place_left jump_in_place_left ))
    waitmovement(0)
    delay(40)

    applymovement(LOCALID_DR_SC, moves(walk_fast_up * 7))
    waitmovement(0)
    removeobject(LOCALID_DR_SC)

    setflag(FLAG_LILYPOND_DREEPY_SECOND)

}

script LilyPond_Dreepy_Trigger_Third{
     if (flag(FLAG_LILYPOND_DREEPY_THIRD)) {end}

    lockall
    delay(24)

    playmoncry(SPECIES_DREEPY, CRY_MODE_NORMAL)
    applymovement(LOCALID_DR_TH, moves(walk_fast_down * 2 jump_in_place_down emote_exclamation_mark jump_in_place_down * 2 ))
    waitmovement(0)
    delay(40)

    applymovement(LOCALID_DR_TH, moves(walk_fast_up * 7))
    waitmovement(0)
    removeobject(LOCALID_DR_TH)

    setflag(FLAG_LILYPOND_DREEPY_THIRD)
   
}

script LilyPond_Cave_Dreepy{
    playmoncry(SPECIES_DREEPY, CRY_MODE_NORMAL)
    msgbox(format("Dreepy is nuzzling Lapras' wound, but it's not helping much."), MSGBOX_NPC)
    end
}
script LilyPond_Cave_Drakloak{
    playmoncry(SPECIES_DRAKLOAK, CRY_MODE_NORMAL)
    msgbox(format("Drakloak looks worried. Must be hard, knowing there's nothing you can do..."), MSGBOX_NPC)
    end
}
script LilyPond_Cave_Dragapult{
    playmoncry(SPECIES_DRAGAPULT, CRY_MODE_NORMAL)
    msgbox(format("Dragapult is looking around. Perhaps its trying to keep everyone safe?"), MSGBOX_NPC)
    end
}

script LilyPond_Cave_Lapras{
    lockall
    faceplayer
    playmoncry(SPECIES_LAPRAS, CRY_MODE_NORMAL)    
    msgbox(format("Lapras is crying in pain! What happened?\p...\pThere's a huge wound on its side! A regular Potion or Super Potion won't cut it for this..."), MSGBOX_NPC)
    delay(8)
    checkitem(ITEM_HYPER_POTION)
    if (var(VAR_RESULT) == 0){end}

    msgbox(format("Administer a Hyper Potion on Lapras' wound?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == 0){end}

    playse(SE_USE_ITEM)
    msgbox(format("{PLAYER} used a Hyper Potion on Lapras."))
    release
    delay(16)
    playmoncry(SPECIES_LAPRAS, CRY_MODE_NORMAL)    
    msgbox(format("Lapras is looking better!"))
    release

    //dragapult leaves
    applymovement(3, moves(emote_exclamation_mark))
    applymovement(4, moves(emote_exclamation_mark))
    applymovement(5, moves(emote_exclamation_mark))
    applymovement(6, moves(emote_exclamation_mark))
    waitmovement(0)
    delay(24)
    applymovement(1, moves(walk_up * 2))
    waitmovement(0)        
    applymovement(1, moves(walk_up * 5))
    applymovement(3, moves(walk_down * 4))
    applymovement(4, moves(walk_down * 7))
    applymovement(5, moves(walk_down * 7))
    applymovement(6, moves(walk_down * 7))
    waitmovement(0)    

    delay(8)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    applymovement(3, moves(walk_left walk_up * 2))
    waitmovement(0)

    delay(16)

    playmoncry(SPECIES_DREEPY, CRY_MODE_NORMAL)    
    applymovement(3, moves(emote_exclamation_mark jump_in_place_up))
    msgbox(format("DREEPY: dreee!!!"))
    release
    delay(16)
    applymovement(3, moves(walk_fast_down * 4))
    waitmovement(0)

    removeobject(1)
    removeobject(3)
    removeobject(4)
    removeobject(5)
    removeobject(6)

    setflag(FLAG_LILYPOND_LAPRAS)
    //thank you
    releaseall
    end

}









//fisherman quest - get trolling rod and legendary
//three brothers are fishing in lily pond
//when they were young, they had a fourth brother who went missing somewhere in ivy river
//brother was raised by zarude

//part 1: deliver mail to the other two fishermen (get rod for this)
    //the mail is asking the brothers if they want to try and search
    //player has input and says yes
//part 2: meet them further south (access to legendary)
    //they begin searching and ask the player to help
    //find a boy's scarf
    //take it back to the fishermen - they confirm it's their brothers
//part 3: 
    //zarude and brother are there
    //reunited
    //zarude leaves
//part 4: find zarude later in that patch


script LilyPond_Fisherman_A{
    if (var(VAR_LILYPOND_FISHERMAN_QUEST) == 0) {
        msgbox(format("My brothers and I come to fish here every year. On the same day."), MSGBOX_NPC)
        end
    }

    if (var(VAR_LILYPOND_FISHERMAN_QUEST) == 1){
        msgbox(format("My brothers and I come to fish here every year. On the same day."), MSGBOX_NPC)   
        applymovement(1, moves(emote_question_mark))
        waitmovement(0)
        delay(24)
        msgbox(format("You have a letter for me? Let me see.\p...\p...\p...\pGot it. How can I say no? I'm going ahead!"))     
        addvar(VAR_LILYPOND_FISHERMAN_QUEST, 1)
        setflag(FLAG_LILYPOND_FISHERMAN_A_LETTER)
        
        //move the fisherman
        applymovement(1, moves(walk_left face_up))
        waitmovement(0)
        delay(8)
        removeobject(1)
        //move the ship
        applymovement(2, moves(walk_right * 10))
        waitmovement(0)
        removeobject(2)

        end
    }

}
script LilyPond_Fisherman_B{
    if (var(VAR_LILYPOND_FISHERMAN_QUEST) == 0) {
        msgbox(format("Such a beautiful pond...such bitter memories."), MSGBOX_NPC)
        end
    }

    if (var(VAR_LILYPOND_FISHERMAN_QUEST) == 1){
        msgbox(format("Such a beautiful pond...such bitter memories."), MSGBOX_NPC)   
        applymovement(3, moves(emote_question_mark))
        waitmovement(0)
        delay(24)
        msgbox(format("You have a letter for me? What does it say?\p...\p...\p...\pUnderstood. I'm in. Time to move!"))     
        addvar(VAR_LILYPOND_FISHERMAN_QUEST, 1)
        setflag(FLAG_LILYPOND_FISHERMAN_B_LETTER)

        //move the fisherman
        applymovement(3, moves(walk_right face_up))
        waitmovement(0)
        delay(8)
        removeobject(3)
        //move the ship
        applymovement(5, moves(walk_right * 5 walk_down *7))
        waitmovement(0)
        removeobject(5)

        end
    }

}
script LilyPond_Fisherman_C{
    if (var(VAR_LILYPOND_FISHERMAN_QUEST) == 2) {
        msgbox(format("Have you delivered the mail to my brothers yet?"), MSGBOX_NPC)
        end
    }


    if (var(VAR_LILYPOND_FISHERMAN_QUEST) == 1) {
        msgbox(format("Have you delivered the mail to my brothers yet?"), MSGBOX_NPC)
        end
    }


    //Startquest
    lockall
    msgbox(format("My brothers and I fish around Lily Pond. We have a connection to this place - although not a happy one. Do you want to hear our story?"), MSGBOX_YESNO)

    if (var(VAR_RESULT) == 0){
        msgbox(format("Just as well. It hurts to tell it."), MSGBOX_NPC)
        end
    }

    msgbox(format("We were four brothers. Many years ago, we came to Lily Pond to fish for our first Pokemon. While three of us stayed here, our youngest brother said he wanted to go down the waterfall and into Ivy River. We tried to stop him, but he went away, and was never seen again."))
    release
    delay(16)
    message(format("A sad story, I know."))
    waitmessage
    dynmultichoice(0, 0, TRUE, 3, 0, DYN_MULTICHOICE_CB_NONE, "I'm sorry to hear that.", "I'm sure he's safe.", "Don't give up!")

    if (var(VAR_RESULT) == 0){
        msgbox(format("Thank you.\pI've taken up too much of your time. I appreciate you listening to me."), MSGBOX_NPC)
        end
    }
    if (var(VAR_RESULT) == 1){
        msgbox(format("Thank you. I'm sure too.\pI've taken up too much of your time. I appreciate you listening to me."), MSGBOX_NPC)
        end
    }

    msgbox(format("Don't give up, you say? But what can we do? Search again?\p...\pBut you're right, of course. We mustn't lose hope! I'd like to try searching once again. Perhaps my brothers would too. If I write them a letter asking them to help search, would you deliver it to them?"))
    release
    giveitem(ITEM_FISHERMAN_LETTER)
    startquest(QUEST_LILYPOND_FISHERMAN)
    setflag(FLAG_LILYPOND_FISHERMAN_QUEST_START)
    setvar(VAR_LILYPOND_FISHERMAN_QUEST, 1)

    releaseall
    end
}



