raw`
    .set LOCALID_S_MAIN, 3
    .set LOCALID_S1, 1
    .set LOCALID_S2, 2
    .set LOCALID_S3, 4
    .set LOCALID_S4, 5
`

mapscripts PellucaFishery_MapScripts{}



script PellucaFishery_NPC1{
    msgbox(format("Yeah, that's right! I've got the Qwil blood in my veins! I dream about Qwilfish - nobody's a bigger Qwilsquadder than me!\p...\p...is that a Detective Academy pass?\pListen, I'm an agent working undercover. Don't blow my cover!"), MSGBOX_NPC)
    end
}
script PellucaFishery_NPC2{
    msgbox(format("The boss is actually gonna make some real change happen around here! But for that, we need money. That's why we're taxing the citizens!"), MSGBOX_NPC)
    end
}
script PellucaFishery_NPC3{
    msgbox(format("What's up new recruit?"), MSGBOX_NPC)
    end
}
script PellucaFishery_NPC4{
    msgbox(format("This used to be a boathouse before we came in. All this stuff is from back then."), MSGBOX_NPC)
    end
}


//1 main dude
//0: start the quest (sharpedo is attacking but its not normal)
//1: you'll help but you can't beat that sharpedo as you are - come back when you get a mega stone
//2: get surf with mega stone, and surf south of here to beat the sharpedo. but you need gym 6.
//3: completed quest

script PellucaFishery_EventScript_MainDude{
    lockall
    faceplayer

    speakername("Q-squad Boss")
    msgbox(format("So you're the new recruit, eh? Got a Qwilfish and everything, do ya?\p...\pFunny thing is, I don't remember any new recruits signing up."))
    release

    delay(16)
    //camera pan down
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 2 walk_right * 2))
    waitmovement(0)
    special(RemoveCameraObject)


    //move all the sailors
    applymovement(LOCALID_S1, moves(walk_right * 2 walk_up * 2 face_right))
    applymovement(LOCALID_S3, moves(walk_down * 2 walk_right face_up))
    applymovement(LOCALID_S4, moves(walk_left * 5))
    applymovement(LOCALID_S2, moves(walk_left * 3 walk_down * 4 face_up))
    waitmovement(0)

    delay(16)
    speakername("Q-squad Boss")
    msgbox(format("Thought you were being slick, eh? Trying to pull a fast one on us?\pMake the kid pay, boys!"))
    release    
    delay(16)

    //battle 1
    applymovement(LOCALID_S1, moves(jump_in_place_right))    
    speakername("Q-squad Grunt")
    msgbox(format("Aye-aye boss!\pYou're about to get crushed, kid! That's what you get for messing with the Qwilsquad!"))    
    trainerbattle_no_intro(TRAINER_PELLUCA_QWILSQUAD_A, format("I let you down, boss!"))
    
    delay(8)
    applymovement(LOCALID_S1, moves(walk_left face_right))    
    speakername("Q-squad Grunt")
    msgbox(format("Argh! This kid is good!"))    

    applymovement(LOCALID_S4, moves(jump_in_place_left))    
    speakername("Q-squad Grunt")
    msgbox(format("S'matter with you? You're ruining our squad's name!\pC'mon kid, you think you can get away with this?"))    
    trainerbattle_no_intro(TRAINER_PELLUCA_QWILSQUAD_B, format("No! I lost! Sorry, boss!"))
    
    delay(8)
    applymovement(LOCALID_S4, moves(walk_right face_left))    
    speakername("Q-squad Grunt")
    msgbox(format("Boss! We-we can't handle this trainer. You gotta take care of this!"))    

    delay(16)
    speakername("Q-squad Boss")
    msgbox(format("...\pFine then.\pI respect a strong trainer. But respect goes only so far. You're going to pay for what you did to my boys!"))
    release    
    trainerbattle_no_intro(TRAINER_PELLUCA_QWILSQUAD_BOSS, format("Overqwil! Noo!"))

    delay(16)
    speakername("Q-squad Boss")
    msgbox(format("Argh! Fine! Fine!\pI'm man enough to admit defeat. You're strong, kid. I'll give you that.\pWhat's your name?\p{PLAYER}?\p...\pThe mayor sent you? Why are you even listening to that blundering idiot?\pHe's totally useless! That's why I decided to step in. Pelluca City is my home.\pBut if you say so, I'm willing to consider talking to him again."))
    release    

    //Camera shake
	setvar(VAR_0x8004, 2)  //vertical pan
	setvar(VAR_0x8005, 2)  //horizontal pan
	setvar(VAR_0x8006, 8)  //num shakes
	setvar(VAR_0x8007, 5)  //shake delay
    special(ShakeCamera)    
    delay(48)

    applymovement(LOCALID_S1, moves(emote_exclamation_mark jump_in_place_down))    
    applymovement(LOCALID_S2, moves(emote_exclamation_mark jump_in_place_down))    
    applymovement(LOCALID_S3, moves(emote_exclamation_mark jump_in_place_down))    
    applymovement(LOCALID_S4, moves(emote_exclamation_mark jump_in_place_down))    
    applymovement(LOCALID_S_MAIN, moves(emote_exclamation_mark jump_in_place_down))    
    applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_exclamation_mark jump_in_place_down))   
    waitmovement(0)
    delay(24)     
    speakername("Q-squad Boss")
    msgbox(format("What was that?\pBoys - we gotta go outside and see what's going on!\pC'mon {PLAYER}!"))    
    release

    //exit
    setflag(FLAG_PELLUCA_REGULAR_CITIZENS)
    clearflag(FLAG_PELLUCA_FLOODED_CITIZENS)    
    
    setvar(VAR_PELLUCA_MAYOR_QUEST, 3)
    warp(MAP_PELLUCA_CITY, 43, 28)


    releaseall
    end
}

