//Local IDs for shopkeeper, grunts, leader, boss etc.
raw `
    .set LOCALID_GRUNT1, 1
    .set LOCALID_GRUNT2, 2
    .set LOCALID_GRUNT3, 3
    .set LOCALID_GRUNT4, 4
    .set LOCALID_BOSS, 9
    .set LOCALID_LEADER, 5
    .set LOCALID_SHOPKEEPER2F, 8
`


mapscripts CeramBaseCamp_Main_MapScripts{}

script CeramBaseCamp_Main_EventScript_EvilTrigger1{
    //Check flag
    if (flag(FLAG_CERAMBASECAMP_EVIL_BATTLE)){end}

    //Move player to the spot
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left * 2))
    waitmovement(0)
    call(CeramBaseCamp_Main_EventScript_MainEncounter)
}

script CeramBaseCamp_Main_EventScript_EvilTrigger2{
    //Check flag
    if (flag(FLAG_CERAMBASECAMP_EVIL_BATTLE)){end}

    //Move player to the spot
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left * 2 walk_up face_left))
    waitmovement(0)    
    call(CeramBaseCamp_Main_EventScript_MainEncounter)
}

script CeramBaseCamp_Main_EventScript_EvilTrigger3{
    //Check flag
    if (flag(FLAG_CERAMBASECAMP_EVIL_BATTLE)){end}

    //Move player to the spot    
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left * 2 walk_up *2 face_left))
    waitmovement(0)
    call(CeramBaseCamp_Main_EventScript_MainEncounter)
}


script CeramBaseCamp_Main_EventScript_MainEncounter{
    //Main dialogue between the people (shopkeeper abducted)
    lockall
    applymovement(LOCALID_LEADER, moves(jump_in_place_up))
    waitmovement(0)
    msgbox(format("???: Release our shopkeeper at once! How dare you!"))
    release
    applymovement(LOCALID_BOSS, moves(jump_in_place_down))
    waitmovement(0)
    msgbox(format("???: Get out of my face, leader! We have business that concerns you not one bit. We want his supplies for our trek up MT CERAM!"))
    msgbox(format("ARIAN: The name's ARIAN! And no way! This is MY base camp! You're not harming anyone while I'm around!"))
    msgbox(format("???: Hahaha! Well said! You are a leader after all. But how do you intend to handle the two of us, when there's just one of you?"))
    release


    //Player is noticed - leader notices, asks for help    
    applymovement(LOCALID_LEADER, moves(face_right))
    waitmovement(0)
    applymovement(LOCALID_LEADER, moves(emote_exclamation_mark))
    waitmovement(0)    
    msgbox(format("Hey - you! You look like a trainer. I need your help. These people are trying to steal the shopkeeper's supplies. We have to stop them!\pI'll take the boss. You deal with the one on the right for me!"))
    release

    //Player walks up and deals with grunt
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left face_up))
    waitmovement(0)
    applymovement(LOCALID_GRUNT4, moves(emote_exclamation_mark))
    waitmovement(0)
    msgbox(format("???: GABRIELLE: It's you?"))
    release
    applymovement(LOCALID_GRUNT4, moves(face_left))
    waitmovement(0)
    msgbox(format("GABRIELLE: Boss! Boss! This is the kid that beat me in Route 4. I was trying to take the dreamstone from that Clefairy hiker."))
    release
    applymovement(LOCALID_BOSS, moves(face_right))
    waitmovement(0)
    msgbox(format("???: That wasn't a dreamstone you idiot - that was a moonstone! Nonetheless, remind me to punish you for that later. \pGABRIELLE:...yes boss.\p???: But there is a dreamstone sitting right on top of Mt. Ceram, and these people are in the way! GABRIELLE, you deal with this pest while I put the leader in her place. And you better win this time!"))
    release
    applymovement(LOCALID_GRUNT4, moves(face_down))
    waitmovement(0)
    msgbox(format("GABRIELLE: You heard that...what was your name again? {PLAYER}? Yeah, {PLAYER}! I'm not going to lose to you this time!"))
    release 

    //Set all sprites facing each other
    applymovement(LOCALID_BOSS, moves(face_down))    
    applymovement(LOCALID_LEADER, moves(face_up))
    waitmovement(0)

    //Single battle - vs the grunt
    trainerbattle_no_intro(TRAINER_CERAMBASECAMP_EVIL_GABRIELLE, format("No! Not again!"))


    //Defeat, but it doesn't matter - they'll just make it up the mountain anyway
    applymovement(LOCALID_BOSS, moves(jump_in_place_down))
    waitmovement(0)
    msgbox(format("???: Agh! You stupid fire-type monkey! I just didn't have my {PKMN} with me."))
    release
    msgbox(format("ARIAN: Serves you right! Now move over and give our shopkeeper back."))
    applymovement(LOCALID_GRUNT4, moves(face_left))
    waitmovement(0)
    msgbox(format("GABRIELLE: I...I'm sorry boss! I lost again."))
    release 
    applymovement(LOCALID_BOSS, moves(face_right))
    waitmovement(0)
    msgbox(format("???: You useless, incompetent fool! Argh! How can we accomplish anything with such terrible support!"))   
    release 
    applymovement(LOCALID_BOSS, moves(face_down))
    applymovement(LOCALID_GRUNT4, moves(face_down))
    waitmovement(0)
    msgbox(format("???: Fine, take your stupid shopkeeper back. We'll make it up the mountain anyway and there's nothing you can do to stop us!"))
    release

    //Walk out
    applymovement(LOCALID_BOSS, moves(walk_left walk_down *2 walk_right * 10))
    applymovement(LOCALID_GRUNT4, moves(walk_left *2 walk_down *2 walk_right * 10))
    waitmovement(0)

    //Shopkeeper thanks both
    //ARIAN says the boss was powerful...I can't let you go alone without at least getting the badge
    applymovement(LOCALID_LEADER, moves(face_right))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))
    waitmovement(0)    
    msgbox(format("ARIAN: Thanks for the help trainer! I needed it there. I could've taken them both alone, but I didn't have all my {PKMN} on me."))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
    applymovement(LOCALID_LEADER, moves(face_up))
    waitmovement(0)
    msgbox(format("ARIAN: They didn't harm you right?\pSHOPKEEPER: No they didn't. Thank you ARIAN! And you too, trainer. I owe you one. Stop by our counter downstairs for trekking supplies later on."))
    release
    applymovement(LOCALID_SHOPKEEPER2F, moves(walk_down walk_left walk_down *2 walk_right * 10))
    waitmovement(0)

    //Final dialogue
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))
    applymovement(LOCALID_LEADER, moves(face_right))
    waitmovement(0)
    msgbox(format("ARIAN: That boss was really tough!\pHmm? You've met these thugs before? And you're also going to Mt. Ceram? What's up there?\pDreamstones? I thought those were just rumours."))
    msgbox(format("ARIAN: They're connected to Professor Tenebris' disappearance? \pWell I gotta do something - Professor Tenebris is the one who gave me my GROWLITHE."))
    msgbox(format("ARIAN: Those guys seem to be after the dreamstone too...but I can't let you go up the mountain as you are now."))
    msgbox(format("ARIAN: {PLAYER}, was it? Mt. Ceram is filled with dangerous {PKMN}. The rules around here are you can't climb unless you have my gym badge and my blessing. If you can beat me, I'll be able to send you up the hill with confidence. See you around, {PLAYER}!"))
    release

    //End encounter
    fadescreen(FADE_TO_BLACK)
    removeobject(LOCALID_GRUNT1)
    removeobject(LOCALID_GRUNT2)
    removeobject(LOCALID_GRUNT3)
    removeobject(LOCALID_GRUNT4)
    removeobject(LOCALID_BOSS)
    removeobject(LOCALID_LEADER)
    removeobject(LOCALID_SHOPKEEPER2F)

    setflag(FLAG_CERAMBASECAMP_EVIL_BATTLE)
    clearflag(FLAG_HIDE_BASECAMP_SHOPKEEPER)

    fadescreen(FADE_FROM_BLACK)    
    releaseall

}





script CeramBaseCamp_Main_EventScript_ExitTrigger{
    lockall
    applymovement(9, moves(face_left emote_exclamation_mark))
    waitmovement(0)
    release
    msgbox(format("Hey kid! The rules are you can't climb unless you have the FIRE BADGE. If you can't beat ARIAN, you won't be able to handle the heat of the mountain!"))
    release
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down))
    waitmovement(0)
    releaseall

}



script CeramBaseCamp_Main_TrainerBattle_Grunt1 {
    trainerbattle_single(TRAINER_CERAMBASECAMP_GRUNT1, format("Hey, where do you think you're going kid? This supply depot is under Team Somber's control!"), format("Gah! I lost!"))
    msgbox(format("We're here to collect supplies for the climb up Mt. Ceram."))
    release
    end
}

script CeramBaseCamp_Main_TrainerBattle_Grunt2 {
    trainerbattle_single(TRAINER_CERAMBASECAMP_GRUNT2, format("We're going to steal all the climbing supplies here!"), format("I can't believe it!"))
    msgbox(format("You just got lucky. I'll crush you if I ever see you again!"))
    release
    end
}

script CeramBaseCamp_Main_TrainerBattle_Grunt3 {
    trainerbattle_single(TRAINER_CERAMBASECAMP_GRUNT3, format("How did you get this far? No matter - you can't stop us from taking all the supplies!"), format("No! My {PKMN} were too weak!"))
    msgbox(format("You're too late - the boss is already with the shopkeeper."))
    release
    end
}













///MART

script CeramBaseCamp_Main_Pokemart_EventScript_Clerk{
    lock
	faceplayer
    msgbox(format("Hey {PLAYER}! Here you can get mountain-climbing supplies!"))
	message(gText_HowMayIServeYou)
	waitmessage
	pokemart(CeramBaseCamp_Main_Pokemart_Inventory)
	msgbox(gText_PleaseComeAgain, MSGBOX_DEFAULT)
	release
	end
}


mart CeramBaseCamp_Main_Pokemart_Inventory{
    ITEM_POKE_BALL
    ITEM_REPEL
    ITEM_ESCAPE_ROPE
    ITEM_FRESH_WATER
    ITEM_SODA_POP
    ITEM_LAVA_COOKIE

    
}















///ALL THE NPCS
//FLAG_CERAMBASECAMP_EVIL_BATTLE

script CeramBaseCamp_Main_NPC1{
    lockall
    faceplayer

    if (flag(FLAG_CERAMBASECAMP_EVIL_BATTLE)){
        //Regular case
        msgbox(format("I'm glad those Team Somber people have left!"))
    } else {
        //When Team Somber is there
        msgbox(format("Who are these Team Somber people? They act all high and mighty, but they're really just harrassing innocent folk."))
    }

    releaseall
}

script CeramBaseCamp_Main_NPC2{
    lockall
    faceplayer

    if (flag(FLAG_CERAMBASECAMP_EVIL_BATTLE)){
        //Regular case
        msgbox(format("It's believed that a dreamstone is a crystallised representation of a {PKMN}'s memories."))
    } else {
        //When Team Somber is there
        msgbox(format("I overheard them talking about a dreamstone! Dreamstones are supposed to be very rare. What do they want with one?"))
    }

    releaseall
}

///QUEST
script CeramBaseCamp_Main_NPC3{
    lockall
    faceplayer

    if (flag(FLAG_CERAMBASECAMP_EVIL_BATTLE)){

        if (flag(FLAG_CERAMBASECAMP_MAIN_BERRY)){
            msgbox(format("Potions are all well and good, but berries can be used in battle!"))
        } else {
            msgbox(format("Berries don't grow here, so I brought my own! Would you like some?"))
            release
            giveitem(ITEM_ORAN_BERRY, 2)
            setflag(FLAG_CERAMBASECAMP_MAIN_BERRY)
        }
            //Regular case
    } else {
        //When Team Somber is there
        msgbox(format("These thugs took the shopkeeper upstairs! But ARIAN, the Gym Leader, just went up there to get him back. He's going to teach them a lesson!"))
    }

    releaseall
}

script CeramBaseCamp_Main_NPC4{
    lockall
    faceplayer

    if (flag(FLAG_CERAMBASECAMP_EVIL_BATTLE)){
        //Regular case
        msgbox(format("This isn't really a town, so the supply depot and the management office is all we have."))
    } else {
        //When Team Somber is there
        msgbox(format("Shh! Don't look here! You'll attract their attention!"))
    }

    releaseall
}

//TRADE NPC (later)
script CeramBaseCamp_Main_NPC5{
    lockall
    faceplayer

    if (flag(FLAG_CERAMBASECAMP_NPC_TRADE)){
        msgbox(format("My sister will be so happy. She's always wanted her own {PKMN}."))
        releaseall
        end
    }

    if (flag(FLAG_CERAMBASECAMP_EVIL_BATTLE)){
        //Regular case
        showmonpic(SPECIES_NYMBLE, 10, 3)
        msgbox(format("I want to gift my sister a NYMBLE. It's her favourite {PKMN}! But I couldn't find any on Route 4."))
        hidemonpic
        msgbox(format("If you have one, will you trade it for my WIMPOD?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            //do trade

            //First is trade mon setup
            setvar(VAR_0x8008, INGAME_TRADE_MEOWTH) //Get the specific in-game trade mon (trade.h)
            copyvar(VAR_0x8004, VAR_0x8008)         //copy the trade mon

            specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	        copyvar(VAR_0x8009, VAR_RESULT)         //Get and copy the requested mon


            //Now choose your mon
            special(ChoosePartyMon)                 //choose your mon
            waitstate                               //wait
            copyvar(VAR_0x800A, VAR_0x8004)         //idk why but copy player selected mon

            //Decline if nothing chosen
            if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN){
               msgbox(format("If you have one, will you trade it for my WIMPOD?"))
               releaseall
               end          
            }

            copyvar(VAR_0x8005, VAR_0x800A)         //idk why but copy the trade mon
            specialvar(VAR_RESULT, GetTradeSpecies)
            copyvar(VAR_0x800B, VAR_RESULT)         //idk why but copy the var result
            if (var(VAR_RESULT) == VAR_0x8009) {
                //If mon is nymble, carry on
                copyvar(VAR_0x8004, VAR_0x8008)
                copyvar(VAR_0x8005, VAR_0x800A)
                special(CreateInGameTradePokemon)
                special(DoInGameTradeScene)
                waitstate
                msgbox(format("Thank you! Please take good care of my WIMPOD."))
                setflag(FLAG_CERAMBASECAMP_NPC_TRADE)      

            } else {
                //else (mon isn't nymble)
                msgbox(format("Hey, this isn't a NYMBLE! I'm not stupid, you know?"))
                releaseall
                end
            }
           


        } else {
            //no trade
            msgbox(format("Oh...well, if you change your mind, you know where I am!"))
        }
    } else {
        //When Team Somber is there
        msgbox(format("I can't wait for ARIAN to put these crooks in their place!"))
    }

    releaseall
}


	// lock
	// faceplayer
	// goto_if_set FLAG_BATTLE_FRONTIER_TRADE_DONE, BattleFrontier_Lounge6_EventScript_TradeCompleted
	// setvar VAR_0x8008, INGAME_TRADE_MEOWTH
	// copyvar VAR_0x8004, VAR_0x8008
	// specialvar VAR_RESULT, GetInGameTradeSpeciesInfo
	// copyvar VAR_0x8009, VAR_RESULT
	// msgbox BattleFrontier_Lounge6_Text_WouldYouLikeToTrade, MSGBOX_YESNO
	// goto_if_eq VAR_RESULT, NO, BattleFrontier_Lounge6_EventScript_DeclineTrade
	// special ChoosePartyMon
	// waitstate
	// copyvar VAR_0x800A, VAR_0x8004
	// goto_if_eq VAR_0x8004, PARTY_NOTHING_CHOSEN, BattleFrontier_Lounge6_EventScript_DeclineTrade
	// copyvar VAR_0x8005, VAR_0x800A
	// specialvar VAR_RESULT, GetTradeSpecies
	// copyvar VAR_0x800B, VAR_RESULT
	// goto_if_ne VAR_RESULT, VAR_0x8009, BattleFrontier_Lounge6_EventScript_NotRequestedMon
	// copyvar VAR_0x8004, VAR_0x8008
	// copyvar VAR_0x8005, VAR_0x800A
	// special CreateInGameTradePokemon
	// special DoInGameTradeScene
	// waitstate
	// msgbox BattleFrontier_Lounge6_Text_PromiseIllBeGoodToIt, MSGBOX_DEFAULT
	// setflag FLAG_BATTLE_FRONTIER_TRADE_DONE
	// release
	// end

script CeramBaseCamp_Main_NPC6{
    lockall
    faceplayer

    if (flag(FLAG_CERAMBASECAMP_EVIL_BATTLE)){
        //Regular case
        msgbox(format("On the other side of Mt. Ceram is Goldensun City. I hear the trek down from the summit is tough, because the native CENTISKORCH and SCOLIPEDE fight each other."))
    } else {
        //When Team Somber is there
        msgbox(format("I can't wait for ARIAN to put these crooks in their place!"))
    }

    releaseall
}



