raw `
    .set LOCALID_GRUNT1, 12
    .set LOCALID_GRUNT2, 13
    .set LOCALID_DEWPIDER, 21

`

mapscripts Route4_MapScripts {}

//NURSE
script Route4_NurseJoy_OnRoute{
    lockall
    faceplayer
    msgbox(format("Can you believe it? They stationed us on these rocks. I'm a nurse for crying out loud!\pSigh..."))
    call(Common_EventScript_NurseJoyOnRoute)
    delay(16)
    releaseall
    end
}



script Route4_EventScript_EvilTeam{
    //Block text
    lockall
    msgbox(format("Hey kid - this road is closed due to...uh...repairs. Yeah. Sorry."))
    release
    delay(8)
    msgbox(format("You're saying we look suspicious? We're just...um...construction workers. Sorry. Yeah."))
    msgbox(format("So you'll step back and let us do our work, right?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == 1){
        msgbox(format("Good. Now scram!"))
        releaseall
        end
    }

    msgbox(format("You don't want to budge? Grr...alright fine, we're not construction workers. We're Team Somber! And now we're going to teach you a lesson!"))
    release

    //Trainer double battle
    trainerbattle_no_intro(
        TRAINER_ROUTE4_GRUNTS, 
        format("Stupid kid!")    
    )

    goto(Route4_EventScript_EvilTeamDefeated)
    releaseall
}

script Route4_EventScript_EvilTeamDefeated{
    //Final dialogue
    applymovement(LOCALID_GRUNT1, moves(face_right))
    applymovement(LOCALID_GRUNT2, moves(face_left)) 
    waitmovement(0)
    applymovement(LOCALID_GRUNT1, moves(jump_in_place_right))
    waitmovement(0)
    msgbox(format("Can't believe we lost to some stupid kid..."))
    release
    applymovement(LOCALID_GRUNT2, moves(jump_in_place_left))
    waitmovement(0)
    msgbox(format("We gotta tell the boss at Ceram Base Camp about this!"))
    release
    applymovement(LOCALID_GRUNT1, moves(face_down))
    applymovement(LOCALID_GRUNT2, moves(face_down)) 
    waitmovement(0)  
    msgbox(format("We'll remember this, kid!"))
    release
    
    //End the sequence
    fadescreen(FADE_TO_BLACK)
    removeobject(LOCALID_GRUNT1)
    removeobject(LOCALID_GRUNT2)
    setflag(FLAG_ROUTE4_EVIL_BATTLE)
    fadescreen(FADE_FROM_BLACK)
    releaseall
}






script Route4_TrainerBattle_Raha {
    trainerbattle_single(TRAINER_ROUTE4_RAHA, format("My Pokémon were very expensive! There's no way you can beat me!"), format("But Father spent so much money!"))
    msgbox(format("Maybe I should ask Father to buy some stronger Pokémon...or maybe I should try and train them myself, like you!"))
    release
    end
}

script Route4_TrainerBattle_Cammy {
    trainerbattle_single(TRAINER_ROUTE4_CAMMY, format("Are you a new trainer? Let this old lady teach you some new tricks."), format("Well fought!"))
    msgbox(format("You were the teacher and I was the student! Haha!"))
    release
    end
}

script Route4_TrainerBattle_Sona {
    trainerbattle_single(TRAINER_ROUTE4_SONA, format("Different Pokémon inhabit different areas. I caught mine right here!"), format("Oh no!"))
    msgbox(format("No matter where they're from, all Pokémon need love and care."))
    release
    end
}

script Route4_TrainerBattle_Jenny {
    trainerbattle_single(TRAINER_ROUTE4_JENNY, format("My PANCHAM and I train here every morning. We want to become the strongest!"), format("We need to train harder!"))
    msgbox(format("Pancham and I will become Pokémon Champions one day!"))
    release
    end
}

    //The Kids Trio
    script Route4_TrainerBattle_Kid1 {
        trainerbattle_single(TRAINER_ROUTE4_KID1, format("Hah! Bet you didn't see me there, did you? You entered our secret hideout and now you're going to pay!"), format("Waah!"))

        if (flag(FLAG_ROUTE4_DEWPIDER_ATTACK)){
            msgbox(format("Eek! It's the Dewpider!"))
        } else {
            msgbox(format("It's summer break so we play here all day until the sun goes down!"))
        }
        release
        end
    }

    script Route4_TrainerBattle_Kid2 {
        trainerbattle_single(TRAINER_ROUTE4_KID2, format("Ooh, you made it this far! But I won't let you lay your hands on the boss! I'm going to stop you right now!"), format("Boss! Help!"))

        if (flag(FLAG_ROUTE4_DEWPIDER_ATTACK)){
            msgbox(format("Someone help the boss! The Dewpider is attacking him!"))
        } else {
            msgbox(format("Our secret hideout is really cool, don't you think?"))
        }       
        release
        end
    }

    script Route4_TrainerBattle_Kid3 {
        lockall
        faceplayer

        if (flag(FLAG_ROUTE4_KIDGANG)){
            msgbox(format("I have so much fun with my friends here...I hate going back home. I hate my family!"))
        } else {
            msgbox(format("Ahem...welcome to our secret hideout, old one. You may look down upon us for being so small, but let me tell you something...Even us kids are strong!"))
            trainerbattle_no_intro(TRAINER_ROUTE4_KID3, format("My Pokémon! Adults are lame!"))

            msgbox(format("You have proved your worth, old one. You may now accept this reward."))
            giveitem(ITEM_POKE_DOLL) 
            setflag(FLAG_ROUTE4_KIDGANG)
            msgbox(format("...\p...when will I go back home?\pNever! I hate my family! They only care about my big brother. I'm not scared of any Dewpiders. I want...I want to be outside all day!"))  
        }
        releaseall
        end
    }

    //Kids trio sign
    script Route4_Sign_KidsTrio{
        msgbox(format("A child has scrawled on this sign.\pTHIS IS OUR SECRET BASE. GET OUT OR BEWARE!!!!"), MSGBOX_SIGN)
        end
    }


    //Dewpider trigger
    script Route4_Dewpider_Trigger{
        if (flag(FLAG_ROUTE4_DEWPIDER_ATTACK)) {end}
        if (flag(FLAG_ROUTE4_KIDGANG)) {
            lockall
            playmoncry(SPECIES_DEWPIDER, CRY_MODE_NORMAL)
            waitmoncry
            msgbox(format("Aaahhh!! Help!!!!"))
            release

            setobjectxyperm(LOCALID_DEWPIDER, 9, 23)
            setflag(FLAG_ROUTE4_DEWPIDER_ATTACK)
            clearflag(FLAG_HIDE_ROUTE4_DEWPIDER)
            releaseall
            end
        } else {
            end
        }
    }

    script Route4_Dewpider{
        lockall
        faceplayer
        playmoncry(SPECIES_DEWPIDER, CRY_MODE_NORMAL)
        delay(16)
        msgbox(format("It's the Dewpider! It snaps at you and attacks!"))
        release        

        setwildbattle(SPECIES_DEWPIDER, 13)
        dowildbattle

        //result
        specialvar(VAR_RESULT, GetBattleOutcome)
        if (var(VAR_RESULT) == B_OUTCOME_WON){
            removeobject(LOCALID_DEWPIDER)
            setflag(FLAG_ROUTE4_DEWPIDER_FINISHED)
            setflag(FLAG_HIDE_ROUTE4_DEWPIDER)
            goto(Route4_Dewpider_Defeated)
        }
        if (var(VAR_RESULT) == B_OUTCOME_CAUGHT){
            removeobject(LOCALID_DEWPIDER)            
            setflag(FLAG_ROUTE4_DEWPIDER_FINISHED)
            setflag(FLAG_HIDE_ROUTE4_DEWPIDER)            
            goto(Route4_Dewpider_Defeated)            
        }            
        if (var(VAR_RESULT) == B_OUTCOME_RAN){
            msgbox(format("The Dewpider snaps at you before turning around."))
            release
            delay(12)
            applymovement(22, moves(face_up))
            delay(12)
            msgbox(format("Will the kids just be left like this?"))
            release
        }
    }

    script Route4_Dewpider_Defeated{
        lockall
        removeobject(LOCALID_DEWPIDER)
        applymovement(1, moves(emote_exclamation_mark jump_in_place_down))
        waitmovement(0)
        delay(12)
        msgbox(format("You saved us! Thank you. We were so scared!\p...\pI'll go home. I can't let my friends be hurt by the Dewpider."))
        release
        applymovement(1, moves(walk_down jump_in_place_down emote_exclamation_mark))
        waitmovement(0)        
        delay(12)
        msgbox(format("C'mon gang! Let's go."))
        release

        fadescreen(FADE_TO_BLACK)
        removeobject(1)
        removeobject(2)
        removeobject(3)
        fadescreen(FADE_FROM_BLACK)
    }


//NPCs
script Route4_NPC_Couple1{
    msgbox(format("What a lovely view!\pThe lake is nice too..."), MSGBOX_NPC)
    end
}

script Route4_NPC_Couple2{
    msgbox(format("What a lovely view!\pIt's a shame about her..."), MSGBOX_NPC)
    end
}

//Generic NPC
script Route4_NPC_A{
    msgbox(format("I don't understand why they don't just build a bridge across to Ceram Base Camp. Sure, trainers can cross with HM SPLASH, but the city management needs to think about us civilians too. We're not all trainers!"), MSGBOX_NPC)
    end
}

//Warns the player of the kids
script Route4_NPC_B{
    if (flag(FLAG_ROUTE4_DEWPIDER_FINISHED)){
        msgbox(format("My brother's gone home now. Finally!"), MSGBOX_NPC)
    } else {
        if (flag(FLAG_ROUTE4_DEWPIDER_ATTACK)){
            msgbox(format("What was that cry? It sounded like my stupid brother. Hah! He's only getting what he deserves."), MSGBOX_NPC)
        } else {
            msgbox(format("Mom told me to come here and get my little brother. He's such a brat! Everyone knows its dangerous to play here because of the Dewpider. But he just runs out and comes here anyway!"), MSGBOX_NPC)
        }
    }

    end
}

//Give item
script Route4_NPC_Cook{

    if (flag(FLAG_ROUTE4_COOK)){
        speakername("Chef")
        msgbox(format("Maybe I'll see you somewhere else, since I travel all over Cormoria. And if you ever visit Pelluca City, be sure to drop by our restaurant!"), MSGBOX_NPC)        
    } else {
        lockall
        faceplayer
        speakername("Chef")
        msgbox(format("Hello young trainer! I'm a chef from the Pelluca Restaurant, all the way on the other side of Cormoria! Why am I here, you ask? It's because I travel all across Cormoria to find the best ingredients for the restaurant! I'm looking for a special berry that only grows around Mt. Ceram. It's called a Rawst Berry. If you happen to have one, would you give it to me?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            checkitem(ITEM_RAWST_BERRY)
            if (var(VAR_RESULT) == 1){
                removeitem(ITEM_RAWST_BERRY)
                speakername(NULL)
                msgbox(format("{PLAYER} gave the chef a Rawst Berry."), MSGBOX_SIGN)
                speakername("Chef")
                msgbox(format("Oh wonderful! This will add a ton of flavour to our signature apple pie!\nThank you trainer. What's your name?\p...\p{PLAYER}, is it? In exchange for the berry, I have a couple of trinkets you may like."))

                goto(Route4_NPC_Cook_Reward)

            } else {
                speakername("Chef")
                msgbox(format("I don't see any Rawst Berries on you. Come back when you get one!"), MSGBOX_NPC)
            }
        } else {
            speakername("Chef")
            msgbox(format("A shame! But no matter - I'll continue my search."), MSGBOX_NPC)
        }
    }


    releaseall
    end
}

script Route4_NPC_Cook_Reward{
    speakername("Chef")
    message(format("Please take one!"))
    waitmessage
    dynmultichoice(0,0, TRUE, 4, 0, DYN_MULTICHOICE_CB_NONE, "Shell Bell", "Adrenaline Orb")
    if (var(VAR_RESULT) == 0){
        goto(Route4_NPC_Cook_RewardA)
    } else {
        goto(Route4_NPC_Cook_RewardB)                
    }
}

script Route4_NPC_Cook_RewardA{
    setvar(VAR_0x8006, ITEM_SHELL_BELL)
    showitemdescription
    speakername("Chef")
    msgbox(format("Are you sure?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == 1){
        hideitemdescription
        giveitem(ITEM_SHELL_BELL)
        goto(Route4_NPC_Cook_RewardEnd)
    } else {
        hideitemdescription
        goto(Route4_NPC_Cook_Reward)
    }
}

script Route4_NPC_Cook_RewardB{
    setvar(VAR_0x8006, ITEM_ADRENALINE_ORB)
    showitemdescription
    speakername("Chef")
    msgbox(format("Are you sure?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == 1){
        hideitemdescription
        giveitem(ITEM_ADRENALINE_ORB)
        goto(Route4_NPC_Cook_RewardEnd)
    } else {
        hideitemdescription
        goto(Route4_NPC_Cook_Reward)
    }
}

script Route4_NPC_Cook_RewardEnd{
    setflag(FLAG_ROUTE4_COOK)
    speakername("Chef")
    msgbox(format("Maybe I'll see you somewhere else, since I travel all over Cormoria. And if you ever visit Pelluca City, be sure to drop by our restaurant!"), MSGBOX_NPC)            
    end
}

//Generic NPC
script Route4_NPC_D{
    msgbox(format("There are some grunts blocking the path to Ceram Base Camp! They remind me of my younger days...I used to be in an evil team in the Hoenn region."), MSGBOX_NPC)
    end
}



