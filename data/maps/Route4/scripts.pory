mapscripts Route4_MapScripts {}
raw `
    .set LOCALID_GRUNT1, 12
    .set LOCALID_GRUNT2, 13

`


script Route4_EventScript_EvilTeam{
    //Block text
    lockall
    msgbox(format("Hey kid - this road is closed due to...uh...repairs. Yeah. Sorry."))
    msgbox(format("We look suspicious? We're just...um...construction workers. Sorry. Yeah."))
    msgbox(format("So you'll step back and let us do our work, right?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == 1){
        msgbox(format("Good. Now scram!"))
        releaseall
        end
    }

    msgbox(format("You don't want to budge? Grr...alright fine, we're not construction workers. We're Team Somber! And now we're going to teach you a lesson!"))
    release

    //Trainer double battle
    trainerbattle_no_intro(
        TRAINER_ROUTE4_GRUNTS, 
        format("Stupid kid!")    
    )

    goto(Route4_EventScript_EvilTeamDefeated)
    releaseall
}

script Route4_EventScript_EvilTeamDefeated{
    //Final dialogue
    applymovement(LOCALID_GRUNT1, moves(face_right))
    applymovement(LOCALID_GRUNT2, moves(face_left)) 
    waitmovement(0)
    applymovement(LOCALID_GRUNT1, moves(jump_in_place_right))
    waitmovement(0)
    msgbox(format("Can't believe we lost to some stupid kid..."))
    release
    applymovement(LOCALID_GRUNT2, moves(jump_in_place_left))
    waitmovement(0)
    msgbox(format("We gotta tell the boss at CERAM BASE CAMP about this!"))
    release
    applymovement(LOCALID_GRUNT1, moves(face_down))
    applymovement(LOCALID_GRUNT2, moves(face_down)) 
    waitmovement(0)  
    msgbox(format("We'll remember this, kid!"))
    release
    
    //End the sequence
    fadescreen(FADE_TO_BLACK)
    removeobject(LOCALID_GRUNT1)
    removeobject(LOCALID_GRUNT2)
    setflag(FLAG_ROUTE4_EVIL_BATTLE)
    fadescreen(FADE_FROM_BLACK)
    releaseall
}






script Route4_TrainerBattle_Raha {
    trainerbattle_single(TRAINER_ROUTE4_RAHA, format("My {PKMN} were very expensive! There's no way you can beat me!"), format("But Father spent so much money!"))
    msgbox(format("Maybe I should ask Father to buy some stronger {PKMN}...or maybe I should try and train them myself, like you!"))
    release
    end
}

script Route4_TrainerBattle_Cammy {
    trainerbattle_single(TRAINER_ROUTE4_CAMMY, format("Are you a new trainer? Let this old lady teach you some new tricks."), format("Well fought!"))
    msgbox(format("You were the teacher and I was the student! Haha!"))
    release
    end
}

script Route4_TrainerBattle_Sona {
    trainerbattle_single(TRAINER_ROUTE4_SONA, format("Different {PKMN} inhabit different areas. I caught mine right here!"), format("Oh no!"))
    msgbox(format("No matter where they're from, all {PKMN} need love and care."))
    release
    end
}

script Route4_TrainerBattle_Jenny {
    trainerbattle_single(TRAINER_ROUTE4_JENNY, format("My PANCHAM and I train here every morning. We want to become the strongest!"), format("We need to train harder!"))
    msgbox(format("Pancham and I will become {PKMN} Champions one day!"))
    release
    end
}

    //The Kids Trio
    script Route4_TrainerBattle_Kid1 {
        trainerbattle_single(TRAINER_ROUTE4_KID1, format("Hah! Bet you didn't see me there, did you? You entered our secret hideout and now you're going to pay!"), format("Waah!"))

        if (flag(FLAG_ROUTE4_DEWPIDER_ATTACK)){
            msgbox(format("Eek! It's the DEWPIDER!"))
        } else {
            msgbox(format("It's summer break so we play here all day until the sun goes down!"))
        }
        release
        end
    }

    script Route4_TrainerBattle_Kid2 {
        trainerbattle_single(TRAINER_ROUTE4_KID2, format("Ooh, you made it this far! But I won't let you lay your hands on the boss! I'm going to stop you right now!"), format("Boss! Help!"))

        if (flag(FLAG_ROUTE4_DEWPIDER_ATTACK)){
            msgbox(format("Someone help the boss! The DEWPIDER is attacking him!"))
        } else {
            msgbox(format("Our secret hideout is really cool, don't you think?"))
        }       
        release
        end
    }

    script Route4_TrainerBattle_Kid3 {
        lockall
        faceplayer

        if (flag(FLAG_ROUTE4_KIDGANG)){
            msgbox(format("I have so much fun with my friends here...I hate going back home. I hate my family!"))
        } else {
            msgbox(format("Ahem...welcome to our secret hideout, old one. You may look down upon us for being so small, but let me tell you something...Even us kids are strong!"))
            trainerbattle_no_intro(TRAINER_ROUTE4_KID3, format("My {PKMN}! Adults are lame!"))

            msgbox(format("You have proved your worth, old one. You may now accept this reward."))
            giveitem(ITEM_POKE_DOLL) 
            setflag(FLAG_ROUTE4_KIDGANG)
            msgbox(format("...\p...when will I go back home?\pNever! I hate my family! They only care about my big brother. I'm not scared of any DEWPIDERS. I want...I want to be outside all day!"))  
        }
        releaseall
        end
    }

    //Kids trio sign
    script Route4_Sign_KidsTrio{
        msgbox(format("A child has scrawled on this sign.\pTHIS IS OUR SECRET BASE. GET OUT OR BEWARE!!!!"), MSGBOX_SIGN)
        end
    }


    //Dewpider trigger
    script Route4_Dewpider_Trigger{
        if (flag(FLAG_ROUTE4_DEWPIDER_ATTACK)) {end}
        if (flag(FLAG_ROUTE4_KIDGANG)) {
            lockall
            msgbox(format("Aaahhh!! Help!!!!"))
            release

            setobjectxyperm(22, 9, 23)
            setflag(FLAG_ROUTE4_DEWPIDER_ATTACK)
            releaseall
            end
        } else {
            end
        }
    }

    script Route4_Dewpider{
        lockall
        faceplayer
        playmoncry(SPECIES_DEWPIDER, CRY_MODE_NORMAL)
        delay(16)
        msgbox(format("It's the DEWPIDER!"))
        release        

        setwildbattle(SPECIES_DEWPIDER, 15)
        dowildbattle

        //result
        specialvar(VAR_RESULT, GetBattleOutcome)
        if (var(VAR_RESULT) == B_OUTCOME_WON){
            removeobject(22)
            setflag(FLAG_ROUTE4_DEWPIDER_FINISHED)
            goto(Route4_Dewpider_Defeated)
        }
        if (var(VAR_RESULT) == B_OUTCOME_CAUGHT){
            removeobject(22)            
            setflag(FLAG_ROUTE4_DEWPIDER_FINISHED)
            goto(Route4_Dewpider_Defeated)            
        }            
        if (var(VAR_RESULT) == B_OUTCOME_RAN){
            msgbox(format("The DEWPIDER snaps at you before turning around."))
            release
            delay(12)
            applymovement(22, moves(face_up))
            delay(12)
            msgbox(format("Will the kids just be left like this?"))
            release
        }
    }

    script Route4_Dewpider_Defeated{
        lockall
        applymovement(1, moves(emote_exclamation_mark jump_in_place_down))
        waitmovement(0)
        delay(12)
        msgbox(format("You saved us! Thank you. We were so scared!\p...\pI'll go home. I can't let my friends be hurt by the DEWPIDER."))
        release
        applymovement(1, moves(walk_down jump_in_place_down emote_exclamation_mark))
        waitmovement(0)        
        delay(12)
        msgbox(format("C'mon gang! Let's go."))
        release

        fadescreen(FADE_TO_BLACK)
        removeobject(1)
        removeobject(2)
        removeobject(3)
        fadescreen(FADE_FROM_BLACK)
    }


//NPCs
script Route4_NPC_Couple1{
    lockall
    faceplayer
    msgbox(format("What a lovely view!\pThe lake is nice too..."))
    releaseall
    warp(MAP_ROUTE6, 18, 91)
}

script Route4_NPC_Couple2{
    lockall
    faceplayer
    msgbox(format("What a lovely view!\pIt's a shame about her..."))
    releaseall
}

//Generic NPC
script Route4_NPC_A{
    lockall
    faceplayer
    msgbox(format("There's a small tree up ahead blocking the path. A {PKMN} with CUT might be able to cut it down...but the city management needs to think about us civilians. We're not all trainers!"))
    releaseall
}

//Warns the player of the kids
script Route4_NPC_B{
    lockall
    faceplayer

    if (flag(FLAG_ROUTE4_DEWPIDER_FINISHED)){
        msgbox(format("My brother's gone home now. Finally!"))
    } else {
        if (flag(FLAG_ROUTE4_DEWPIDER_ATTACK)){
            msgbox(format("What was that cry? It sounded like my stupid brother. Hah! He's only getting what he deserves."))
        } else {
            msgbox(format("Mom told me to come here and get my little brother. He's such a brat! Everyone knows its dangerous to play here because of the DEWPIDER. But he just runs out and comes here anyway!"))
        }
    }

    releaseall
}

//Give item
script Route4_NPC_Cook{
    lockall
    faceplayer

    if (flag(FLAG_ROUTE4_COOK)){
        msgbox(format("Maybe I'll see you somewhere else, since I travel all over CORMORIA. And if you ever visit PELLUCA CITY, be sure to drop by our restaurant!"))        
    } else {
        msgbox(format("Hello young trainer! I'm a cook from the PELLUCA RESTAURANT, all the way on the other side of CORMORIA! Why am I here, you ask? It's because I travel all across CORMORIA to find the best ingredients for the restaurant! I'm looking for a special berry that only grows around MT CERAM. It's called a RAWST BERRY. If you happen to have one, would you give it to me?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            checkitem(ITEM_RAWST_BERRY)
            if (var(VAR_RESULT) == 1){
                removeitem(ITEM_RAWST_BERRY)
                msgbox(format("{PLAYER} gave the cook a RAWST BERRY"), MSGBOX_SIGN)
                msgbox(format("Oh wonderful! This will add a ton of flavour to our signature APPLE PIE!\nThank you trainer. What's your name?\p...\p{PLAYER}, is it? In exchange, please take this."))
                release
                giveitem(ITEM_SHELL_BELL)
                msgbox(format("Maybe I'll see you somewhere else, since I travel all over CORMORIA. And if you ever visit PELLUCA CITY, be sure to drop by our restaurant!"))            
                setflag(FLAG_ROUTE4_COOK)
            }
        }
    }


    releaseall
}

//Generic NPC
script Route4_NPC_D{
    lockall
    faceplayer
    msgbox(format("There are some grunts blocking the path to CERAM BASE CAMP! They remind me of my younger days...I used to be in an evil team in the HOENN region."))
    releaseall
}



