mapscripts SilversunCity_Theater_MapScripts{}








//LEADER
script SilversunCity_Theater_Leader{
    lockall
    faceplayer

    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("GLORIA: Such art! Such expression!\p...\pGLORIA: Now be a dear and get offstage, {PLAYER}. The audience doesn't seem to be too fond of you."))
        releaseall
        end
    }

    //dialogue
    msgbox(format("GLORIA: Hmm, so it's you. Not spectacular, but you'll do.")) 
    fadescreen(FADE_TO_BLACK)    
    setobjectxy(OBJ_EVENT_ID_PLAYER, 3, 6)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    applymovement(8, moves(face_down))
    delay(8)

    special(SpawnCameraObject)
    setobjectxy(OBJ_EVENT_ID_CAMERA, 5, 6)
    waitmovement(0)
    special(RemoveCameraObject)

    fadescreen(FADE_FROM_BLACK)    
    msgbox(format("Welcome to the Gloria Show! Tonight, we're doing a live performance that will sweep you off your feet!"))
    release
    playse(SE_APPLAUSE)
    msgbox(format("AUDIENCE: Woohoo! Go Gloria!"))
    release
    applymovement(8, moves(face_left))   
    msgbox(format("GLORIA: You're a trainer, correct? Then you must be after my Gym Badge. If you want it, you'll have to earn it, right here on stage! The lights, the sound! The audience, the emotions! I specialise in the art of dark drama, {PLAYER}. I live to surprise my audience and shock them. And tonight, your defeat will be my next performance!")) 
    //Move player and leader
    fadescreen(FADE_TO_BLACK)

    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
    applymovement(8, moves(face_left))
    waitmovement(0)
    hidefollower
    fadescreen(FADE_FROM_BLACK)

    //Talk
    msgbox(format("GLORIA: Now, {PLAYER}, enthrall me with your danse macabre!"))
    release

    //Battle
    trainerbattle_no_intro(TRAINER_SILVERSUNGYM_LEADER, "What a performace!")


    //Camera, need to sort this bit properly
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 3 face_down))
    waitmovement(0)
    applymovement(8, moves(face_down))
    waitmovement(0)
    special(RemoveCameraObject)

    msgbox(format("GLORIA: A cheer from the audience, please!\pAUDIENCE: Boo! How dare you beat Gloria, you horrible trainer! Boo!\p{PLAYER} ducked to avoid a tomato!"))
    release
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
    applymovement(8, moves(face_left))
    waitmovement(0)    
    msgbox(format("GLORIA: Ah...don't mind, {PLAYER}. Show business can be tough. But what a battle! Such elegance! Such style and panache! {PLAYER}, were you not a trainer, I would recruit you right away! But you dance to a different tune. And I must reward you with this."))

    //Play gym badge fanfare
    call(Common_EventScript_PlayGymBadgeFanfare)
    //Receive the badge
    msgbox(format("{PLAYER} received the Drama Badge from Gloria."))
    release
    setflag(FLAG_BADGE04_GET)

    //Describe the badge features (use HMs, allow pokemon control etc.)
    msgbox(format("The Drama Badge..."))

    //Give item also

    //Bye-bye
    msgbox(format("GLORIA: It was a pleasure sharing the stage with you. May you weave and bob with elegance in the dance of life, {PLAYER}!"))   
    release

    fadescreen(FADE_TO_BLACK)
    special(SpawnCameraObject)
    setobjectxy(OBJ_EVENT_ID_CAMERA, 5, 6)
    delay(16)
    special(RemoveCameraObject)
    fadescreen(FADE_FROM_BLACK)
    releaseall
    end
}








//BLOCKERS
script SilversunCity_Theater_EventScript_SomberBlocker{
    lockall
    faceplayer
    msgbox(format("GABRIELLE: Hey, nobody's allowed back here. Scram!\p...oh, it's you. I can guess what you're here for.\p...\p...\pGo on, get inside then. I won't stop you. With this, we're square, hear me?\pOh, I'm going to get into trouble...but I always pay back my debts!\pI really hope the grunts inside kick you right back out!"))

    setflag(FLAG_SILVERSUN_SOMBERHQ_ENTRANCE)

    applymovement(6, moves(walk_up))
    waitmovement(0)
    removeobject(6)
    releaseall
    end
}








//NPCs
//Old man 1st row
script SilversunCity_Theater_NPC1{
    lockall
    faceplayer
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("GLORIA is graceful even in defeat. Now that's something everyone can learn from."))
    } else {
        msgbox(format("GLORIA is the face of Silversun City! A Gym Leader AND a famous actress? She's really a role model for women."))
    }
    releaseall
    end
}
//old woman 1st row
script SilversunCity_Theater_NPC2{
    lockall
    faceplayer
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("Incredible! Her SEVIPER and ABSOL! Such a shame they lost, and to such a...nondescript opponent."))
    } else {
        msgbox(format("They say GLORIA caught her SEVIPER and ABSOL while shooting on set in the HOENN region."))
    }
    releaseall
    end
}
//girl breeder left
script SilversunCity_Theater_NPC3{
    lockall
    faceplayer
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("What a show!"))
    } else {
        msgbox(format("I wish Pokemon were allowed in. I would've brought my SPRIGATITO along to watch."))
    }
    releaseall
    end
}
//girl red right
script SilversunCity_Theater_NPC4{
    lockall
    faceplayer
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("Did you see GLORIA's SEVIPER! Too bad that other person defeated it. A Greek tragedy!\n...oh, it was you."))
    } else {
        msgbox(format("I'm a huuuge GLORIA fan! I've been waiting for this show for months!"))
    }
    releaseall
    end
}
//dude in the back
script SilversunCity_Theater_NPC5{
    lockall
    faceplayer
    if (flag(FLAG_BADGE04_GET)){
        if (flag(FLAG_SILVERSUN_THEATER_LENS)){
            msgbox(format("Even with my ZOOM LENS I couldn't see well...what a bummer!"))
        } else {
            msgbox(format("Even with my ZOOM LENS I couldn't see well...what a bummer! Why don't you take them? I'm going to get a new pair anyway."))
            setflag(FLAG_SILVERSUN_THEATER_LENS)
            giveitem(ITEM_ZOOM_LENS)
        }
    } else {
        if (flag(FLAG_SILVERSUN_THEATER_LENS)){
            msgbox(format("Ahh, now let's settle in and watch the show."))
        }
        msgbox(format("Are you going to watch the show from all the way back here too?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
           msgbox(format("Then these should help. I've got an extra pair."))            
            setflag(FLAG_SILVERSUN_THEATER_LENS)
            giveitem(ITEM_ZOOM_LENS)
           msgbox(format("You can use it outside of the theater too! Any Pokemon holding it will have their accuracy increased."))             
        }

    }
    releaseall
    end
}


//backstage actress
script SilversunCity_Theater_NPC6{
    lockall
    faceplayer
    if (flag(FLAG_SILVERSUN_THEATER_LENS)){
        msgbox(format("Right now I'm only an extra, but I'm about to land my first starring role any day now!"))
    } else {
        msgbox(format("Who are you? Only cast and crew are allowed backstage.\pAre you acting tonight?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
         msgbox(format("Ooh! Are you going to go on-stage next to Gloria?"), MSGBOX_YESNO)           
            if (var (VAR_RESULT) == 1){
                msgbox(format("Oh my god! You're a star! Can I have your autograph?"), MSGBOX_YESNO)
                if (var(VAR_RESULT) == 1){
                    msgbox(format("...Thank you! Please take this. A little something to remember me by!"))
                    giveitem(ITEM_SILK_SCARF)
                    setflag(FLAG_SILVERSUN_THEATER_LENS)
                }
            } else {
                msgbox(format("So you're an extra too? Sigh...when will we get our big break?"))
            }
        } else {
            msgbox(format("Oh, so you're part of the crew. I'm an extra, so we're both playing support roles."))
        }
    }
    releaseall
    end
}
//backstage actor
script SilversunCity_Theater_NPC7{
    lockall
    faceplayer
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("You must have nerves of steel, doing all that in front of a crowd that size!"))
    } else {
        msgbox(format("...breathe...breathe...I'm calming my nerves. Even after years of training, I get nervous before going on-stage."))
    }
    releaseall
    end
}



