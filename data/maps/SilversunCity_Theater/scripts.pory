mapscripts SilversunCity_Theater_MapScripts{}








//LEADER
script SilversunCity_Theater_Leader{
    lockall
    faceplayer

    if (flag(FLAG_BADGE04_GET)){
        speakername("Gloria")
        msgbox(format("Such art! Such expression!\p...\pNow be a dear and get offstage, {PLAYER}. The audience doesn't seem to be too fond of you."))
        releaseall
        end
    }
    
    //Savegame
    special(SaveGame)
    waitstate
    release

    //dialogue
    speakername("Gloria")
    msgbox(format("Hmm, so it's you. Not spectacular, but you'll do.")) 
    fadescreenswapbuffers(FADE_TO_BLACK)    
    setobjectxy(OBJ_EVENT_ID_PLAYER, 3, 6)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    applymovement(8, moves(face_down))
    delay(8)

    special(SpawnCameraObject)
    setobjectxy(OBJ_EVENT_ID_CAMERA, 5, 6)
    waitmovement(0)
    special(RemoveCameraObject)

    fadescreenswapbuffers(FADE_FROM_BLACK)    
    speakername("Gloria")
    msgbox(format("Welcome to the Gloria Show! Tonight, we're doing a live performance that will sweep you off your feet!"))
    release
    playse(SE_APPLAUSE)
    speakername("Audience")
    msgbox(format("Woohoo! Go Gloria!"))
    release
    applymovement(8, moves(face_left)) 
    speakername("Gloria")  
    msgbox(format("You're a trainer, correct? Then you must be after my Gym Badge. If you want it, you'll have to earn it, right here on stage! The lights, the sound! The audience, the emotions! I specialise in the art of dark drama, {PLAYER}. I live to surprise my audience and shock them. And tonight, your defeat will be my next performance!")) 
    //Move player and leader
    fadescreenswapbuffers(FADE_TO_BLACK)

    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
    applymovement(8, moves(face_left))
    waitmovement(0)
    hidefollower
    fadescreenswapbuffers(FADE_FROM_BLACK)

    //Talk
    speakername("Gloria")
    msgbox(format("Now, {PLAYER}, enthrall me with your danse macabre!"))
    release

    //Battle
    trainerbattle_no_intro(TRAINER_SILVERSUNGYM_LEADER, "What a performace!")


    //Camera, need to sort this bit properly
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 3 face_down))
    waitmovement(0)
    applymovement(8, moves(face_down))
    waitmovement(0)
    special(RemoveCameraObject)

    speakername("Gloria")
    msgbox(format("A cheer from the audience, please!"))
    speakername("Audience")
    msgbox(format("Boo! How dare you beat Gloria, you horrible trainer! Boo!"))
    msgbox(format("{PLAYER} ducked to avoid a tomato!"))
    release
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
    applymovement(8, moves(face_left))
    waitmovement(0)    
    speakername("Gloria")
    msgbox(format("Ah...don't mind, {PLAYER}. Show business can be tough. But what a battle! Such elegance! Such style and panache! {PLAYER}, were you not a trainer, I would recruit you right away! But you dance to a different tune. And I must reward you with this."))

    //Play gym badge fanfare
    call(Common_EventScript_PlayGymBadgeFanfare)
    //Receive the badge
    speakername("Gloria")
    msgbox(format("{PLAYER} received the Drama Badge from Gloria."))
    release
    setflag(FLAG_BADGE04_GET)

    //Describe the badge features (use HMs, allow pokemon control etc.)
    speakername("Gloria")
    msgbox(format("The Drama Badge will let you use HM Strength outside of battle. And I must give you this beautiful jewel too."))
    //Give item also
    giveitem(ITEM_RARE_SHARD)

    //Bye-bye
    speakername("Gloria")
    msgbox(format("It was a pleasure sharing the stage with you. May you elegantly bob and weave through the dance of life, {PLAYER}!"))   
    release

    fadescreenswapbuffers(FADE_TO_BLACK)
    special(SpawnCameraObject)
    setobjectxy(OBJ_EVENT_ID_CAMERA, 5, 6)
    delay(16)
    special(RemoveCameraObject)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    releaseall
    end
}








//BLOCKERS
script SilversunCity_Theater_EventScript_SomberBlocker{
    lockall
    faceplayer
    speakername("Gabrielle")
    msgbox(format("Hey, nobody's allowed back here. Scram!\p...oh, it's you. Sigh...I can guess what you're here for.\p...\pGo on, get inside then. I won't stop you. With this, we're square, hear me?\pOh, I'm going to get into trouble...but I always pay back my debts!\pI really hope the grunts inside kick you right back out!"))
    release
    
    setflag(FLAG_SILVERSUN_SOMBERHQ_ENTRANCE)

    applymovement(6, moves(walk_up))
    waitmovement(0)
    removeobject(6)
    releaseall
    end
}








//NPCs
//Old man 1st row
script SilversunCity_Theater_NPC1{
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("Gloria is graceful even in defeat. Now that's something everyone can learn from."), MSGBOX_NPC)
    } else {
        msgbox(format("Gloria is the face of Silversun City! A Gym Leader AND a famous actress? She's really a role model for women."), MSGBOX_NPC)
    }
    end
}
//old woman 1st row
script SilversunCity_Theater_NPC2{
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("Incredible! Her Bombirdier! Such a shame she lost, and to such a...nondescript opponent."), MSGBOX_NPC)
    } else {
        msgbox(format("They say Gloria caught her Bombirdier while shooting an adventure flick in the Ivy River."), MSGBOX_NPC)
    }
    end
}
//girl breeder left
script SilversunCity_Theater_NPC3{
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("What a show!"), MSGBOX_NPC)
    } else {
        msgbox(format("I wish Pokémon were allowed in. I would've brought my Budew along to watch...hey! How'd they let your Pokémon in? That's so unfair!"), MSGBOX_NPC)
    }
    end
}
//girl red right
script SilversunCity_Theater_NPC4{
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("Did you see Gloria's Bombirdier! Too bad that other person defeated it. A Greek tragedy!\n...oh, it was you."), MSGBOX_NPC)
    } else {
        msgbox(format("I'm a huuuge Gloria fan! I've been waiting for this show for months!"), MSGBOX_NPC)
    }
    end
}

//dude in the back
script SilversunCity_Theater_NPC5{
    if (flag(FLAG_BADGE04_GET)){
        if (flag(FLAG_SILVERSUN_THEATER_LENS)){
            msgbox(format("To be honest, it's not even my fault. The theater should arrange better seating, especially if they're charging the same price for all tickets!"), MSGBOX_NPC)
        } else {
            msgbox(format("I'm glad I brought lenses because the performance was great!"))
            goto(SilversunCity_Theater_Lens_Reward)
        }
    } else {
        if (flag(FLAG_SILVERSUN_THEATER_LENS)){
            msgbox(format("Ahh, now let's settle in and watch Gloria's performance."), MSGBOX_NPC)
        } else {
            msgbox(format("It's so hard to see what's going on from the back without a pair of lenses. Are you going to watch the show from here too?"), MSGBOX_YESNO)
            if (var(VAR_RESULT) == 1){
                goto(SilversunCity_Theater_Lens_Reward)
            } else {
                msgbox(format("Got better seats? Good for you! I wish I'd booked mine faster."), MSGBOX_NPC)
            }
        }
    }
    end
}

script SilversunCity_Theater_Lens_Reward{
    message(format("Why don't you wear a pair of lenses? I've got extra."))    
    waitmessage        
    dynmultichoice(0,0, TRUE, 2, 0, DYN_MULTICHOICE_CB_NONE, "Wide Lens", "Scope Lens")
    if (var(VAR_RESULT) == 0){
        goto(SilversunCity_Theater_Lens_RewardA)
    } else {
        goto(SilversunCity_Theater_Lens_RewardB)
    }
}
script SilversunCity_Theater_Lens_RewardA{
    setvar(VAR_0x8006, ITEM_WIDE_LENS)
    showitemdescription
    msgbox(format("Are you sure?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == 1){
        hideitemdescription
        giveitem(ITEM_WIDE_LENS)
        goto(SilversunCity_Theater_Lens_RewardEnd)
    } else {
        hideitemdescription
        goto(SilversunCity_Theater_Lens_Reward)
    }
}
script SilversunCity_Theater_Lens_RewardB{
    setvar(VAR_0x8006, ITEM_ZOOM_LENS)
    showitemdescription
    msgbox(format("Are you sure?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == 1){
        hideitemdescription
        giveitem(ITEM_ZOOM_LENS)
        goto(SilversunCity_Theater_Lens_RewardEnd)
    } else {
        hideitemdescription
        goto(SilversunCity_Theater_Lens_Reward)
    }
}
script SilversunCity_Theater_Lens_RewardEnd{
    setflag(FLAG_SILVERSUN_THEATER_LENS)
    msgbox(format("You can use it outside of the theater too!"), MSGBOX_NPC)
    end
}









//backstage actress
script SilversunCity_Theater_NPC6{
    if (flag(FLAG_SILVERSUN_THEATER_SCARF)){
        msgbox(format("Right now I'm only an extra, but I'm about to land my first starring role any day now!"), MSGBOX_NPC)
    } else {
        lockall
        faceplayer
        msgbox(format("Who are you? Only cast and crew are allowed backstage.\pAre you acting tonight?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
         msgbox(format("Ooh! Are you going to go on-stage next to Gloria?"), MSGBOX_YESNO)           
            if (var (VAR_RESULT) == 1){
                msgbox(format("Oh wow! You're a star!\nCan I have your autograph?"), MSGBOX_YESNO)
                if (var(VAR_RESULT) == 1){
                    msgbox(format("...Thank you! Please take this."))
                    giveitem(ITEM_DESTINY_KNOT)
                    setflag(FLAG_SILVERSUN_THEATER_SCARF)
                    msgbox(format("A little something to remember me by!"), MSGBOX_NPC)
                    end
                }
            } else {
                msgbox(format("So you're an extra too? Sigh...when will we get our big break?"), MSGBOX_NPC)
            }
        } else {
            msgbox(format("Oh, you're part of the crew. I'm an extra, so we're both playing support roles.\pSigh...when will we get our big break?"), MSGBOX_NPC)
        }
        releaseall
    }
    end
}
//backstage actor
script SilversunCity_Theater_NPC7{
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("You must have nerves of steel, doing all that in front of a crowd that size!"), MSGBOX_NPC)
    } else {
        msgbox(format("...breathe...breathe...I'm calming my nerves. Even after years of training, I get nervous before going on-stage."), MSGBOX_NPC)
    }
    end
}



