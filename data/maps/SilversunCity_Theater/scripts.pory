mapscripts SilversunCity_Theater_MapScripts{}








//LEADER
script SilversunCity_Theater_Leader{
    lockall
    faceplayer

    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("GLORIA: Such art! Such expression!\p...\pGLORIA: Now be a dear and get offstage, {PLAYER}. The audience doesn't seem to be too fond of you."))
        releaseall
        end
    }

    call(Common_EventScript_SaveGame)
    //dialogue
    msgbox(format("GLORIA: Hmm, so it's you. Not spectacular, but you'll do.")) 
    fadescreen(FADE_TO_BLACK)    
    setobjectxy(OBJ_EVENT_ID_PLAYER, 3, 6)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    applymovement(8, moves(face_down))
    delay(8)

    special(SpawnCameraObject)
    setobjectxy(OBJ_EVENT_ID_CAMERA, 5, 6)
    waitmovement(0)
    special(RemoveCameraObject)

    fadescreen(FADE_FROM_BLACK)    
    msgbox(format("Welcome to the Gloria Show! Tonight, we're doing a live performance that will sweep you off your feet!"))
    release
    playse(SE_APPLAUSE)
    msgbox(format("AUDIENCE: Woohoo! Go Gloria!"))
    release
    applymovement(8, moves(face_left))   
    msgbox(format("GLORIA: You're a trainer, correct? Then you must be after my Gym Badge. If you want it, you'll have to earn it, right here on stage! The lights, the sound! The audience, the emotions! I specialise in the art of dark drama, {PLAYER}. I live to surprise my audience and shock them. And tonight, your defeat will be my next performance!")) 
    //Move player and leader
    fadescreen(FADE_TO_BLACK)

    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
    applymovement(8, moves(face_left))
    waitmovement(0)
    hidefollower
    fadescreen(FADE_FROM_BLACK)

    //Talk
    msgbox(format("GLORIA: Now, {PLAYER}, enthrall me with your danse macabre!"))
    release

    //Battle
    trainerbattle_no_intro(TRAINER_SILVERSUNGYM_LEADER, "What a performace!")


    //Camera, need to sort this bit properly
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 3 face_down))
    waitmovement(0)
    applymovement(8, moves(face_down))
    waitmovement(0)
    special(RemoveCameraObject)

    msgbox(format("GLORIA: A cheer from the audience, please!\pAUDIENCE: Boo! How dare you beat Gloria, you horrible trainer! Boo!\p{PLAYER} ducked to avoid a tomato!"))
    release
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
    applymovement(8, moves(face_left))
    waitmovement(0)    
    msgbox(format("GLORIA: Ah...don't mind, {PLAYER}. Show business can be tough. But what a battle! Such elegance! Such style and panache! {PLAYER}, were you not a trainer, I would recruit you right away! But you dance to a different tune. And I must reward you with this."))

    //Play gym badge fanfare
    call(Common_EventScript_PlayGymBadgeFanfare)
    //Receive the badge
    msgbox(format("{PLAYER} received the Drama Badge from Gloria."))
    release
    setflag(FLAG_BADGE04_GET)

    //Describe the badge features (use HMs, allow pokemon control etc.)
    msgbox(format("The Drama Badge..."))

    //Give item also

    //Bye-bye
    msgbox(format("GLORIA: It was a pleasure sharing the stage with you. May you weave and bob with elegance in the dance of life, {PLAYER}!"))   
    release

    fadescreen(FADE_TO_BLACK)
    special(SpawnCameraObject)
    setobjectxy(OBJ_EVENT_ID_CAMERA, 5, 6)
    delay(16)
    special(RemoveCameraObject)
    fadescreen(FADE_FROM_BLACK)
    releaseall
    end
}








//BLOCKERS
script SilversunCity_Theater_EventScript_SomberBlocker{
    lockall
    faceplayer
    msgbox(format("GABRIELLE: Hey, nobody's allowed back here. Scram!\p...oh, it's you. I can guess what you're here for.\p...\p...\pGo on, get inside then. I won't stop you. With this, we're square, hear me?\pOh, I'm going to get into trouble...but I always pay back my debts!\pI really hope the grunts inside kick you right back out!"))

    setflag(FLAG_SILVERSUN_SOMBERHQ_ENTRANCE)

    applymovement(6, moves(walk_up))
    waitmovement(0)
    removeobject(6)
    releaseall
    end
}








//NPCs
//Old man 1st row
script SilversunCity_Theater_NPC1{
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("Gloria is graceful even in defeat. Now that's something everyone can learn from."), MSGBOX_NPC)
    } else {
        msgbox(format("Gloria is the face of Silversun City! A Gym Leader AND a famous actress? She's really a role model for women."), MSGBOX_NPC)
    }
    end
}
//old woman 1st row
script SilversunCity_Theater_NPC2{
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("Incredible! Her Servine and Absol! Such a shame they lost, and to such a...nondescript opponent."), MSGBOX_NPC)
    } else {
        msgbox(format("They say Gloria caught her Seviper and Absol while shooting on set in the HOENN region."), MSGBOX_NPC)
    }
    end
}
//girl breeder left
script SilversunCity_Theater_NPC3{
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("What a show!"), MSGBOX_NPC)
    } else {
        msgbox(format("I wish Pokémon were allowed in. I would've brought my Sprigatito along to watch."), MSGBOX_NPC)
    }
    end
}
//girl red right
script SilversunCity_Theater_NPC4{
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("Did you see Gloria's Servine! Too bad that other person defeated it. A Greek tragedy!\n...oh, it was you."), MSGBOX_NPC)
    } else {
        msgbox(format("I'm a huuuge Gloria fan! I've been waiting for this show for months!"), MSGBOX_NPC)
    }
    end
}

//dude in the back
script SilversunCity_Theater_NPC5{
    if (flag(FLAG_BADGE04_GET)){
        if (flag(FLAG_SILVERSUN_THEATER_LENS)){
            msgbox(format("To be honest, it's not even my fault. The theater should arrange better seating, especially if they're charging the same price for all tickets!"))
        } else {
            msgbox(format("I'm glad I brought lenses because the performance was great! Do you want a pair? I brought an extra."))
            dynmultichoice(0,0, TRUE, 2, 0, DYN_MULTICHOICE_CB_NONE, "WIDE LENS", "SCOPE LENS")
            if (var(VAR_RESULT) == 0){
                giveitem(ITEM_WIDE_LENS)
                msgbox(format("You can use it outside of the theater too! Any Pokémon holding it will have their accuracy increased."), MSGBOX_NPC)
            } else {
                giveitem(ITEM_SCOPE_LENS)
                msgbox(format("You can use it outside of the theater too! Any Pokémon holding it will land more critical hits."), MSGBOX_NPC)
            }
            
            setflag(FLAG_SILVERSUN_THEATER_LENS)
        }
    } else {
        if (flag(FLAG_SILVERSUN_THEATER_LENS)){
            msgbox(format("Ahh, now let's settle in and watch Gloria's performance."))
        } else {
            msgbox(format("It's so hard to see what's going on from the back without a pair of lenses. Are you going to watch the show from here too?"), MSGBOX_YESNO)
            if (var(VAR_RESULT) == 1){
                msgbox(format("Why don't you wear a pair of lenses? I've got extra."))            
                dynmultichoice(0,0, TRUE, 2, 0, DYN_MULTICHOICE_CB_NONE, "WIDE LENS", "SCOPE LENS")
                if (var(VAR_RESULT) == 0){
                    giveitem(ITEM_WIDE_LENS)
                    msgbox(format("You can use it outside of the theater too! Any Pokémon holding it will have their accuracy increased."), MSGBOX_NPC)
                } else {
                    giveitem(ITEM_SCOPE_LENS)
                    msgbox(format("You can use it outside of the theater too! Any Pokémon holding it will land more critical hits."), MSGBOX_NPC)
                }
                
                setflag(FLAG_SILVERSUN_THEATER_LENS)            
            } else {
                msgbox(format("Got better seats? Good for you! I wish I'd booked mine faster."), MSGBOX_NPC)
            }
        }
    }
    end
}


//backstage actress
script SilversunCity_Theater_NPC6{
    if (flag(FLAG_SILVERSUN_THEATER_SCARF)){
        msgbox(format("Right now I'm only an extra, but I'm about to land my first starring role any day now!"), MSGBOX_NPC)
    } else {
        lockall
        msgbox(format("Who are you? Only cast and crew are allowed backstage.\pAre you acting tonight?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
         msgbox(format("Ooh! Are you going to go on-stage next to Gloria?"), MSGBOX_YESNO)           
            if (var (VAR_RESULT) == 1){
                msgbox(format("Oh my god! You're a star! Can I have your autograph?"), MSGBOX_YESNO)
                if (var(VAR_RESULT) == 1){
                    msgbox(format("...Thank you! Please take this. A little something to remember me by!"), MSGBOX_NPC)
                    giveitem(ITEM_SILK_SCARF)
                    release
                    setflag(FLAG_SILVERSUN_THEATER_SCARF)
                }
            } else {
                msgbox(format("So you're an extra too? Sigh...when will we get our big break?"), MSGBOX_NPC)
            }
        } else {
            msgbox(format("Oh, so you're part of the crew. I'm an extra, so we're both playing support roles."), MSGBOX_NPC)
        }
        releaseall
    }
    end
}
//backstage actor
script SilversunCity_Theater_NPC7{
    if (flag(FLAG_BADGE04_GET)){
        msgbox(format("You must have nerves of steel, doing all that in front of a crowd that size!"), MSGBOX_NPC)
    } else {
        msgbox(format("...breathe...breathe...I'm calming my nerves. Even after years of training, I get nervous before going on-stage."), MSGBOX_NPC)
    }
    end
}



