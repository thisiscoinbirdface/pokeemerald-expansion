mapscripts SilversunCity_BlindOrphanage_MapScripts{}


script SilversunCity_BlindOrphanage_PokedexGameStart{
    lockall
    faceplayer

    //Pre-quest
    if (var(VAR_SILVERSUN_ORPHANAGE_MAINQUEST) == 0){
        msgbox(format("I'm looking for a book on all the Pokémon in Cormoria! But I'm too short to touch the braille on the book covers..."), MSGBOX_NPC)
        end
    }
    if (var(VAR_SILVERSUN_ORPHANAGE_MAINQUEST) == 1){
        msgbox(format("I'm looking for a book on all the Pokémon in Cormoria! But I'm too short to touch the braille on the book covers..."), MSGBOX_NPC)
        end
    }    

    if (var(VAR_SILVERSUN_BRAILLE_BOOK) == 4){
        msgbox(format("This book is hard to understand! I wish I could touch all these Pokémon so I know what they feel like. But the teacher says that's dangerous too! What if I touch a Pincurchin or a Corsola?"), MSGBOX_NPC)
        end
    }

    if (var(VAR_SILVERSUN_BRAILLE_BOOK) == 3){
        msgbox(format("Is it you? Did you bring the book?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 0){
            msgbox(format("...oh, okay. That's fine."))

        } else {
            checkitem(ITEM_ORPHANAGE_BOOK)
            if (var(VAR_RESULT) == 1){
                removeitem(ITEM_ORPHANAGE_BOOK)
                msgbox(format("{PLAYER} gave the boy a copy of POKéMON A TO Z."), MSGBOX_SIGN)
                release
                msgbox(format("Yayy! Thank you!"))
                release  
                setvar(VAR_SILVERSUN_BRAILLE_BOOK, 4)

                //If the other activity is done, mark the main quest as complete
                if (flag(FLAG_SILVERSUN_BRAILLE_1_DONE)){
                    setvar(VAR_SILVERSUN_ORPHANAGE_MAINQUEST, 3)
                }
            } else {
                msgbox(format("Hey, you didn't bring a book!"), MSGBOX_NPC)
            }
        }

        releaseall
        end
    }

    if (var(VAR_SILVERSUN_BRAILLE_BOOK) == 2){
        msgbox(format("Is it you? Did you bring the book?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 0){
            msgbox(format("...oh, okay. That's fine."))

        } else {
            checkitem(ITEM_ORPHANAGE_BOOK)
            if (var(VAR_RESULT) == 1){
                removeitem(ITEM_ORPHANAGE_BOOK)
                msgbox(format("{PLAYER} gave the boy a book."), MSGBOX_SIGN)
                release
                msgbox(format("Hey, this isn't it! I want the right book!"))
                release  
                setvar(VAR_SILVERSUN_BRAILLE_BOOK, 1)
            } else {
                msgbox(format("Hey, you didn't bring a book!"), MSGBOX_NPC)
            }
        }

        releaseall
        end
    }

    if (var(VAR_SILVERSUN_BRAILLE_BOOK) == 1){
        msgbox(format("I want a book on all the Pokémon in Cormoria!"), MSGBOX_NPC)
        end
    }

    setvar(VAR_SILVERSUN_BRAILLE_BOOK, 1)
    msgbox(format("I'm looking for a book on all the Pokémon in Cormoria! But I'm too short to touch the braille on the book covers. If you find me one, can you bring it to me?"), MSGBOX_NPC)
    end
}

script SilversunCity_Book1{
    lockall
    braillemessage(Silversun_BrailleBook1)
    waitbuttonpress
    closebraillemessage

    if (var(VAR_SILVERSUN_BRAILLE_BOOK) == 1){
        msgbox(format("Take this book?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            setvar(VAR_SILVERSUN_BRAILLE_BOOK, 2)
            msgbox(format("{PLAYER} took a book from the shelf."))
            additem(ITEM_ORPHANAGE_BOOK)
        }
    }

    releaseall
    end
}

script SilversunCity_Book2{
    lockall
    braillemessage(Silversun_BrailleBook2)
    waitbuttonpress
    closebraillemessage

    if (var(VAR_SILVERSUN_BRAILLE_BOOK) == 1){
        msgbox(format("Take this book?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            setvar(VAR_SILVERSUN_BRAILLE_BOOK, 2)
            msgbox(format("{PLAYER} took a book from the shelf."))
            additem(ITEM_ORPHANAGE_BOOK)
        }
    }

    releaseall
    end
}

script SilversunCity_Book3{
    lockall
    braillemessage(Silversun_BrailleBook3)
    waitbuttonpress
    closebraillemessage

    if (var(VAR_SILVERSUN_BRAILLE_BOOK) == 1){
        msgbox(format("Take this book?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            setvar(VAR_SILVERSUN_BRAILLE_BOOK, 3)
            msgbox(format("{PLAYER} took a book from the shelf."))
            additem(ITEM_ORPHANAGE_BOOK)
        }
    }

    releaseall
    end
}

script SilversunCity_Book4{
    lockall
    braillemessage(Silversun_BrailleBook4)
    waitbuttonpress
    closebraillemessage

    if (var(VAR_SILVERSUN_BRAILLE_BOOK) == 1){
        msgbox(format("Take this book?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            setvar(VAR_SILVERSUN_BRAILLE_BOOK, 2)
            msgbox(format("{PLAYER} took a book from the shelf."))
            additem(ITEM_ORPHANAGE_BOOK)
        }
    }

    releaseall
    end
}


script SilversunCity_BlindOrphanage_BrailleGameStart{
    if (flag(FLAG_SILVERSUN_BRAILLE_1_DONE)){
        speakername("Teacher")
        msgbox(format("Sheena is preparing for her next test."), MSGBOX_NPC)
        end
    }

    lockall
    faceplayer

    if (var(VAR_SILVERSUN_ORPHANAGE_MAINQUEST) == 0){
        speakername("Teacher")
        msgbox(format("I work at the Silversun Theater, but on the weekends, I teach braille to the children here. It's very fulfilling!"))
        release
        end
    }
    if (var(VAR_SILVERSUN_ORPHANAGE_MAINQUEST) == 1){
        speakername("Teacher")
        msgbox(format("I work at the Silversun Theater, but on the weekends, I teach braille to the children here. It's very fulfilling!"))
        release
        end
    }    

    speakername("Teacher")
    msgbox(format("Sheena is about to take a braille test. Would you like to volunteer and help out?"), MSGBOX_YESNO)

    if (var(VAR_RESULT) == 0){
        speakername("Teacher")
        msgbox(format("Okay.\nSheena, let's continue."))
        release
        applymovement(5, moves(face_left))
        waitmovement(0)
        release

        releaseall
        end
    }

    speakername("Teacher")
    msgbox(format("Excellent. Sheena has to read three sentences in braille correctly. I'll give Sheena the questions, and you keep a tally of her answers, okay?"))
    release
    delay(30)
    applymovement(5, moves(face_left))
    speakername("Teacher")
    msgbox(format("Are you ready Sheena?"))
    release
    delay(24)
    applymovement(8, moves(jump_in_place_right))
    delay(24)
    speakername("Sheena")
    msgbox(format("Yes!"))
    release
    delay(16)
    speakername("Teacher")
    msgbox(format("Okay then. Here's the first question!"))
    release

    //Question 1 (boy got it wrong)
    braillemessage(Silversun_Braille_Quiz1)
    waitbuttonpress
    closebraillemessage
    speakername("Sheena")
    msgbox(format("I got it! I got it!\nAN APPLIN A DAY KEEPS THE DOCTOR AT BAY."))
    release
    speakername("Teacher")
    msgbox(format("Next question!"))    
    release


    //Question 2 (boy got it right)
    braillemessage(Silversun_Braille_Quiz2)
    waitbuttonpress
    closebraillemessage
    speakername("Sheena")
    msgbox(format("I got it! I got it!\nROLY POLY WITH ROLYCOLY."))
    release
    speakername("Teacher")
    msgbox(format("Next question!"))    
    release

    //Question 3 (boy got it wrong)
    braillemessage(Silversun_Braille_Quiz3)
    waitbuttonpress
    closebraillemessage
    speakername("Sheena")
    msgbox(format("I got it! I got it!\pFEEBAS EVOLVES WITH A WIND SAIL."))
    release
    delay(16)
    speakername("Teacher")
    msgbox(format("And that's all the questions!"))    
    release
    delay(16)
    faceplayer

    //result (the correct answer is 1/3)
    speakername("Teacher")
    msgbox(format("So how many did SHEEA get right?")) 
    dynmultichoice(0, 0, TRUE, 4, 0, DYN_MULTICHOICE_CB_NONE, "0/3", "1/3", "2/3", "3/3")

    if (var(VAR_RESULT) == 1){
        speakername("Teacher")
        msgbox(format("Yes...looks like Sheena needs more practice.\pThanks for volunteering.\pThank you!"))
        release 

        setflag(FLAG_SILVERSUN_BRAILLE_1_DONE)
        //If the other activity is done, mark the main quest as complete
        if (var(VAR_SILVERSUN_BRAILLE_BOOK) == 4){
            setvar(VAR_SILVERSUN_ORPHANAGE_MAINQUEST, 3)
        }
               
    } else {
        speakername("Teacher")
        msgbox(format("That doesn't seem right. Are you sure you checked her answers correctly?"))
    } 

    releaseall
    end
}



script SilversunCity_BlindOrphanage_NPC_1{
    lockall
    faceplayer
    speakername("Sheena")
    msgbox(format("I'm learning braille!"))
    releaseall
    end
}

script SilversunCity_BlindOrphanage_NPC_2{
    lockall
    faceplayer
    msgbox(format("Sometimes I imagine what colors look like when Ralts sends me warm thoughts. Is yellow warm?"))
    releaseall
    end
}

script SilversunCity_BlindOrphanage_NPC_3{
    lockall
    faceplayer
    msgbox(format("Many of our children may not see the world, but they feel it - especially when a Pokémon is by their side."))
    releaseall
    end
}

script SilversunCity_BlindOrphanage_NPC_5{
    lockall
    faceplayer
    msgbox(format("I gave my Noibat to the orphanage. Now it helps the kids walk around at night."))
    releaseall
    end
}

script SilversunCity_BlindOrphanage_NPC_6{
    lockall
    faceplayer
    msgbox(format("I listen to people's footsteps. Yours sound kind of...bouncy. Are you happy today?"))
    releaseall
    end
}

script SilversunCity_BlindOrphanage_Ralts{
    playmoncry(SPECIES_RALTS, CRY_MODE_NORMAL)
    msgbox(format("Ralts is focused intently on its child...perhaps its sending some warm thoughts across."), MSGBOX_NPC)
    end
}

script SilversunCity_BlindOrphanage_Noibat{
    playmoncry(SPECIES_NOIBAT, CRY_MODE_NORMAL)
    msgbox(format("Noibat looks bored...it must be waiting for night-time!"), MSGBOX_NPC)
    end
}

script SilversunCity_BlindOrphanage_Furfrou{
    playmoncry(SPECIES_FURFROU, CRY_MODE_NORMAL)
    msgbox(format("Furfrou barks! And barks again!"), MSGBOX_NPC)
    end
}















script SilversunCity_BlindOrphanage_OldManQuest{
    lockall
    faceplayer

    if (var(VAR_SILVERSUN_ORPHANAGE_MAINQUEST) == 4){
        msgbox(format("Thanks to the efforts of you and everyone else here, the children are doing well."))
    }

    if (var(VAR_SILVERSUN_ORPHANAGE_MAINQUEST) == 0){
        msgbox(format("Ah, a visitor? You'll have to speak up, my ears aren't what they used to be...\pWelcome to my little dream. This place...it's more than walls and windows. It's where light finds those who've never seen it. They told me it was impossible. Blind children with Pokémon was too dangerous, they said. And yet, look around. There's more trust in these halls than in half the world."))
        setvar(VAR_SILVERSUN_ORPHANAGE_MAINQUEST, 1)
        delay(24)
    }

    if (var(VAR_SILVERSUN_ORPHANAGE_MAINQUEST) == 3){
        msgbox(format("The orphanage is really impressed with your work, {PLAYER}. We're ever so grateful. Please accept something as a token of your time. Since we often receive donations, we have many things lying around."))
        goto(SilversunCity_BlindOrphanage_Reward)
    }


    if (var(VAR_SILVERSUN_ORPHANAGE_MAINQUEST) == 2){
        msgbox(format("How's the volunteering going?"))
        release
    }


    if (var(VAR_SILVERSUN_ORPHANAGE_MAINQUEST) == 1){
        msgbox(format("You've got the look of someone with a kind heart - and strong legs. Both are useful around here. Tell me, would you consider helping out around the orphanage for a while? The children and their Pokémon  could use a little extra care."), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            msgbox(format("Excellent! Kindness like yours is what keeps us going. You won't have to do much - just lend a hand here and there. Please ask around for people in need of help. Come speak to me again once you're finished."))
            release
            //start quest
            setvar(VAR_SILVERSUN_ORPHANAGE_MAINQUEST, 2)
        } else {
            //move on
            msgbox(format("Oh well. Whenever you'd like to, we're always looking for help!"))
        }
    }

    releaseall
    end

}



script SilversunCity_BlindOrphanage_Reward{
    message(format("Perhaps you may find one of these useful?"))
    waitmessage
    dynmultichoice(0, 0, TRUE, 2, 0, DYN_MULTICHOICE_CB_NONE, "Wise Glasses", "Muscle Band")

    if (var(VAR_RESULT) == 0){
        goto(SilversunCity_BlindOrphanage_RewardA)
    } else{
        goto(SilversunCity_BlindOrphanage_RewardB)
    }
}
script SilversunCity_BlindOrphanage_RewardA{
    setvar(VAR_0x8006, ITEM_WISE_GLASSES)
    showitemdescription
    msgbox(format("Are you sure?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == 1){
        hideitemdescription
        giveitem(ITEM_WISE_GLASSES)
        goto(SilversunCity_BlindOrphanage_RewardEnd)
    } else {
        hideitemdescription
        goto(SilversunCity_BlindOrphanage_Reward)
    }
}
script SilversunCity_BlindOrphanage_RewardB{
    setvar(VAR_0x8006, ITEM_MUSCLE_BAND)
    showitemdescription
    msgbox(format("Are you sure?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == 1){
        hideitemdescription
        giveitem(ITEM_MUSCLE_BAND)
        goto(SilversunCity_BlindOrphanage_RewardEnd)
    } else {
        hideitemdescription
        goto(SilversunCity_BlindOrphanage_Reward)
    }
}
script SilversunCity_BlindOrphanage_RewardEnd{
    setvar(VAR_SILVERSUN_ORPHANAGE_MAINQUEST, 4)
    msgbox(format("Please visit us again!"), MSGBOX_NPC)
    end
}






