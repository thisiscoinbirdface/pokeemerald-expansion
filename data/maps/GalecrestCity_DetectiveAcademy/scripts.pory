raw `
    .set LOCALID_KOHLA, 16
`

mapscripts GalecrestCity_DetectiveAcademy_MapScripts{}



//Kohla talk
script GalecrestCity_DetectiveAcademy_EventScript_Kohla{
    lockall

    //Kohla stares blankly
    //Explains his past and motivations
    speakername("Kohla")
    msgbox(format("...\p...\p...\pI saw a lot of bad stuff growing up, so I wanted to become a detective.\pSo I did.\pI graduated from this academy at the top of my class...\p...but it wasn't enough."))
    release
    //Turns, thanks player for telling him what he needs
    faceplayer
    waitmovement(0)
    delay(24)
    speakername("Kohla")
    msgbox(format("{PLAYER}, it was you who stopped Team Somber from getting the dreamstone.\pThanks to you, I learned something. Being the Ace Rookie isn't enough.\pTo stop Team Somber - no, to stop all crime, I need to become stronger than the criminals.\pAt the end of the day, it's all about how strong you are."))
    release 

    //Start walking out
    applymovement(LOCALID_KOHLA, moves(walk_right walk_up*3 walk_left face_down))
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
    //Reminder for silversun city and gym badge
    speakername("Kohla")
    msgbox(format("Silversun City is east of here, but the path is blocked by rocks. If you beat the Gym Leader here, you'll be able to use the HM Rock Smash, which will let you pass.\pSee you in Silversun City...or sooner than that, I think."))
    release
    //Leaves
    applymovement(LOCALID_KOHLA, moves(walk_up * 4))
    waitmovement(0)
    removeobject(LOCALID_KOHLA)
    setflag(FLAG_DETECTIVE_ACAD_KOHLA)    

    releaseall
}



//BREECH
script GalecrestCity_DetectiveAcademy_Breech{
    speakername("Breech")
    msgbox(format("It's you {PLAYER}! How are you? Clefairy and I are sitting in this class on Pokémon battles. Yep, to be a good detective, you need to be a strong trainer too!\p...\pNot that Clefairy and I want to be detectives, haha! We're just lifelong learners."), MSGBOX_NPC)
    applymovement(13, moves(face_left))
    end
}

script GalecrestCity_DetectiveAcademy_Clefairy{
    lockall
    playmoncry(SPECIES_CLEFAIRY, CRY_MODE_NORMAL)

    if (flag(FLAG_GALECREST_CLEFAIRY_ITEM)){
        msgbox(format("Clefairy is extremely focused on the teacher. A good student!"))    
    } else {
        msgbox(format("Clefairy is extremely focused on the teacher. A good student!"))
        release
        applymovement(17, moves(emote_exclamation_mark))
        faceplayer
        waitmovement(0)
        delay(16)
        speakername("Clefairy")
        msgbox(format("Clee---!!!"))
        release
        delay(16)
        msgbox(format("Clefairy hands you an item..."))
        giveitem(ITEM_FOCUS_BAND)
        msgbox(format("This must be the secret of Clefairy 's focus! Better not disturb her any further...thank you Clefairy!"))
        release
        applymovement(17, moves(face_left))
        waitmovement(0)
        setflag(FLAG_GALECREST_CLEFAIRY_ITEM)
    }

    releaseall
    end
}








//NPCs
//Lobby
script GalecrestCity_DetectiveAcademy_Receptionist{
    lockall
    faceplayer
    msgbox(format("Hello, trainer! Welcome to the Detective Academy!")) 
    goto(GalecrestCity_DetectiveAcademy_Receptionist_Multichoice)

    releaseall
    end
}

script GalecrestCity_DetectiveAcademy_Receptionist_Multichoice{
    msgbox(format("How may I help you today?"))
    dynmultichoice(0, 0, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Tell me about the Academy!", "Can I get a student ID?", "I want a Charcadet!")

    if (var(VAR_RESULT) == 0){
        msgbox(format("The Galecrest Detective Academy is one of the most prestigious schools in all of Cormoria! Our top-caliber candidates train and study to become the very best detectives. Many go on to join the police force or pursue the path of a trainer."))
        release
        delay(16)
        goto(GalecrestCity_DetectiveAcademy_Receptionist_Multichoice)
    }

    if (var(VAR_RESULT) == 1){
        goto(GalecrestCity_DetectiveAcademy_Receptionist_StudentID)
    }

    if (var(VAR_RESULT) == 2){
        goto(GalecrestCity_DetectiveAcademy_Receptionist_Charcadet)
    }

    releaseall
    end
}

script GalecrestCity_DetectiveAcademy_Receptionist_StudentID{
    checkitem(ITEM_DETECTIVE_STUDENT_ID)

    if (var(VAR_RESULT) == 1){
        msgbox(format("Sorry, you already have a student ID."))
        goto(GalecrestCity_DetectiveAcademy_Receptionist_Multichoice)        
    } else {
        msgbox(format("Unfortunately, only students are allowed IDs and enrolment is closed for the year. However, we do offer day passes to prospective students for a nominal fee of 1000."))
        release
        delay(16)
        showmoneybox(0, 0)
        msgbox(format("Would you like to purchase one for 1000?"), MSGBOX_YESNO)

        if (var(VAR_RESULT) == 1){
            hidemoneybox
            checkmoney(1000)
            if (var(VAR_RESULT) == 1){
                msgbox(format("Very good! Here's your temporary student ID."))
                giveitem(ITEM_DETECTIVE_STUDENT_ID)
                setflag(FLAG_GALECREST_STUDENTID_GET)
                removemoney(1000)
                msgbox(format("With this ID, you'll be able to access the academy facilities as if you were a normal student. You can even sit in any ongoing classes!"))
                release
            } else {
                msgbox(format("Sorry, you don't have enough money. Please come back later!"))
                release
                goto(GalecrestCity_DetectiveAcademy_Receptionist_Multichoice)                
            }
        } else {
            hidemoneybox
            goto(GalecrestCity_DetectiveAcademy_Receptionist_Multichoice)
        }

    }

    releaseall
    end
}

script GalecrestCity_DetectiveAcademy_Receptionist_Charcadet{
    
    msgbox(format("Can I see your student ID please?\p..."))

    if (checkitem(ITEM_FAKE_STUDENT_ID)){
        if (flag(FLAG_GALECREST_CHARCADET_GET)){
            msgbox(format("You've already received your Charcadet. Please return to class!"))
        } else {
            msgbox(format("Everything seems to be in order. What happened - did they forget to give you your Charcadet at the entrance ceremony? Well, no matter. Here you go!"))
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox(format("The Detective Academy has gifted you a Charcadet!"))
            givemon(SPECIES_CHARCADET, 20)
            msgbox(format("Please treat your Charcadet with love and care!"))
            release
            setflag(FLAG_GALECREST_CHARCADET_GET)
        }
    } else {
    checkitem(ITEM_DETECTIVE_STUDENT_ID)

        if (var(VAR_RESULT) == 1){
            msgbox(format("Sorry, as a temporary student, you're not eligible for a Charcadet."))
            goto(GalecrestCity_DetectiveAcademy_Receptionist_Multichoice)        
        } else {
            msgbox(format("Sorry, only students are eligible for a Charcadet."))
            goto(GalecrestCity_DetectiveAcademy_Receptionist_Multichoice)    
        }
    }

    releaseall
    end
}


//Lobby
script GalecrestCity_DetectiveAcademy_Guard{
    if (flag(FLAG_GALECREST_STUDENTID_GET)){
        msgbox(format("You got a one-day student ID? Maybe I should get one too..."), MSGBOX_NPC)
    } else {
        msgbox(format("Garden access is for students only. That ARMAROUGE won't let you in without a STUDENT ID!"), MSGBOX_NPC)
    }
    end
}

script GalecrestCity_DetectiveAcademy_Armarouge_Garden{
    lockall
    faceplayer
    playmoncry(SPECIES_ARMAROUGE, CRY_MODE_NORMAL)
    if (flag(FLAG_GALECREST_STUDENTID_GET)){
        msgbox(format("Armarouge leers down at you...and nods approvingly!"))
        release
        applymovement(22, moves(walk_left face_down))
        waitmovement(0)
    } else {
        msgbox(format("Armarouge leers down at you...and continues to look straight ahead."))
        applymovement(22, moves(face_down))
        waitmovement(0)
    }
    releaseall
    end
}

script GalecrestCity_DetectiveAcademy_Armarouge_Classroom{
    lockall
    faceplayer
    playmoncry(SPECIES_ARMAROUGE, CRY_MODE_NORMAL)
    if (flag(FLAG_GALECREST_STUDENTID_GET)){
        msgbox(format("Armarouge leers down at you...and nods approvingly!"))
        release
        applymovement(23, moves(walk_right face_down))
        waitmovement(0)
    } else {
        msgbox(format("Armarouge leers down at you...and continues to look straight ahead."))
        applymovement(23, moves(face_down))
        waitmovement(0)
    }
    releaseall
    end
}

script GalecrestCity_DetectiveAcademy_NPC1{
    msgbox(format("Ooh! Ohh! Are the new class rankings out yet?"), MSGBOX_NPC)
    end
}

script GalecrestCity_DetectiveAcademy_NPC2{
    msgbox(format("Every semester, the school ranks all the students. The top student is called the Ace Rookie! Kohla held the title every semester, but he's graduated now, so the field is open again!"), MSGBOX_NPC)
    end
}

script GalecrestCity_DetectiveAcademy_NPC3{
    msgbox(format("All new students receive a Charcadet as their first Pokémon. It's the standard detective companion."), MSGBOX_NPC)
    end
}

script GalecrestCity_DetectiveAcademy_NPC4{
    
    
    msgbox(format("Since many rich folk live in Galecrest City, there's a lot of theft. New students are often handed smaller cases to solve as part of their internship. But some also intern at the Galecrest Gym!"), MSGBOX_NPC)
    end
}

script GalecrestCity_DetectiveAcademy_NPC5{
    msgbox(format("I'm going to join today! I'm so excited to get my Charcadet and become an Ace Rookie!"), MSGBOX_NPC)
    end
}


//Hall
script GalecrestCity_DetectiveAcademy_NPC6{
    msgbox(format("Are you a student? You shouldn't be here if you aren't...oh, you are."), MSGBOX_NPC)
    end
}


//Classrooms
    //Double battle
    script GalecrestCity_DetectiveAcademy_NPC7{
        lockall
        faceplayer
        msgbox(format("We're battling to practice for our exams. It's very competitive here. Losers like you can't cut it."))
        applymovement(10, moves(face_right))
        waitmovement(0)
        releaseall
    }

script GalecrestCity_DetectiveAcademy_NPC8{
    trainerbattle_single(TRAINER_GALECREST_DETECTIVE_A, format("Hey, you look weak. You'd be a good training dummy for my Charcadet!"), format("But you're not even a student!"))
    msgbox(format("You may have beaten me, but I bet you can't beat Kohla! He's the star of the Academy!"), MSGBOX_AUTOCLOSE)
    end
}

script GalecrestCity_DetectiveAcademy_NPC9{
    msgbox(format("These two only focus on battling. But true detectives can do a lot more! We have to investigate clues, interrogate suspects and disguise ourselves."), MSGBOX_NPC)
    end
}

script GalecrestCity_DetectiveAcademy_NPC10{
    lockall
    msgbox(format("...and as you can see here, according to the theory of...\pThe teacher is engrossed in his lecture."))
    releaseall
    end
}

script GalecrestCity_DetectiveAcademy_NPC11{
    msgbox(format("I love this teacher's style! I wish he taught a more interesting subject."), MSGBOX_NPC)
    end
}

script GalecrestCity_DetectiveAcademy_NPC12{
    msgbox(format("I love the topic but this teacher is so boring!"), MSGBOX_NPC)
    end
}




script GalecrestCity_DetectiveAcademy_Charcadet_Battle{
    lockall
    playmoncry(SPECIES_CHARCADET, CRY_MODE_NORMAL)
    applymovement(18, moves(jump_in_place_left))
    waitmovement(0)
    msgbox(format("Charcadet is battling intensely!"))
    releaseall
    end
}

script GalecrestCity_DetectiveAcademy_Yamper_Battle{
    lockall
    playmoncry(SPECIES_YAMPER, CRY_MODE_NORMAL)
    applymovement(19, moves(jump_in_place_right))
    waitmovement(0)    
    msgbox(format("Yamper's not backing down!"))
    releaseall
    end
}


script GalecrestCity_DetectiveAcademy_Charcadet_A{
    playmoncry(SPECIES_CHARCADET, CRY_MODE_NORMAL)
    msgbox(format("Charcadet is also curious about the class rankings!"), MSGBOX_NPC)
    end
}




