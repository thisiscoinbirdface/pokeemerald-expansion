mapscripts Championship_R3_MapScripts{    
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_TEMP_0, 0: Championship_R3_OnWarp
    ]

    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_TEMP_1, 0: Championship_R3
    ]
}

script Championship_R3_OnWarp{
    setvar(VAR_TEMP_0, 1)
}

script Championship_R3{
    lockall

    //vars and stuff
    setvar(VAR_TEMP_1, 1)
    setdynamicwarp(MAP_CHAMPIONSHIP_R3_LOBBY, 4, 13)

    //main cutscene
    special(SpawnCameraObject)

    setrespawn(HEAL_LOCATION_CHAMPIONSHIP_CORRIDOR)

    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 4))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up walk_left * 2 walk_up * 2 face_right))
    waitmovement(0)
    delay(16)
    
    msgbox_delay("Announcer", format("Alright everyone, welcome back!\nThe tournament is heating up!\pWe're down to the eight best trainers in all of Cormoria now!\pLet's hear a huge cheer for the trainers! Show your support!\pAfter a conclusive round of 16 victory, {COLOR GREEN}Ace Rookie{COLOR DARK_GRAY} {COLOR GREEN}Kohla{COLOR DARK_GRAY} stands in the lead for..."))

    applymovement(1, moves(jump_in_place_left))
    msgbox_delay("Berger", format("Just you wait and watch - Avalugg and I will crush the {COLOR GREEN}Ace Rookie{COLOR DARK_GRAY}!"))

    msgbox_delay("Announcer", format("Trainers - are you ready?"))

    applymovement(1, moves(jump_in_place_left))
    msgbox_delay("Berger", format("I can't believe I'm seeing you here of all places!\p...\pWho would've guessed back in {COLOR GREEN}Gastree City{COLOR DARK_GRAY} that you and I would follow the same path?\pAvalugg and I are strong - we're the strongest duo in all of Cormoria!"))

    trainerbattle_no_intro(TRAINER_CHAMPIONSHIP_C, format("Avalugg...how could we lose?"))

    //opponent exit
    applymovement(1, moves(jump_in_place_left))
    msgbox_delay("Berger", format("You were so strong, {PLAYER}...\nI guess this is what it takes to become the Pok√©mon Champion.\pI get it. We get it!\nWe're not ready yet.\pBut next time, Avalugg and I will be coming for the crown!\pGood luck, {PLAYER}!"))    
    
    playmoncry(SPECIES_PINSIR, CRY_MODE_NORMAL)
    applymovement(2, moves(jump_in_place_left))
    waitmovement(0)
    delay(24)

    applymovement(1, moves(walk_left walk_down))
    waitmovement(0)

    applymovement(2, moves(walk_left walk_down walk_left))
    applymovement(1, moves(walk_down walk_left walk_down))
    waitmovement(0)
    removeobject(1)

    applymovement(2, moves(walk_down))
    waitmovement(0)
    removeobject(2)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left * 2 walk_down))
    waitmovement(0)
    special(RemoveCameraObject)

    setvar(VAR_CHAMP_STAGE, 4)
    setflag(FLAG_BEAT_R3)

    releaseall
    end

}





