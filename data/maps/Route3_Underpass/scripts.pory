mapscripts Route3_Underpass_MapScripts{}

script Route3_Underpass_Nurse{
    lockall
    faceplayer
    msgbox(format("This area is closed for the demo."))
    releaseall
    end
}

script Route3_Underpass_NPC1{
    lockall
    faceplayer
    msgbox(format("A hiker with a CLEFAIRY ran past here a while ago. He was being chased by some weirdo in a dark outfit. The things you see while on the road..."))
    releaseall
}

script Route3_Underpass_NPC2{
    lockall
    faceplayer
    msgbox(format("They say that a rare {PKMN} has made these caves its home. I need to go deeper down!"))
    releaseall
}

script Route3_Underpass_EvilBattle_Trigger0{
    if (flag(FLAG_ROUTE3_UNDERPASS_EVILTEAM)){
        end
    }    
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right face_down))
    waitmovement(0)    
    goto(Route3_Underpass_EvilBattle)
}
script Route3_Underpass_EvilBattle_Trigger1{
    if (flag(FLAG_ROUTE3_UNDERPASS_EVILTEAM)){
        end
    }    
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    waitmovement(0)    
    goto(Route3_Underpass_EvilBattle)
}
script Route3_Underpass_EvilBattle_Trigger2{
    if (flag(FLAG_ROUTE3_UNDERPASS_EVILTEAM)){
        end
    }    
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left face_down))
    waitmovement(0)
    goto(Route3_Underpass_EvilBattle)
}


script Route3_Underpass_EvilBattle{
    lockall

    playbgm(MUS_ENCOUNTER_MAGMA, FALSE)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 2))
    waitmovement(0)
    special(RemoveCameraObject)

    //Some convo between hiker and magma
    applymovement(2, moves(jump_in_place_right))
    waitmovement(0)    
    msgbox(format("???: Give it to me!"))
    applymovement(3, moves(walk_right face_left))
    waitmovement(0)    
    msgbox(format("BREECH: No! Stay away! CLEFAIRY is MY partner!"))
    msgbox(format("???: I said give it to me! It's holding a dreamstone. That's too precious to be held by someone like you!"))
    applymovement(2, moves(walk_right))
    waitmovement(0)
    msgbox(format("BREECH: I don't know anything about any dreamstones, please! Just stop!"))
    release
    playmoncry(SPECIES_CLEFAIRY, CRY_MODE_NORMAL)    
    applymovement(6, moves(jump_in_place_left * 2))
    waitmovement(0)
    msgbox(format("CLEFAIRY: clee!!!\p???: Hahaha! Your CLEFAIRY wants to protect you, but you don't even have the will to fight alongside her. Just give up before I bring my {PKMN} out!"))
    release

    //They look at you and you walk to them
    applymovement(2, moves(jump_in_place_up emote_question_mark))
    waitmovement(0)
    msgbox(format("???: Who's there? What you do want?"))
    applymovement(3, moves(face_up))
    waitmovement(0)    
    msgbox(format("BREECH: {PLAYER}! It's you! This thug is trying to take CLEFAIRY away! Help me, please!"))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down))
    waitmovement(0)     
    msgbox(format("???: Oh, you're just some stupid kid. How about you mind your own business?"))
    msgbox(format("GABRIELLE: No? No?!?! Well then...let GABRIELLE teach you a lesson: don't stick your nose where it doesn't belong!"))
    release

    //Start battle
    trainerbattle_no_intro(TRAINER_ROUTE3_EVIL_GABRIELLE, "My {PKMN} just aren't good enough!")

    //End battle - baddie says revenge
    applymovement(3, moves(face_left))
    waitmovement(0) 
    applymovement(2, moves(face_right))
    waitmovement(0)    
    msgbox(format("GABRIELLE: Fine - keep your stupid CLEFAIRY!"))
    release
    applymovement(2, moves(jump_in_place_up))
    waitmovement(0)
    msgbox(format("GABRIELLE: And you! I'll remember this, {PLAYER}. You'll regret messing with Team Somber!"))
    release
    applymovement(2, moves(walk_down * 2))
    waitmovement(0)
    removeobject(2)

    //Breech says he needs to train
    applymovement(6, moves(walk_up))
    applymovement(3, moves(walk_left face_up))
    waitmovement(0)     
    msgbox(format("{PLAYER} - CLEFAIRY and I are in your debt. The way you handled that thug was incredible."))
    msgbox(format("I've realised something. When alone, I was free to do as I wish and avoid battling. But with a partner, I can't be that way."))
    msgbox(format("CLEFAIRY was ready to protect me, but I wasn't prepared to do the same for her.\p...\pI let her down."))
    msgbox(format("But no more! I'm going to learn to hold my ground. If Team Somber comes after CLEFAIRY again, we'll be ready."))
    msgbox(format("{PLAYER} - we owe you. As thanks, CLEFAIRY and I want to give you the stone that GABRIELLE person was after..."))
    release
    giveitem(ITEM_EVERSTONE)
    msgbox(format("I don't know about dreamstones, but it looks like a regular EVERSTONE to me. Who knows what she saw?"))
    msgbox(format("Don't worry - CLEFAIRY's bored of that stone already. Next, we'll head to MT CERAM to collect rare stones there!"))
    release
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    applymovement(3, moves(walk_down * 2))
    applymovement(6, moves(walk_left walk_down * 2))
    waitmovement(0)     
    removeobject(3)
    applymovement(6, moves(face_up))
    waitmovement(0)
    delay(30)
    playmoncry(SPECIES_CLEFAIRY, CRY_MODE_NORMAL)
    msgbox(format("CLEFAIRY: clee!!!"))
    release
    applymovement(6, moves(face_down))
    waitmovement(0)
    removeobject(6)

    //End
    setflag(FLAG_ROUTE3_UNDERPASS_EVILTEAM)
    releaseall
    
}


