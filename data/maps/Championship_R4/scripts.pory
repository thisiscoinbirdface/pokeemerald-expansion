mapscripts Championship_R4_MapScripts{    
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_TEMP_0, 0: Championship_R4_OnWarp
    ]

    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_TEMP_1, 0: Championship_R4
    ]
}

script Championship_R4_OnWarp{
    setvar(VAR_TEMP_0, 1)
}

script Championship_R4{
    lockall

    //vars and stuff
    setvar(VAR_TEMP_1, 1)
    setdynamicwarp(MAP_CHAMPIONSHIP_R4_LOBBY, 4, 13)

    //main cutscene
    special(SpawnCameraObject)

    setrespawn(HEAL_LOCATION_CHAMPIONSHIP_CORRIDOR)

    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 4))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up walk_left * 2 walk_up * 2 face_right))
    waitmovement(0)
    delay(16)
    
    msgbox_delay("Announcer", format("It's time for the semi-finals!\pLet's hear a huge cheer for the trainers! Show your support!"))

    applymovement(1, moves(jump_in_place_left))
    msgbox_delay("Gabrielle", format("This is unexpected...or maybe not.\pYou and I have crossed paths so many times, I feel like it's almost destiny that we're opponents here."))

    msgbox_delay("Announcer", format("Trainers - are you ready?"))

    applymovement(1, moves(jump_in_place_left))
    msgbox_delay("Gabrielle", format("{PLAYER}, we've been through a lot, haven't we?\pNot all of it together.\nThe first time we teamed up was...\p...Yeah!\pIn {COLOR RED}Ancient Cormoria{COLOR DARK_GRAY}, when Moxie and the team abandoned me.\pIt feels like a lifetime ago...\p{PLAYER} - I'm a different person now.\pAfter leaving, {COLOR BLUE}Team Somber{COLOR DARK_GRAY}, I've grown so much and seen so much!\pIt's all because of you.\pNo matter who wins today, I'm glad we met, {PLAYER}."))

    trainerbattle_no_intro(TRAINER_CHAMPIONSHIP_D, format("You're amazing!"))

    //opponent exit
    applymovement(1, moves(jump_in_place_left))
    msgbox_delay("Gabrielle", format("I've lost to you so many times now...but this time, I don't feel too bad.\pI don't know who won the other semifinal, but whoever it is - you have to promise me that you'll win, okay? That way, I can tell everyone I lost to the Champion!"))    
    
    playmoncry(SPECIES_PINSIR, CRY_MODE_NORMAL)
    applymovement(2, moves(jump_in_place_left))
    waitmovement(0)
    delay(24)

    applymovement(1, moves(walk_left walk_down))
    waitmovement(0)

    applymovement(2, moves(walk_left walk_down walk_left))
    applymovement(1, moves(walk_down walk_left walk_down))
    waitmovement(0)
    removeobject(1)

    applymovement(2, moves(walk_down))
    waitmovement(0)
    removeobject(2)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left * 2 walk_down))
    waitmovement(0)
    special(RemoveCameraObject)

    setvar(VAR_CHAMP_STAGE, 5)
    setflag(FLAG_BEAT_R4)

    releaseall
    end

}

