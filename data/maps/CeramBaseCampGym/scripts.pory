raw `
    .set LOCALID_GUARD, 2
    .set LOCALID_LEADER, 5
`

mapscripts CeramBaseCampGym_MapScripts{}

script CeramBaseCampGym_Red{
    lockall
    faceplayer
    msgbox(format("Welcome to the Ceram Base Camp Management Office And Gym! The Gym Leader ARIAN, who also manages the base camp, specialises in FIRE type Pokemon. Avoid using BUG and GRASS types!"))
    releaseall
}

script CeramBaseCampGym_Guard{
    lockall
    faceplayer
    msgbox(format("Arian's got a busy schedule. He sets up all his appointments in advance."))
    releaseall
}

script CeramBaseCampGym_NPC_A{
    lockall
    faceplayer
    msgbox(format("I can't battle. Arian tried to teach me but both him and his Pokemon are so intimidating!"))
    releaseall
}

script CeramBaseCampGym_NPC_B{
    lockall
    faceplayer
    msgbox(format("Why does everyone used FIRE type Pokemon in here? It's already sweltering hot. Our air conditioning bills are too high!"))
    releaseall
}


script CeramBaseCampGym_TrainerBattle_A {
    trainerbattle_single(TRAINER_CERAMBASECAMPGYM_A, format("Who would've thought managing a basecamp would involve so much paperwork?"), "Back to my work!")
    msgbox(format("The base camp doubling as a Gym is just extra work for the employees! And we didn't get a raise too!"))
    release
    end
}

script CeramBaseCampGym_TrainerBattle_B {
    trainerbattle_single(TRAINER_CERAMBASECAMPGYM_B, format("I've been waiting all day for an appointment. I'm so frustrated that I need to let it out!"), "At least I've cooled down a bit.")
    msgbox(format("All this bureaucracy is mind-numbing."))
    release
    end
}

script CeramBaseCampGym_GuardTrigger{
    if (flag(FLAG_CERAMBASECAMPGYM_APPOINTMENTLETTER)) {end}

    lockall
    applymovement(LOCALID_GUARD, moves(emote_exclamation_mark face_right))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))
    waitmovement(0)
    delay(16)
    msgbox(format("Hold on! You can't have a meeting with the manager without an appointment. Please come back once you have a signed APPOINTMENT LETTER."))
    release
    applymovement(LOCALID_GUARD, moves(face_down))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down))
    waitmovement(0)

    releaseall
}

script CeramBaseCampGym_EventScript_NPC_LetterPrint{
    lockall
    faceplayer

    if (checkitem(ITEM_APPOINTMENT_LETTER)){
        if (flag(FLAG_CERAMBASECAMPGYM_APPOINTMENTLETTER)){
            msgbox(format("I'm busy now. Get out."))
        } else {
            msgbox(format("You'll have to get this letter signed by the assistant manager in the basement."))
        }
    } else {
        msgbox(format("You're a trainer and want an APPOINTMENT LETTER? I can print you a letter, but first I'll need to see your credentials.\p...\pNo, not your trainer card. Don't you have a Pokemon League Pass? You don't? Well then a battle should prove that you really are who you say you are!"))
        release

        //battle
        trainerbattle_no_intro(TRAINER_CERAMBASECAMPGYM_C, "I lost")

        //print letter
        msgbox(format("Very good! Well then, let me do my job. Here you go!"))
        //Get item
        giveitem(ITEM_APPOINTMENT_LETTER)
        //But you need a signature
        msgbox(format("But this letter won't be enough without the approval of the assistant manager. He sits in the basement. Get this letter signed by him."))
    }
    releaseall
}

script CeramBaseCampGym_EventScript_NPC_Signature{
        lockall
        faceplayer    
    if (flag(FLAG_CERAMBASECAMPGYM_APPOINTMENTLETTER)){
        msgbox(format("I'm busy now. Get out."))
    } else {
        if (checkitem(ITEM_APPOINTMENT_LETTER)){

            msgbox(format("So you got an APPOINTMENT LETTER and want my signature on it? Well...I wouldn't mind doing it for a small token of appreciation...say...500?"))
            release
            showmoneybox(0, 0)
            msgbox(format("Will you slip the money under the table?"), MSGBOX_YESNO)

            if(var(VAR_RESULT) == 0){
                hidemoneybox
                msgbox(format("You'll have to do it the way everyone else does it then!"))
                //battle
                trainerbattle_no_intro(TRAINER_CERAMBASECAMPGYM_D, "I wish you had just paid up!")                
            } else {
                //remove money
                hidemoneybox
                removemoney(500)
                msgbox(format("{PLAYER} bribed the assistant manager."))
                release
            }

            msgbox(format("Alright, I'll sign the letter."))
            msgbox(format("{PLAYER} got the APPOINTMENT LETTER signed by the assistant manager."))
            release
            setflag(FLAG_CERAMBASECAMPGYM_APPOINTMENTLETTER)
            msgbox(format("I'm busy now. Get out."))        
        } else {
            msgbox(format("You're a trainer and want an APPOINTMENT LETTER? You'll have to go upstairs, get a letter printed, and then I can sign and approve it.")) 
        }
    }

    releaseall
}





















script CeramBaseCampGym_LeaderBattle {
    call(Common_EventScript_SaveGame)

    lockall
    faceplayer

    if (flag(FLAG_BADGE02_GET)){
        msgbox(format("That was a great battle. You've earned my confidence. Go get 'em, {PLAYER}!"))
    } else { //else do the battle
        msgbox(CeramBaseCampGym_Text_Intro)
        trainerbattle_no_intro(TRAINER_CERAMBASECAMPGYM_LEADER, CeramBaseCampGym_Text_Defeated)
        
        //Receive the badge
        msgbox(format("{PLAYER} received the CINDER BADGE from ARIAN."))
        release
        setflag(FLAG_BADGE02_GET)

        //Play gym badge fanfare
        call(Common_EventScript_PlayGymBadgeFanfare)

        //Describe the badge features (use HMs, allow pokemon control etc.)
        msgbox(format("The CINDER BADGE increases the ATK of all your Pokemon and lets you control Pokemon upto lv. 20."))
        msgbox(format("And I'd also like to give you this."))
        release
        //Give TM also
        giveitem(ITEM_TM_SUNNY_DAY)
        msgbox(format("This TM will let your Pokemon bring sunlight on the battlefield, powering up FIRE-type moves!"))
        release

        //Bye-bye
        msgbox(format("And now it's time for you to set off on your journey. I hope you manage to find the dreamstone and Professor Tenebris. I don't know why Team Somber is after the dreamstones, but be careful if you run into them!"))
        releaseall
        
    }

    releaseall

}


text CeramBaseCampGym_Text_Intro{
    format("Who's my next appointment? Ah! It's you, {PLAYER}. A pleasant surprise. I'm sick of all this work. This office is supposed to be a Gym too, but we get very few trainers. Most people that come are hikers or collectors. It's good to have a chance to battle for a change!")
}

text CeramBaseCampGym_Text_Defeated{
    format("What a match! What a display! I could not be more pleased, {PLAYER}. The way you stand, you might be able to handle yourself on Mt. Ceram. I present you with the CINDER BADGE.")
}


