raw `
    .set LOCALID_GAB, 13
    .set LOCALID_RUE, 18    
    .set LOCALID_DET1, 19
    .set LOCALID_DET2, 20
`

mapscripts GalecrestCity_MapScripts{
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0: GalecrestCity_OnFrame
    ]
}




//Galecrest first time cutscene
//Flag: FLAG_GALECREST_POST_ANCIENT


//You land back: done
//On warp: Gabrielle dialogue - thanks you (tells you that you're in galecrest with the detective academy)
//Walk together to some place, then see the cops, and gab fucks off.

script GalecrestCity_OnFrame{
    //If the flag is done, then skip
    if (flag(FLAG_GALECREST_POST_ANCIENT)) {end}

    //Cutscene
    //Phase 1: Gabrielle
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
    waitmovement(0)
    msgbox(format("GABRIELLE:...have we left that place?\pGABRIELLE: Yes! Yes we have! I'm so relieved. I've never been so happy to see a city before!\pGABRIELLE: This must be Galecrest City. The Detective Academy is here. I suppose you'll be headed there. Just head north from here...\pGABRIELLE:...and I'll be going back to Team Somber.\pGABRIELLE: Sigh...\p...\pGABRIELLE: {PLAYER}, we're still enemies! But you saved me. I owe you one.\p...\pGABRIELLE: T-thanks. Bye."))
    release
    
    //Remove gab
    fadescreen(FADE_TO_BLACK)
    removeobject(LOCALID_GAB)
    fadescreen(FADE_FROM_BLACK)

    setflag(FLAG_GALECREST_POST_ANCIENT)
    releaseall
}

//detectives - meet them inside the detective academy
//where were you?
//...
//This is incredible. I'm glad you survived that.
//Where was kohla? he was defeated?
//kohla - quiet.
//Rue - I never knew this, ill go research
//Detectives - we received a tipoff that prof tenebris was sighted in Silversun City to the east.
//Team Somber HQ is also there.
//Everything makes sense - Team Somber knew things about the dreamstone, so they must have kidnapped the prof.

//We all should head there.


script GalecrestCity_DetectiveMeet_Trigger0{
    if (flag(FLAG_GALECREST_DETECTIVE_CONVO)) {end}

    lockall

    //Make all people face player and exclamation mark
    applymovement(LOCALID_DET1, moves(face_right emote_exclamation_mark))
    applymovement(LOCALID_DET2, moves(face_right emote_exclamation_mark))
    applymovement(LOCALID_RUE, moves(face_right emote_exclamation_mark))
    waitmovement(0)
    delay(16)

    msgbox(format("RUE: {PLAYER}! You're here! Where have you been?"))
    release

    //Move player
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left *2 walk_up walk_left))

    //Trigger next script
    call(GalecrestCity_DetectiveMeet_Main)
}

script GalecrestCity_DetectiveMeet_Trigger1{
    if (flag(FLAG_GALECREST_DETECTIVE_CONVO)) {end}

    lockall

    //Make all people face player and exclamation mark
    applymovement(LOCALID_DET1, moves(face_right emote_exclamation_mark))
    applymovement(LOCALID_DET2, moves(face_right emote_exclamation_mark))
    applymovement(LOCALID_RUE, moves(face_right emote_exclamation_mark))
    waitmovement(0)
    delay(16)

    msgbox(format("RUE: {PLAYER}! You're here! Where have you been?"))
    release

    //Move player
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left *3))
    waitmovement(0)

    //Trigger next script
    call(GalecrestCity_DetectiveMeet_Main)
}

script GalecrestCity_DetectiveMeet_Trigger2{
    if (flag(FLAG_GALECREST_DETECTIVE_CONVO)) {end}

    lockall

    //Make all people face player and exclamation mark
    applymovement(LOCALID_DET1, moves(face_right emote_exclamation_mark))
    applymovement(LOCALID_DET2, moves(face_right emote_exclamation_mark))
    applymovement(LOCALID_RUE, moves(face_right emote_exclamation_mark))
    waitmovement(0)
    delay(16)    

    msgbox(format("RUE: {PLAYER}! You're here! Where have you been?"))
    release

    //Move player
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left *2 walk_down walk_left))
    waitmovement(0)

    //Trigger next script
    call(GalecrestCity_DetectiveMeet_Main)
}

script GalecrestCity_DetectiveMeet_Trigger3{
    if (flag(FLAG_GALECREST_DETECTIVE_CONVO)) {end}

    lockall

    //Make all people face player and exclamation mark
    applymovement(LOCALID_DET1, moves(face_right emote_exclamation_mark))
    applymovement(LOCALID_DET2, moves(face_right emote_exclamation_mark))
    applymovement(LOCALID_RUE, moves(face_right emote_exclamation_mark))
    waitmovement(0)
    delay(16)    

    msgbox(format("RUE: {PLAYER}! You're here! Where have you been?"))
    release

    //Move player
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left *2 walk_down *2 walk_left))
    waitmovement(0)

    //Trigger next script
    call(GalecrestCity_DetectiveMeet_Main)
}


script GalecrestCity_DetectiveMeet_Main{
    msgbox(format("RUE: We heard from Kohla that you went up to the peak to stop Team Somber. What happened up there?\p...\p..."))
    applymovement(LOCALID_DET1, moves(face_right emote_exclamation_mark))
    applymovement(LOCALID_DET2, moves(face_right emote_exclamation_mark))
    applymovement(LOCALID_RUE, moves(face_right emote_exclamation_mark))
    waitmovement(0)    
    msgbox(format("RUE: What! Is that true? Did all of that happen?\p...\pRUE: I'm glad you're safe, {PLAYER}. What a series of events!"))
    msgbox(format("DETECTIVE: So the dreamstones transported you to another dimension that you couldn't recognise? And the Pokemon were completely different too?"))
    release
    applymovement(LOCALID_DET1, moves(face_down ))
    applymovement(LOCALID_RUE, moves(face_up ))
    delay(16)
    msgbox(format("DETECTIVE: Did you know this was possible Rue?\p RUE: No, I had no idea. We knew that dreamstones absorbed the memories of sleeping Pokemon - that's why its called a dreamstone! But to transport things and people..."))
    release
    applymovement(LOCALID_DET1, moves(face_right))
    applymovement(LOCALID_RUE, moves(face_right))
    delay(16)
    msgbox(format("RUE: {PLAYER}, you encountered ancient Pokemon there, right? I wonder...\pRUE: Did the dreamstone take you back to the past somehow?\pRUE: Incredible...so much we don't yet know...\pRUE: I need to do some research. I'm going to head back to the lab.\pRUE: {PLAYER}, the detectives have some updates for you.\p...\pRUE: I'm glad you're okay."))
    release

    //Rue exit
    applymovement(LOCALID_DET1, moves(face_left))
    applymovement(LOCALID_DET2, moves(face_left)) 
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left)) 

    applymovement(LOCALID_RUE, moves(walk_left * 6))
    waitmovement(0)
    removeobject(LOCALID_RUE)

    //Back to detectives
    applymovement(LOCALID_DET1, moves(face_right))
    applymovement(LOCALID_DET2, moves(face_right))      
    delay(16)
    msgbox(format("DETECTIVE: We've uncovered a new lead on Professor Tenebris. An agent told us that she was spotted in Silversun City."))
    release
    delay(16)
    msgbox(format("DETECTIVE: There's one issue...Team Somber. They're a known and dangerous criminal syndicate that aims to wrest power from the government.\pDETECTIVE: Their hideout is confirmed to be somewhere around Silversun City too.\pDETECTIVE: If the Professor, her dreamstone and Team Somber are in the same spot, things could erupt. We have to be there!"))
    msgbox(format("DETECTIVE: But now that we know Team Somber is involved, it's not safe for you, {PLAYER}. You may have handled a couple of grunts, but you're still a child.\pSo heed these instructions carefully - make your way to Silversun City as soon as possible and keep a low profile. Do NOT engage with any suspicious individuals!"))       
    release
    applymovement(LOCALID_DET1, moves(face_up))
    applymovement(LOCALID_DET2, moves(face_up))    
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))   
    delay(24)    
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 3))
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 2))
    msgbox(format("DETECTIVE: This building is the Detective Academy. Your frierd Kohla was trained here. He knows the plan already. Go and talk to him first."))       
    release

    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_down * 5))    
    waitmovement(0)
    special(RemoveCameraObject)        
    applymovement(LOCALID_DET1, moves(face_right))
    applymovement(LOCALID_DET2, moves(face_right))    
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))         
    delay(24)
    msgbox(format("DETECTIVE: See you in Silversun City."))
    release

    //exit
    fadescreen(FADE_TO_BLACK)
    removeobject(LOCALID_DET1)
    removeobject(LOCALID_DET2)
    fadescreen(FADE_FROM_BLACK)

    //Setvars quests etc
    setflag(FLAG_GALECREST_DETECTIVE_CONVO)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_DREAMSTONE_MYSTERIES, SUB_QUEST_3)    
    releaseall
    
}


















//POOCHYENA blocking the route
script GalecrestCity_EventScript_Poochyena{
    if (flag(FLAG_GALECREST_POOCHYENA)) {end}

    lockall
    msgbox(format("POOCHYENA: grr...\pThe Poochyena are clearly on edge. Better not aggravate them!"))
    setflag(FLAG_SYS_EXP_ALL)
    releaseall

}















//NPCs
script GalecrestCity_NPC_1{
    msgbox(format("You don't look like someone from here...are you a new student at the Detective Academy?"), MSGBOX_NPC)
    end
}

script GalecrestCity_NPC_2{
    msgbox(format("The city doesn't service our neighborhood. That's why these grass patches and puddles have come up."), MSGBOX_NPC)
    end
}

script GalecrestCity_NPC_3{
    msgbox(format("All the rich kids play at home, but me and my brother love to splash in these puddles!"), MSGBOX_NPC)
    end
}

script GalecrestCity_NPC_4{ 
    msgbox(format("When I grow up, I want to be just like my sister!"), MSGBOX_NPC)
    end
}

script GalecrestCity_Froakie{
    lockall
    faceplayer
    playmoncry(SPECIES_FROAKIE, CRY_MODE_ENCOUNTER)
    msgbox(format("FROAKIE is having fun with the siblings!"))
    releaseall
    end
}



//Give an item
script GalecrestCity_NPC_5{
    lockall
    faceplayer    

    if (flag(FLAG_GALECREST_ROCKSMASH)){
        msgbox(format("I need to find a Pokemon that can learn ROCK SMASH..."))
    } else {
        msgbox(format("This rock can be smashed by a Pokemon that knows a certain move...I forgot which one though! Do you know?"))
        dynmultichoice(0,0, TRUE, 4, 0, DYN_MULTICHOICE_CB_NONE, "ROCK BLAST", "ROCK SLIDE", "ROCK SMASH", "STONE EDGE")
        if (var(VAR_RESULT) == 2){
            msgbox(format("Yes! Now I remember! In fact, I also remember that I have the HM for it here. Why don't you keep it?"))
            giveitem(ITEM_HM_ROCK_SMASH)
            setflag(FLAG_GALECREST_ROCKSMASH)
        } else {
            msgbox(format("No..."))
        }
    }

    

    
    releaseall
}

script GalecrestCity_NPC_6{
    msgbox(format("If you head west, you enter the elite residential neighborhood.\pThe area south of here is poorer."), MSGBOX_NPC)
    end
}

script GalecrestCity_NPC_7{
    msgbox(format("The city garden is maintained by Rotoms! But for some reason, I've never seen any of them at work."), MSGBOX_NPC)
    end
}

script GalecrestCity_NPC_8{
    msgbox(format("I'm on a date with the girl of my dreams! But we can't ever be together. I'm too poor for her family."), MSGBOX_NPC)
    end
}

script GalecrestCity_NPC_9{
    msgbox(format("I don't care about money! We love each other and that's what matters."), MSGBOX_NPC)
    end
}

script GalecrestCity_NPC_10{  
    msgbox(format("The Detective Academy is the most prestigious police academy in all of Cormoria. My husband helped build it, many years ago, when he ran a construction business."), MSGBOX_NPC)
    end
}

//Nurse Joy gives 3 berries - reduce super-effective damage from the gym leader's attacks.
script GalecrestCity_NPC_11{
    if (flag(FLAG_GALECREST_NURSE_BERRY)){
        msgbox(format("I do the night shift at the Pokemon Center. During the day, Chansey and I like to walk around the garden and pick berries."), MSGBOX_NPC)
    } else {
        lockall
        faceplayer
        msgbox(format("I do the night shift at the Pokemon Center. During the day, Chansey and I like to walk around the garden and pick berries. Here, you can have some of our extra berries."))
        giveitem(ITEM_CHILAN_BERRY, 3)  //Halves normal damage
        setflag(FLAG_GALECREST_NURSE_BERRY)        
        msgbox(format("They're yummy!"), MSGBOX_NPC)
    }
    end
}



script GalecrestCity_Chansey{
    playmoncry(SPECIES_CHANSEY, CRY_MODE_NORMAL)
    msgbox(format("CHANSEY looks happy to be out in the garden!"), MSGBOX_NPC)
    end
}


script GalecrestCity_NPC_12{
    msgbox(format("If you continue west from here, you'll enter Route 5 and the Vilethorn Woods. Many years ago, one of the rich folk here left to build a cottage deep in the woods. Who would want to live in a creepy forest like that?\pRich folk I tell you..."), MSGBOX_NPC)
    end
}

script GalecrestCity_NPC_13{  
    msgbox(format("This part of town is so clean and peaceful!"), MSGBOX_NPC)
    end
}

//Robber battle 
    //VAR_GALECREST_RICH_PEARLS
    //0: start quest
    //1: started quest, no update (do the battle now)
    //2: got the pearls
    //3: done

script GalecrestCity_NPC_14{
    lockall

    if (var(VAR_GALECREST_RICH_PEARLS) == 0){
        msgbox(format("Hehehe...this was too easy!"))
        release
    } else {
        msgbox(format("Hehehe...this was too easy!"))
        release
        delay(24)
        faceplayer    
        applymovement(12, moves(emote_exclamation_mark))
        delay(24)
        msgbox(format("I look suspicious? How dare you! I'm just innocently hanging around behind this rich house! I don't take such accusations lightly!"))
        //trainer battle
        trainerbattle_no_intro(TRAINER_GALECREST_ROBBER, format("I've been caught!"))
        release
        msgbox(format("Fine, you got me! I'm a thief alright? I'll give you what I stole. It's not worth going to jail for."))
        release
        giveitem(ITEM_PEARL_STRING)
        msgbox(format("I knew I shouldn't have tried to steal anything near the Detective Academy!"))
        release

        fadescreen(FADE_TO_BLACK)
        removeobject(12)
        removeobject(36)
        fadescreen(FADE_FROM_BLACK)

        setflag(FLAG_GALECREST_ROBBER)
        setvar(VAR_GALECREST_RICH_PEARLS, 2)
    }

    releaseall
}

script GalecrestCity_Nickit{
    lockall
    playmoncry(SPECIES_NICKIT, CRY_MODE_NORMAL)
    msgbox(format("Nickit is grinning mischievously. What's it upto?"))
    releaseall
    end
}

//change dialogue
script GalecrestCity_NPC_15{ 
    msgbox(format("I really want to visit the Silversun Theatre and see a show! But crossing Vilethorn Woods isn't a good idea..."), MSGBOX_NPC)
    end
}

script GalecrestCity_NPC_16{
    lockall    
    msgbox(format("I'm sorry! I won't skip class again!"))
    releaseall
    end
}

script GalecrestCity_NPC_17{
    lockall    
    msgbox(format("One more misdeneanour and I'll have you expelled, got it? And then you'll never get your Charcadet."))
    releaseall
    end
}

script GalecrestCity_NPC_18{
    lockall
    msgbox(format("All my life I've lived in this neighbourhood. It's all I've seen and all I know...and now I'm too old to go exploring Cormoria."))  
    release
    delay(16)

    if (flag(FLAG_GALECREST_REGRETFULMAN)){
        releaseall
        end
    } else {
        faceplayer  
        msgbox(format("Have you ever felt like I do?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            msgbox(format("Experience does not discriminate. We all feel regret.\pBut you still have time to change.\pTake this as a memento - don't let anything bind you!"))
            giveitem(ITEM_BINDING_BAND)
            setflag(FLAG_GALECREST_REGRETFULMAN)
        } else {
            msgbox(format("You're still innocent. The smile on your face is so bright! It lightens my heart.\pAccept this as a memento - hold tightly onto that smile of yours!"))
            giveitem(ITEM_GRIP_CLAW)
            setflag(FLAG_GALECREST_REGRETFULMAN)
        }
    }
    applymovement(16, moves(face_left))
    releaseall
    end
}

script GalecrestCity_NPC_19{
    msgbox(format("I learned something new in class today! Some berries reduce the power of attacks dealt to them. A Chilan Berry for example reduces the power of a NORMAL type attack. That'd be useful in the gym here, wouldn't it?"), MSGBOX_NPC)
    end
}

script GalecrestCity_NPC_20{
    msgbox(format("Whaddya want? Thought you'd get something by talking to me? You poor people are all the same - always looking for handouts!"), MSGBOX_NPC)
    end
}


script GalecrestCity_NPC_Ranger{ 
    msgbox(format("I'm a Pokemon Ranger. What am I doing? Ranger duty of course! I'm cleaning up the city. There's so much junk lying around!"), MSGBOX_NPC)
    end
}


script GalecrestCity_NPC_Red{
    msgbox(format("Sorry trainer, the Gym Leader's not in right now. He should be back soon. Check out the Detective Academy in the meantime!"), MSGBOX_NPC)
    applymovement(38, moves(face_down))
    waitmovement(0)
    end
}



