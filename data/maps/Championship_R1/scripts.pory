mapscripts Championship_R1_MapScripts{
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_TEMP_0, 0: Championship_R1_OnWarp
    ]

    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_TEMP_1, 0: Championship_R1 
    ]
}

script Championship_R1_OnWarp{
    setvar(VAR_TEMP_0, 1)
}

script Championship_R1{
    lockall

    //vars and stuff
    setvar(VAR_TEMP_1, 1)
    setdynamicwarp(MAP_CHAMPIONSHIP_R1_LOBBY, 4, 13)

    setrespawn(HEAL_LOCATION_CHAMPIONSHIP_CORRIDOR)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 4))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up walk_left * 2 walk_up * 2 face_right))
    waitmovement(0)
    delay(16)
    
    msgbox_delay("Announcer", format("Welcome one and all to the Cormoria Pokemon Championship!\pThe first round, the round of 32, is set to begin shortly.\pLet's hear a huge cheer for the trainers! Show your support!"))

    delay(24)

    applymovement(1, moves(jump_in_place_left))
    msgbox_delay("Chara", format("I bet they're all cheering outside, but we can't hear anything here."))

    msgbox_delay("Announcer", format("Trainers - are you ready?"))

    applymovement(1, moves(jump_in_place_left))
    msgbox_delay("Chara", format("Before we start, do you recognise me? We met on Route 3.\pThen again at Mt. Mirroh!\nI keep running into you.\pI've been all over Cormoria with my Altaria and grown a lot. I've become so much stronger than I was back then.\pMy sister's watching this match live on television. Now I can finally take revenge for her!"))

    trainerbattle_no_intro(TRAINER_CHAMPIONSHIP_A, format("So this is the Pokemon Championship..."))

    //opponent exit
    applymovement(1, moves(jump_in_place_left))
    msgbox_delay("Chara", format("That was such a tough battle!\nYou're a really strong trainer.\pBut you know what? I didn't back down even one bit! That's enough to make my sister proud, right?\pGood luck!"))    
    
    playmoncry(SPECIES_ALTARIA, CRY_MODE_NORMAL)
    applymovement(2, moves(jump_in_place_left))
    waitmovement(0)
    delay(24)

    applymovement(1, moves(walk_left walk_down))
    waitmovement(0)

    applymovement(2, moves(walk_left walk_down walk_left))
    applymovement(1, moves(walk_down walk_left walk_down))
    waitmovement(0)
    removeobject(1)

    applymovement(2, moves(walk_down))
    waitmovement(0)
    removeobject(2)


    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left * 2 walk_down))
    waitmovement(0)
    special(RemoveCameraObject)

    setvar(VAR_CHAMP_STAGE, 2)
    setflag(FLAG_BEAT_R1)

    releaseall
    end

}


