mapscripts CarabrueTown_TenebrisLab_MapScripts {
    
   //MAP_SCRIPT_ON_RESUME: CarabrueTown_TenebrisLab_OnResume

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_LAB_STATE, 0: CarabrueTown_TenebrisLab_EventScript_Start
        VAR_LAB_STATE, 2: CarabrueTown_TenebrisLab_EventScript_NoSupplies        
    ]

    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_TEMP_1, 0: CarabrueTown_TenebrisLab_EventScript_OnWarp
    ]
}
script CarabrueTown_TenebrisLab_EventScript_OnWarp{
    removeobject(8)
    removeobject(9)
    removeobject(10)
}   

script CarabrueTown_TenebrisLab_EventScript_Start {

    lockall
    speakername("???")     
    msgbox(format("Ah! There you are! Finally."))
    release

    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 10))
    waitmovement(0)
    speakername("Rue") 
    msgbox(format("I'm Assistant Professor Rue and welcome to the Tenebris Laboratory...\l...er...{PLAYER}, was it?"))
    release
    
    applymovement(3, moves(face_left))
    waitmovement(3)
    delay(20)
    applymovement(3, moves(face_right))
    waitmovement(3)
    delay(20)
    applymovement(3, moves(face_down))
    waitmovement(3)
    delay(16)

    speakername("Rue")
    msgbox(format("I'm sorry for the mess...we're scrambling to get things done, what with the Professor gone missing and everything.\pDid you bring your welcome package?\pYes?\pOh wonderful! You see, we sent you the wrong package. The one we sent you is the evidence bag for the police!\pThis bag here is your welcome package.\nGo on - open it!"))
    release

    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left face_up))
    waitmovement(0)

    setflag(FLAG_REAL_PACKAGE_GET)
    giveitem(ITEM_POKE_BALL, 5)
    giveitem(ITEM_POTION, 1)   
    giveitem(ITEM_TOWN_MAP)  
    release
    removeobject(5)

    delay(8)

    applymovement(3, moves(jump_in_place_down))
    waitmovement(0)
    delay(16)

    speakername("Rue")
    msgbox(format("Standard supplies for a research associate! Now as for the evidence bag, we'll have to hand that over to the police as quickly as possible."))
    release

    applymovement(3, moves(walk_down face_left))
    waitmovement(3)

    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
    waitmovement(0)

    speakername("Rue")
    msgbox(format("Right, now normally I would brief you on your work, but we've got no time.\pSo I'm just going to have you start working right away, okay?\pWe've been running low on supplies and the nearest Poké Mart is in Fennilahl Town, past Route 1.\pPlease go and pick up our shipment and bring it back.\pBe careful! There are some important things in there."))
    release

    applymovement(3, moves(jump_in_place_left))
    waitmovement(3)
    applymovement(3, moves(face_left, face_right, face_up, face_down, jump_in_place_up_down))
    waitmovement(3)

    speakername("Rue")
    msgbox(format("You'll need a companion Pokémon to protect you. Pick up any of the Pokéballs lying around the lab.\pIf we had our supplies, I could give you the standard companion Pokémon, but without Professor Tenebris, everything's gone haywire here.\pYou'll have to make do with what you find lying around.\pBut once you bring the supplies from the Poké Mart, we'll pair you with the Pokémon you're meant to be with."))
    release

    applymovement(3, moves(jump_in_place_down))
    waitmovement(3)   
    speakername("Rue")
    msgbox(format("Argh! It's all a mess!"))
    release


    //Progress quest
    subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_LAB_FIRST_DAY, SUB_QUEST_2)
    
    //Set var and end
    setvar(VAR_LAB_STATE, 1)
    releaseall

}


script CarabrueTown_TenebrisLab_EventScript_ByTheWayGiveToDetectives{
    lockall
    addobject(3)
    speakername("Rue")
    message(format("Wait! I almost forgot!"))
    applymovement(3, moves(walk_down * 8))
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up))
    speakername("Rue")
    msgbox(format("On the way, could you give the evidence bag you have to the detectives?\lThey're waiting by the Route 1 exit."))
    
    release
    applymovement(3, moves(walk_up * 5))
    waitmovement(0)    
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    waitmovement(0)

    setvar(VAR_LAB_STATE, 2)
    releaseall
    end
}





script CarabrueTown_TenebrisLab_EventScript_NoSupplies {
    if (var(VAR_FENNILAHL_MART_SUPPLIES) == 0){
        end
    } else {

        lockall
        addobject(9)
        addobject(8)

        speakername("Rue")
        msgbox(format("Good, you're back. Come here."))
        release

        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up * 10))
        waitmovement(0)
        applymovement(3, moves(face_left))
        waitmovement(0)
        release
        delay(16)
        speakername("Rue")
        msgbox(format("Detectives, meet {PLAYER}, our newest assistant."))
        release
        delay(16)
        speakername("Detective")
        msgbox(format("We've met already."))

        applymovement(3, moves(face_down))
        waitmovement(0)
        release
        delay(16)
        speakername("Rue")
        msgbox(format("{PLAYER}, did you get the supplies?\pNo?\pWhat happened?"))
        release
        
        applymovement(3, moves(emote_exclamation_mark jump_in_place_down))
        waitmovement(0)
        delay(16)
        speakername("Rue")
        msgbox(format("WHAT?! Professor Tenebris took them?\pSigh...thanks for telling me, {PLAYER}."))
        release

            //Complete previous quest
            completequest(QUEST_LAB_FIRST_DAY)
            release

        delay(16)

        speakername("Rue")
        msgbox(format("But how? When? This is a disaster...\pYou see, those supplies contained something very precious."))
        release
        delay(12)
        applymovement(3, moves(face_left emote_question_mark))
        waitmovement(0)
        delay(16)
        speakername("Rue")
        msgbox(format("Have you ever heard of something\ncalled a dreamstone?\pNo?\pHardly surprising, considering that there are only three in all of Cormoria.\nYou see..."))
        release

        applymovement(3, moves(face_down))
        waitmovement(0)
        delay(16)

        speakername("Rue")
        msgbox(format("Our lab specialises in the research of Pokémon psychology - of which the Professor was a leading expert, as you no doubt know. In particular, a Pokémon's sleeping subconscious psyche is our field of interest."))
        release

        applymovement(3, moves(walk_left walk_down *2 walk_right*2))
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
        waitmovement(0)

        speakername("Rue")
        msgbox(format("This right here is our lab's pride and joy - the Dreamalyser. Once powered, it'll allow us to study what Pokémon undergo while in deep sleep.\lAnd something like this can't be powered using conventional methods."))
        release

        applymovement(3, moves(face_left))
        waitmovement(0)
        delay(20)

        speakername("Rue")
        msgbox(format("No...this thing needs something far more potent. Our plan was to use dreamstones to power it.\pDreamstones are powerful reserves of subconscious Pokémon energy.\pWe don't know yet how they came to be, and so far, we know of only three across all of Cormoria.\pIncredibly rare, you see?\pAnd power like that in the wrong hands..."))
        release

        applymovement(3, moves(walk_left face_up))
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
        waitmovement(0)

        speakername("Rue")
        msgbox(format("This month, we were supposed to receive two things in our supplies - {PLAYER}, your Pokémon...and one dreamstone.\pOne of only three that exist.\pAnd just as we would have powered up the Dreamalyser for the first time ever, the Professor took it and disappeared!\pOh, why did she leave like that?\nWhat on earth was she thinking!?"))
        release
        delay(16)
        
        speakername("Rue")
        msgbox(format("Sorry {PLAYER}...I fear that destiny has chosen your companion for you.\lBut it looks like you two have formed a bond already...this may be for the best."))  
        release
        delay(20)
        
        speakername("Detective")
        msgbox(format("If we could get our hands on one of the other dreamstones, we might be able to understand why the Professor vanished."))
        release

        applymovement(3, moves(walk_left *2 face_up))
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))
        waitmovement(0)    
        applymovement(9, moves(face_down))
        waitmovement(0)
        applymovement(8, moves(face_down))
        waitmovement(0)    

        speakername("Rue")
        msgbox(format("Detectives...please. You must find the Professor and the dreamstone!"))
        speakername("Detective")
        msgbox(format("Finding her is what we're here for. Again, I can't shake the hunch that the other dreamstones could give us a clue."))
        release
        delay(16)        
        applymovement(3, moves(jump_in_place_up))
        waitmovement(0)
        delay(16)
        speakername("Rue")
        msgbox(format("Whatever you need to do!"))
        release
        delay(16)
        speakername("Detective")
        msgbox(format("We'll do our best, Rue. But we're detectives - we can uncover her tracks, but we can't unravel her mind.\lWe'll need some help from one of you."))
        release
        delay(16)
        speakername("Rue")
        msgbox(format("What do you plan to do?"))
        release
        delay(16)
        speakername("Detective")
        msgbox(format("While we track the Professor, one of you should find a dreamstone for us to examine for clues."))
        release
        delay(16)
        speakername("Rue")
        msgbox(format("But none of us can leave the lab! There's too much work."))
        release
        delay(16)
        speakername("Detective")
        msgbox(format("Well...what about the kid?"))
        release
        delay(24)
        applymovement(9, moves(face_right))
        applymovement(8, moves(face_right))
        applymovement(3, moves(face_right))
        waitmovement(0) 
        delay(64)

        applymovement(9, moves(face_down))
        applymovement(8, moves(face_down))
        applymovement(3, moves(face_up))
        waitmovement(0) 
        delay(30)

        speakername("Rue")
        msgbox(format("{PLAYER}? It's too dangerous. {PLAYER}'s Pokémon aren't strong enough yet."))
        release
        delay(16)
        speakername("Detective")
        msgbox(format("What else do you suggest?\n{PLAYER} is the best we've got."))
        release
        delay(16)
        speakername("Rue")
        msgbox(format("Right...yes. You're right."))
        release
        delay(16)

        applymovement(9, moves(face_right))
        waitmovement(0)
        applymovement(8, moves(face_right))
        waitmovement(0)    
        applymovement(3, moves(walk_right *2 face_up))
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
        waitmovement(0)
        speakername("Rue")
        msgbox(format("{PLAYER} - I know it's only your first day, but this is a critical task, and none of us can leave the lab.\pThe dreamstones are scattered all across Cormoria. Have you ever\ltraveled beyond Fennilahl Town?\pCormoria is vast and filled with dangerous Pokémon. I fear you may\lnot be up to the task..."))
        release
        delay(16)

        speakername("Rue")
        msgbox(format("I don't know how I can help...perhaps this may be of use to you."))
        release   
        //Pokedex Get
        playfanfare(MUS_OBTAIN_ITEM)
        msgbox(format("{PLAYER} received a Pokedex from Assistant Professor Rue."))
        waitfanfare  
        setflag(FLAG_SYS_POKEDEX_GET)

        speakername("Rue")
        msgbox(format("It's a Pokedex. It has information on Pokémon you've encountered. You can see its moves, how it evolves, and more!"))
        release

        applymovement(3, moves(walk_left walk_up *2 walk_right face_down))
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
        waitmovement(0)

        speakername("Rue")
        msgbox(format("Sigh...the Professor missing. The dreamstone missing. The lab in shambles. My youngest researcher sent off\lon a dangerous journey."))
        release
        delay(36)
        speakername("Rue")
        msgbox(format("I've failed."))
        release
        delay(16)
        applymovement(8, moves(emote_exclamation_mark))
        applymovement(9, moves(emote_exclamation_mark))
        waitmovement(0)
        delay(16)
        speakername("Detective")
        msgbox(format("Get it together, Rue. The other researchers are counting on you.\lHave some faith in us...and...\p...well...\p...{PLAYER}."))
        release
        delay(16)

        //Rival comes in (Wally)
        playbgm(MUS_RG_ENCOUNTER_RIVAL, FALSE)
        speakername("???")
        msgbox(format("Wait right there!"))    
        addobject(10)
        applymovement(10, moves(walk_fast_up * 10 face_left))
        waitmovement(0)    
        release
        applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))
        waitmovement(0)      
        delay(16)
        speakername("???")
        msgbox(format("Sir, I overheard the whole thing!\nAnd I protest!"))    
        release
        delay(16)

        applymovement(10, moves(face_right))
        waitmovement(0) 
        speakername("???")
        msgbox(format("You can't give this kid a job like that! What's your name?\p{PLAYER}?\pPshh, whatever. Look at you! Your Pokémon look exhausted after a\lsingle trip to Fennilahl Town!"))  
        release
        delay(8)

        applymovement(10, moves(face_left))
        waitmovement(0) 
        delay(8)

        speakername("???")
        msgbox(format("Screw this kid sir! Let ME handle the dreamstones!"))  
        release
        delay(16)
        applymovement(3, moves(emote_question_mark))
        waitmovement(0)
        delay(8)

        speakername("Rue")
        msgbox(format("Excuse me..."))  
        release
        delay(16)
        speakername("Detective")
        msgbox(format("Meet Kohla, this year's Ace Rookie\nfrom the Detective Academy."))  
        release
        delay(16)
        applymovement(10, moves(jump_in_place_right))
        waitmovement(0) 
        delay(16)

        speakername("Kohla")
        msgbox(format("Heard that {PLAYER}? ACE. ROOKIE. This is serious business. You can't handle this. Back off!"))  
        release
        delay(16)
        speakername("Detective")
        msgbox(format("Now hold on Kohla, there's no need for all this. {PLAYER} is representing the Professor's lab. We need someone from the lab to help track the dreamstones."))  
        release
        applymovement(10, moves(face_left))
        waitmovement(0)     
        delay(16)
        speakername("Kohla")
        msgbox(format("I can do it sir!\nAnd I'll prove it right now!")) 
        release
        delay(16)
        applymovement(10, moves(jump_in_place_right))
        waitmovement(0) 
        delay(16)
        speakername("Kohla")
        msgbox(format("C'mon {PLAYER}, let's see who's a better fit for the job!"))  
        release

        //Trainer battle
        trainerbattle_no_intro(TRAINER_KOHLA_LAB, "You won't be this lucky next time!")
        playbgm(MUS_RG_RIVAL_EXIT, FALSE)        
        applymovement(10, moves(emote_exclamation_mark))   
        speakername("Kohla")     
        msgbox(format("Pah! A single battle means nothing!\nYou haven't been through what I have. It takes effort and discipline to become an Ace Rookie. Just you wait - I'll be the one to find the dreamstone!"))
        release
        delay(16)
        applymovement(10, moves(face_left))
        waitmovement(0)     
        delay(16)
        speakername("Kohla")
        msgbox(format("Over and out, sir!")) 
        release

        applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
        waitmovement(0)  
        fadedefaultbgm
        applymovement(10, moves(walk_fast_down * 10))
        waitmovement(0)
        removeobject(10)        

        applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))
        waitmovement(0)  
        speakername("Detective")
        msgbox(format("Well Rue, it looks like your assistant's got some backbone.\p{PLAYER}, we're counting on you and Kohla to get the dreamstone while we track Professor Tenebris.\pRue, do you know anything of the whereabouts of the other dreamstones?"))  
        release
        delay(16)
        speakername("Rue")
        msgbox(format("One lies somewhere inside Mt. Ceram. {PLAYER}, Mt. Ceram is further east from Fennilahl Town. You'll have to reach Gastree City, and then make your way up north towards the mountain."))  
        release
        delay(16)
        speakername("Detective")
        msgbox(format("{PLAYER} - you know what to do. Head east to Gastree City. From there, make your way to Mt. Ceram and find the dreamstone that's there."))  
        release
        delay(16)
        speakername("Rue")
        msgbox(format("I'll let your mother know that you've been assigned some travel work.\pI wish you all the best."))  

        //VARS, FLAGS AND QUESTS
        setvar(VAR_LAB_STATE, 3)
        setflag(FLAG_TENEBRIS_POLICE_PRESCENCE)
        setflag(FLAG_LAB_CALLTOACTION)

        //Set quest active
            //Start next quest
            startquest(QUEST_DREAMSTONE_MYSTERIES)
            subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_DREAMSTONE_MYSTERIES, SUB_QUEST_1)
            
        releaseall
    }
}


script CarabrueTown_TenebrisLab_EventScript_DetectiveGeneric{
    speakername("Detective")
    msgbox(format("Don't let one victory get to your head. Kohla's already got a head start.\lBetter hurry if you want to catch up!"), MSGBOX_NPC)
    end
}











script CarabrueTown_TenebrisLab_EventScript_Pokeball_One{
 
    lockall

    if (flag(FLAG_UNUSED_0x020)){
        msgbox(format("One of the Lab's Pokémon."))
    } else {

        showmonpic(SPECIES_GOTHITA, 10,3)
        playmoncry(SPECIES_GOTHITA, CRY_MODE_NORMAL)
        msgbox("Do you want to choose Gothita?", MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES){
            removeobject(4)
            givemon(SPECIES_GOTHITA, 5, ITEM_ORAN_BERRY)
            setflag(FLAG_SYS_POKEMON_GET)
            if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
                playfanfare(MUS_OBTAIN_ITEM)
                msgbox(format("The Tenebris Laboratory has gifted you a Gothita!"))
                hidemonpic
                removeobject(1)
                setflag(FLAG_UNUSED_0x020)
                call(NicknamePartyMon)
            }
        }
        else{
            hidemonpic
        }
    }

    waitmessage
    releaseall
    end
 
}

script CarabrueTown_TenebrisLab_EventScript_Pokeball_Two{
 
    lockall

    if (flag(FLAG_UNUSED_0x020)){
        msgbox(format("One of the Lab's Pokémon."))
    } else {

        showmonpic(SPECIES_TIMBURR, 10,3)
        playmoncry(SPECIES_TIMBURR, CRY_MODE_NORMAL)
        msgbox("Do you want to choose Timburr?", MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES){
            givemon(SPECIES_TIMBURR, 5, ITEM_ORAN_BERRY)
            setflag(FLAG_SYS_POKEMON_GET)
            removeobject(7)
            if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
                playfanfare(MUS_OBTAIN_ITEM)
                msgbox(format("The Tenebris Laboratory has gifted you a Timburr!"))
                hidemonpic
                removeobject(1)
                setflag(FLAG_UNUSED_0x020)
                call(NicknamePartyMon)
            }
        }
        else{
            hidemonpic
        }
    }

    waitmessage
    releaseall
    end
 
}

script CarabrueTown_TenebrisLab_EventScript_Pokeball_Three{
 
    lockall

    if (flag(FLAG_UNUSED_0x020)){
        msgbox(format("One of the Lab's Pokémon."))
    } else {

        showmonpic(SPECIES_ZIGZAGOON_GALAR, 10,3)
        playmoncry(SPECIES_ZIGZAGOON_GALAR, CRY_MODE_NORMAL)
        msgbox("Do you want to choose Zigzagoon?", MSGBOX_YESNO)
        if(var(VAR_RESULT) == YES){
            givemon(SPECIES_ZIGZAGOON_GALAR, 5, ITEM_ORAN_BERRY)
            setflag(FLAG_SYS_POKEMON_GET)
            removeobject(6)
            if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
                playfanfare(MUS_OBTAIN_ITEM)
                msgbox(format("The Tenebris Laboratory has gifted you a Zigzagoon!"))
                hidemonpic
                removeobject(1)
                setflag(FLAG_UNUSED_0x020)
                call(NicknamePartyMon)
            }
        }
        else{
            hidemonpic
        }
    }

    waitmessage
    releaseall
    end
 
}

script NicknamePartyMon{
    msgbox(format("You can nickname your Pokémon\nvia the summary screen!"), MSGBOX_POKENAV)
    release

    // msgbox("Give it a nickname?", MSGBOX_YESNO)
    // hidemonpic
    // if(var(VAR_RESULT) == YES){
    //     call(Common_EventScript_GetGiftMonPartySlot)
    //     call(Common_EventScript_NameReceivedPartyMon)
    // }
 
}
 
script NicknameBoxMon{
 
    msgbox("Give it a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_NameReceivedBoxMon)
    }
    call(Common_EventScript_TransferredToPC)
 
}

script CarabrueTown_TenebrisLab_EventScript_LeaveWithoutPokemon{

    lockall

    if (!flag(FLAG_UNUSED_0x020)){
        msgbox(format("Assistant Professor Rue told me not to leave without a Pokémon to protect me!"))
        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up))
        waitmovement(0)
        releaseall
        end
    }

    if (var(VAR_LAB_STATE) == 1){
        goto(CarabrueTown_TenebrisLab_EventScript_ByTheWayGiveToDetectives)
    }

    releaseall
    end

}

script CarabrueTown_TenebrisLab_EventScript_Rue{
    speakername("Rue")
    msgbox(format("We're counting on you!"), MSGBOX_NPC)
    end
}


script CarabrueTown_TenebrisLab_EventScript_Scientist1{
    if (flag(FLAG_POST_FINALE_CUTSCENE)){
        msgbox(format("I can't believe Rue was behind this whole mess! I wish I'd caught on sooner!"))
        end
    }

    msgbox(format("There's so much work that I don't even know where to start! So I'm just staring at all these books instead."), MSGBOX_NPC)
    end
}

script CarabrueTown_TenebrisLab_EventScript_Scientist2{
    if (flag(FLAG_POST_FINALE_CUTSCENE)){
        msgbox(format("The detectives told us the entire story. You're incredible, {PLAYER}!"))
        end
    }

    msgbox(format("I'm so grateful we have Assistant Professor Rue. Without him to step in for Professor Tenebris, we'd have had to shut the lab down."), MSGBOX_NPC)
    end
}

script CarabrueTown_TenebrisLab_EventScript_Scientist3{
    if (flag(FLAG_POST_FINALE_CUTSCENE)){
        msgbox(format("Go on, {PLAYER}! Bring the championship trophy to Carabrue Town!"))
        end
    }
    
    if (flag(FLAG_LAB_CALLTOACTION)){
        msgbox(format("A bit unorthodox for a researcher, but you and your new partner are looking good together!"), MSGBOX_NPC)
    } else {
        msgbox(format("I'm sorry we couldn't offer you the standard companion Pokémon.\pBut they're coming in the next batch of supplies! Just be patient."), MSGBOX_NPC)
    }
    end
}



script CarabrueTown_TenebrisLab_Gardevoir{
    if (flag(FLAG_TENEBRIS_GARDEVOIR)){
        msgbox(format("Gardevoir regards you intently..."), MSGBOX_NPC)  
    } else {
        playmoncry(SPECIES_GARDEVOIR, CRY_MODE_NORMAL)
        msgbox(format("Gardevoir regards you intently...\pSomething floats into your hand!"), MSGBOX_NPC)
        lockall
        giveitem(ITEM_STARF_BERRY)
        msgbox(format("Was this her way of welcoming a new member of the lab?\pThank you, Gardevoir!"))
        setflag(FLAG_TENEBRIS_GARDEVOIR)              
        releaseall     
    }
    end
}

script CarabrueTown_TenebrisLab_Metagross{
    playmoncry(SPECIES_METAGROSS, CRY_MODE_NORMAL)
    msgbox(format("It's Asst. Prof. Rue's Metagross. It's so intimidating!"), MSGBOX_NPC)
    end
}


script CarabrueTown_TenebrisLab_Tenebris{
    msgbox_delay("Tenebris", format("Go {PLAYER} go!"))
    end
}

