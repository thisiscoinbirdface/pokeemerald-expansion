mapscripts Championship_R2_MapScripts{    
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_TEMP_0, 0: Championship_R2_OnWarp
    ]

    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_TEMP_1, 0: Championship_R2 
    ]
}

script Championship_R2_OnWarp{
    setvar(VAR_TEMP_0, 1)
}

script Championship_R2{
    lockall

    //vars and stuff
    setvar(VAR_TEMP_1, 1)
    setdynamicwarp(MAP_CHAMPIONSHIP_R2_LOBBY, 4, 13)

    //main cutscene
    special(SpawnCameraObject)

    setrespawn(HEAL_LOCATION_CHAMPIONSHIP_CORRIDOR)

    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_up * 4))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up walk_left * 2 walk_up * 2 face_right))
    waitmovement(0)
    delay(16)
    
    msgbox_delay("Announcer", format("We're back with the round of 16 in the Cormoria Pokemon Championship!\pLet's hear a huge cheer for the trainers! Show your support!\pThe previous round featured some explosive matches, with crowd favourite Kohla bulldozing..."))

    applymovement(1, moves(jump_in_place_left))
    msgbox_delay("Stanja", format("How pointless - praising an early victory when the only thing that matters is who becomes the Champion!"))

    msgbox_delay("Announcer", format("Trainers - are you ready?"))

    applymovement(1, moves(jump_in_place_left))
    msgbox_delay("Stanja", format("I'm Stanja, the ultimate Bug Maniac!\pMy goal was to become a Gym Leader, but then do you know what happened?\pI beat them all! And then I realised that Pinsir and I had what it takes to become the Champion.\pI know you. I've lost to you before.\pWe fought in Vilethorn Woods.\nAnd then again in the Ivy River.\pBut this battle is different.\nThe stakes are different.\pI'M different!"))

    trainerbattle_no_intro(TRAINER_CHAMPIONSHIP_B, format("I was so close!"))

    //opponent exit
    applymovement(1, moves(jump_in_place_left))
    msgbox_delay("Stanja", format("Fine. Fine! FINE!\pI know when I'm beat.\nYou were the better trainer.\pI've beaten countless trainers with my Pinsir, but this time, we couldn't pull through.\pI guess this is the caliber of the Pokemon Championship...\pGood luck, {PLAYER}. You're tough - I'm rooting for you!"))    
    
    playmoncry(SPECIES_PINSIR, CRY_MODE_NORMAL)
    applymovement(2, moves(jump_in_place_left))
    waitmovement(0)
    delay(24)

    applymovement(1, moves(walk_left walk_down))
    waitmovement(0)

    applymovement(2, moves(walk_left walk_down walk_left))
    applymovement(1, moves(walk_down walk_left walk_down))
    waitmovement(0)
    removeobject(1)

    applymovement(2, moves(walk_down))
    waitmovement(0)
    removeobject(2)

    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, moves(walk_left * 2 walk_down))
    waitmovement(0)
    special(RemoveCameraObject)

    setvar(VAR_CHAMP_STAGE, 3)
    setflag(FLAG_BEAT_R2)

    releaseall
    end

}


