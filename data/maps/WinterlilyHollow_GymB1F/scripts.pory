mapscripts WinterlilyHollow_GymB1F_MapScripts{
    MAP_SCRIPT_ON_TRANSITION {
        goto(WinterlilyHollow_GymB1F_SetLayout)
    }
}

//
//VAR_TEMP_0 = the map layout (0 for normal, 1 for switched)

script WinterlilyHollow_GymB1F_Button{
    lockall
    playse(SE_CLICK)
    getplayerxy(VAR_TEMP_2, VAR_TEMP_3)
    warp(MAP_WINTERLILY_HOLLOW_GYM_B1F, VAR_TEMP_2, VAR_TEMP_3)
    releaseall
    end
}

script WinterlilyHollow_GymB1F_FinalButton{
    lockall
    msgbox(format("{PLAYER} found the master switch!\nThis will open all the doors."))
    playse(SE_CLICK)
    getplayerxy(VAR_TEMP_2, VAR_TEMP_3)
    warp(MAP_WINTERLILY_HOLLOW_GYM_B1F, VAR_TEMP_2, VAR_TEMP_3)
    releaseall
    end
}

script WinterlilyHollow_GymB1F_SetLayout{
    if (var(VAR_HOLLOW_GYM_LAYOUT) == 2){
        setmaplayoutindex(LAYOUT_WINTERLILY_HOLLOW_GYM_B1F_OPEN)
        return
    }

    if (var(VAR_HOLLOW_GYM_LAYOUT) == 0){
        setmaplayoutindex(LAYOUT_WINTERLILY_HOLLOW_GYM_B1F)
        setvar(VAR_HOLLOW_GYM_LAYOUT, 1)
    } else {
        setmaplayoutindex(LAYOUT_WINTERLILY_HOLLOW_GYM_B1F_SWITCHED)        
        setvar(VAR_HOLLOW_GYM_LAYOUT, 0)
    }

    return
}

script WinterlilyHollow_GymB1F_SetPermaOpenDoors{
    lockall
    setvar(VAR_HOLLOW_GYM_LAYOUT, 2)
    releaseall
    end
}


//6 trainers
//ELECTRICIAN
script WinterlilyHollow_Gym_Trainer_A{
    trainerbattle_single(TRAINER_WINTERLILY_HOLLOW_GYM_A, format("What's a civilian doing here?\nThe Electric Terrain is dangerous!"), "I guess you're okay!")
    msgbox(format("Leader {COLOR BLUE}Viniel{COLOR DARK_GRAY}'s Manectric overloads the atmosphere with ions, creating electric terrain here."))
    releaseall
    end
}

//ELECTRICIAN
script WinterlilyHollow_Gym_Trainer_B{
    trainerbattle_single(TRAINER_WINTERLILY_HOLLOW_GYM_B, format("This is a power plant that {COLOR BLUE}Viniel{COLOR DARK_GRAY} set up. While trainers come and go, us Electricians work around the clock to serve {COLOR GREEN}Winterlily Hollow{COLOR DARK_GRAY}!"), "Alright, carry on.")
    msgbox(format("This is a power plant that {COLOR BLUE}Viniel{COLOR DARK_GRAY} set up. While trainers come and go, us Electricians work around the clock to serve {COLOR GREEN}Winterlily Hollow{COLOR DARK_GRAY}!"))
    releaseall
    end
}

//BATTLE GIRL
script WinterlilyHollow_Gym_Trainer_C{
    trainerbattle_single(TRAINER_WINTERLILY_HOLLOW_GYM_C, format("Challenge {COLOR BLUE}Viniel{COLOR DARK_GRAY}? Pah!\nMy Pokémon are rarer than his!"), "Wow, you're good!")
    msgbox(format("I wanted to show my Qwilfish off to {COLOR BLUE}Viniel{COLOR DARK_GRAY}, but I can't find the right switches!"))
    releaseall
    end
}

//EMPLOYEE
script WinterlilyHollow_Gym_Trainer_D{
    trainerbattle_single(TRAINER_WINTERLILY_HOLLOW_GYM_D, format("What - you thought everyone would be a lumbering electrician? Someone's gotta keep the books too!"), "Back to the books then!")
    msgbox(format("With so much electricity being generated, we do need someone to keep track of everything."))
    releaseall
    end
}

//SCIENTIST
script WinterlilyHollow_Gym_Trainer_E{
    trainerbattle_single(TRAINER_WINTERLILY_HOLLOW_GYM_E, format("Viniel hired me to research efficient electricity storage, but all I've been doing is battling trainers like you!"), "Another trainer I lost to!")
    msgbox(format("We need more robust electricity storage solutions."))
    releaseall
    end
}


script WinterlilyHollow_LastTrainer_Trigger{
    if (flag(FLAG_WINTERLILY_GYM_TRICK_TRAINER)) {end}
    lockall
    setflag(FLAG_WINTERLILY_GYM_TRICK_TRAINER)
    applymovement(6, moves(emote_exclamation_mark jump_in_place_left))
    delay(30)
    msgbox(format("Oh, a trainer?\pDon't worry, I won't fight you.\nI'm just fixing up the wiring here.\pGo on, flip the switch."), MSGBOX_NPC)
    end
}

//ELECTRICIAN
script WinterlilyHollow_Gym_Trainer_F{
    trainerbattle_single(TRAINER_WINTERLILY_HOLLOW_GYM_F, format("Hah! Gotcha! I'm about to crush you!"), "You crushed me!")
    msgbox(format("Leader {COLOR BLUE}Viniel{COLOR DARK_GRAY}'s right up ahead.\nHe put me here to make sure weak\ltrainers don't disturb his Manectric."))
    releaseall
    end
}




//Helper NPC
script WinterlilyHollow_Gym_NPC1{
    if (var(VAR_HOLLOW_GYM_LAYOUT) == 0){
        msgbox(format("You opened the door to {COLOR BLUE}Viniel{COLOR DARK_GRAY}!\pI'll only come once I figure the switch order out on my own."), MSGBOX_NPC)
    } else {
        msgbox(format("This door takes you to {COLOR BLUE}Viniel{COLOR DARK_GRAY}, but I don't know how to get here when it's open.\pAt this rate, I'll never get my badge!\pA battle?\pI'm too frustrated to battle!"), MSGBOX_NPC)
    }
    end
}




















script WinterlilyHollowGym_LeaderBattle {
    lockall

    if (flag(FLAG_BADGE06_GET)){
        faceplayer
        speakername("Viniel")
        msgbox(format("That was a great battle. You've earned my confidence. Go get 'em, {PLAYER}!"))
    } else { //else do the battle
        //savegame
        call(Common_EventScript_SaveGameBeforeBattle)

        speakername("Viniel")
        msgbox(format("Go Manectric! More! More!\pWait - it looks like we've got a guest."))
        faceplayer
        speakername("Viniel")
        msgbox(format("Ah, it's you! The enemy of my enemy.\pHmm...\pYou still look a little drained.\nDo you really want to battle?"), MSGBOX_YESNO)

        if (var(VAR_RESULT) == 0){
            speakername("Viniel")
            msgbox(format("Come again when you've got the\nspark back in you!"), MSGBOX_NPC)
            end
        }
        speakername("Viniel")
        msgbox(format("Excellent! I can hear your internal battery crackling with energy.\pLook around you, {PLAYER}. This power plant is the lifeblood of {COLOR GREEN}Winterlily Hollow{COLOR DARK_GRAY}. Gym Leaders aren't just strong trainers. We're here to do the best we can for those who can't.\pThat's what makes us, us.\nHahaha!\pBack with those {COLOR BLUE}Team Somber{COLOR DARK_GRAY} grunts, you showed me the same spirit.\pAnd now, I want to see you to bring it out again!"))

        trainerbattle_no_intro(TRAINER_WINTERLILY_HOLLOW_GYM_LEADER, format("Sizzling! Crackling! Hahaha!"))
        
        //Receive the badge
        message(format("{PLAYER} received the Spark Badge from {COLOR BLUE}Viniel{COLOR DARK_GRAY}."))
        waitmessage
        //Play gym badge fanfare
        call(Common_EventScript_PlayGymBadgeFanfare)

        setflag(FLAG_BADGE06_GET)
        setflag(FLAG_SYS_SURF_ENABLED)

        speakername("Viniel")
        msgbox(format("And I'd also like you to have this."))
        release
        //Give TM also
        giveitem(ITEM_RARE_SHARD)
        release

        //Bye-bye
        speakername("Viniel")
        msgbox(format("Since you're off the case now, why don't you take part in the Pokémon Championship this year? You need two more badges - but I warn you! {COLOR BLUE}Jania{COLOR DARK_GRAY} and {COLOR RED}Raazi{COLOR DARK_GRAY} are the strongest Gym Leaders in all of Cormoria!\pIf you want to try, I suggest you head back down to {COLOR BLUE}Pelluca City{COLOR DARK_GRAY}.\lYou'll find the next gym there.\pYou can go down the {COLOR BLUE}Ivy River{COLOR DARK_GRAY}...\p...for which you'll need Surf!"))
        release
        giveitem(ITEM_HM03)
        speakername("Viniel")
        msgbox(format("Keep your spirits high, {PLAYER}!"))

    }

    releaseall
    end
}




script WinterlilyHollow_Gym_Manectric{
    playmoncry(SPECIES_MANECTRIC, CRY_MODE_NORMAL)
    msgbox(format("The air around Manectric is crackling with electricity!"), MSGBOX_NPC)
    end
}







script WinterlilyHollowGym_Red{
    if (flag(FLAG_BADGE06_GET)){
        if (flag(FLAG_WINTERLILYHOLLOWGYM_SPENSER_REWARD)){
            msgbox(format("Well done champ-in-the-making! Now get on out there and find the next gym - I'll see you there!"), MSGBOX_NPC)
        } else {
            lockall
            faceplayer
            msgbox(format("Well done champ-in-the-making! An electrifying showdown! In recognition of your skills, I'd like to give you something."))
            goto(WinterlilyHollowGym_Red_Reward)         
        }
    } else { 
        if (flag(FLAG_WINTERLILYHOLLOWGYM_SPENSER_WATER)){
            msgbox(format("The electricity generated in this plant ionises the air and creates electric terrain!\pSome trainers even bring Electric Seeds to boost their Pokémon in battle.\pNow go get 'em, champ-in-the-making!"), MSGBOX_NPC)
        } else {
            lockall
            faceplayer
            msgbox(format("Welcome to the {COLOR GREEN}Winterlily Hollow{COLOR DARK_GRAY} gym! It's not a gym so much as a power plant. The leader, {COLOR BLUE}Viniel{COLOR DARK_GRAY}, powers this town with his strong ELECTRIC-type Pokémon here in this plant. First, hydrate yourself!"))
            giveitem(ITEM_FRESH_WATER)
            msgbox(format("The electricity generated in this plant ionises the air and creates electric terrain!\pSome trainers even bring Electric Seeds to boost their Pokémon in battle.\pNow go get 'em, champ-in-the-making!"))
            setflag(FLAG_WINTERLILYHOLLOWGYM_SPENSER_WATER)
            releaseall
        }
    }
    end
}

script WinterlilyHollowGym_Red_Reward{
    message(format("Which of these do you fancy?"))
    waitmessage
    dynmultichoice(0,0, TRUE, 2, 0, DYN_MULTICHOICE_CB_NONE, "Electric Gem", "Magnet") 
    if (var(VAR_RESULT) == 0){setvar(VAR_0x8006, ITEM_ELECTRIC_GEM)}
    if (var(VAR_RESULT) == 1){setvar(VAR_0x8006, ITEM_MAGNET)}
    goto(WinterlilyHollowGym_Red_Reward_Give)
}

script WinterlilyHollowGym_Red_Reward_Give{
    showitemdescription
    msgbox(format("Are you sure?"), MSGBOX_YESNO)
    hideitemdescription
    if (var(VAR_RESULT) == 1){
        giveitem(VAR_0x8006) 
        setflag(FLAG_WINTERLILYHOLLOWGYM_SPENSER_REWARD) 
        goto(WinterlilyHollowGym_Red_Reward_End)} 
    else {
        goto(WinterlilyHollowGym_Red_Reward)
    }
}

script WinterlilyHollowGym_Red_Reward_End{    
    msgbox(format("Now get on out there and find the next gym - I'll see you there!"), MSGBOX_NPC)       
    end
}





