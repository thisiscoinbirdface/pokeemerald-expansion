raw `
    .set LOCALID_KOHLA, 7
    .set LOCALID_SKITTYOG, 12
    .set LOCALID_SKITTY1, 16
    .set LOCALID_SKITTY2, 17
    .set LOCALID_SKITTY3, 18
`

mapscripts GastreeCity_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: GastreeCity_OnTransition  

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_GASTREE_SKITTY_QUEST, 0: GastreeCity_EventScript_SkittyQuestStart    
    ]

}


script GastreeCity_OnTransition{

}

script GastreeCity_EventScript_KohlaBattle_Trigger{
    if (flag(FLAG_GASTREE_KOHLA_BATTLE)) {end}

    //Start
    lockall
    playbgm(MUS_RG_ENCOUNTER_RIVAL, FALSE)
    delay(16)

    //Setup intro anim
        //open door
        opendoor(23, 26)
        waitdooranim

        //kohla create
        setobjectxyperm(LOCALID_KOHLA, 23, 26)
        addobject(LOCALID_KOHLA)
        delay(8)
        //kohla walk
        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down face_up))
        waitmovement(0)
        applymovement(LOCALID_KOHLA, moves(walk_down))
        waitmovement(0)
        //close door
        closedoor(23, 26)
        waitdooranim

    delay(12)

    //Kohla talks trash
    msgbox(GastreeCity_KohlaIntro1)
    msgbox(GastreeCity_KohlaIntro2)

    //Battle
    trainerbattle_no_intro(TRAINER_GASTREE_KOHLA, msgbox(format("Not again!")))
    playbgm(MUS_RG_ENCOUNTER_RIVAL, FALSE)

    //Lose battle
    msgbox(GastreeCity_KohlaPostBattle)
    release
    
    applymovement(LOCALID_KOHLA, moves(walk_right *5 walk_up *8))
    waitmovement(0)

    //Set flag
    setflag(FLAG_GASTREE_KOHLA_BATTLE)
    removeobject(LOCALID_KOHLA)
    
    fadedefaultbgm
    releaseall
}

text GastreeCity_KohlaIntro1 {
    format("KOHLA: Well well, look who thought it was fashionable to be late! I'm way ahead of you, chum! I've already beaten Inger and got the GYM BADGE.")
}
text GastreeCity_KohlaIntro2{
    format("KOHLA: I don't mind dishing out a second serving either. This is payback for one-upping me in front of the other detectives!")
}
text GastreeCity_KohlaPostBattle{
    format("KOHLA: You may have won, but I'll get the dreamstone first! I heard that some group called Team Somber is also after the dreamstone. I'm going to head north towards the Ceram Base Camp. Don't come after me - I don't need your help. Over and out, chum!")
}



//NPCs
script GastreeCity_NPC1{
    msgbox(format("You came all the way from Carabrue Town? Woah! Then I bet you don't know - be careful on Route 4! There's a dangerouns Dewpider that attacks people."), MSGBOX_NPC)
    end
}

script GastreeCity_NPC3{
    msgbox(format("My dad drags me here every weekend and its sooo boring. I can't wait to grow up and leave the house!"), MSGBOX_NPC)
    end
}

script GastreeCity_NPC4{
    msgbox(format("I come here every weekend with my daughter for some quality family time. Oh, how I'll miss her when she grows up!"), MSGBOX_NPC)
    end
}

    //Info
script GastreeCity_NPC5{
    msgbox(format("Inger, this city's Gym Leader, uses ROCK types. I should have an advantage with my FIGHTING-type Pokémon right? So then why am I getting cold feet?"), MSGBOX_NPC)
    end
}

script GastreeCity_Mankey{
    playmoncry(SPECIES_MANKEY, CRY_MODE_NORMAL)
    msgbox(format("Mankey is getting angry at his trainer's cold feet."), MSGBOX_NPC)
    end    
}

    //Info
script GastreeCity_NPC6{  
    msgbox(format("Gastree City is famous not just for being a connecting Route to Mt. Ceram Base Camp, but also for its scenic viewpoints and its teahouse!"), MSGBOX_NPC)
    end
}

    //Pokémon flavour
script GastreeCity_NPC7{
    msgbox(format("I want to have tea too! When I get my first Pokemon, I want it to be Poltchageist."), MSGBOX_NPC)
    end
}

    //Pokémon flavour
script GastreeCity_NPC8{
    msgbox(format("Hiiiiyah!\pI'm trying to teach my KECLEON some new moves so that we can go and explore Route 4 together! C'mon KECLEON! C'mon!!"), MSGBOX_NPC)
    applymovement(5, moves(emote_exclamation_mark))
    waitmovement(0)
    msgbox(format("KECLEON: keeee!!!\pKECLEON is as fired up as its trainer is!"), MSGBOX_NPC)
    end
}

script GastreeCity_NPC_Keckleon{ 
    msgbox(format("KECKLEON: keeee!!\pKECLEON is trying hard to match its trainers energy. Go KECLEON!"), MSGBOX_NPC)
    end
}

script GastreeCity_NPC9{
    lockall
    faceplayer    
    msgbox(format("Yo, trainer! Come here. What's the most useful move a Pokémon can learn?"))
    dynmultichoice(0,0, TRUE, 4, 0, DYN_MULTICHOICE_CB_NONE, "ERUPTION", "QUIVER DANCE", "EXPANDING FORCE", "EARTHQUAKE")        
    msgbox(format("Nope - trick question! It's SPLASH!\p...\pNo, seriously. Most people think it's useless, but if you have HM SPLASH, your Pokémon can hop onto rocks in the water like the one here. But you need at least one Gym Badge to be able to use HMs."))
    releaseall
    end
}










    //Quest NPC
    script GastreeCity_EventScript_SkittyQuestStart{

        //INIT: set all flags remove all skittys
        setflag(FLAG_HIDE_GASTREE_SKITTY1)
        setflag(FLAG_HIDE_GASTREE_SKITTY2)
        setflag(FLAG_HIDE_GASTREE_SKITTY3)

        removeobject(LOCALID_SKITTY1)
        removeobject(LOCALID_SKITTY2)
        removeobject(LOCALID_SKITTY3)

        
        lockall
        delay(12)
        applymovement(3, moves(emote_exclamation_mark))
        waitmovement(0)
        msgbox(format("No! Skitty!"))
        release
        applymovement(LOCALID_SKITTYOG, moves(walk_fast_right *2 walk_fast_down * 6))
        waitmovement(0)
        applymovement(3, moves(face_down))
        waitmovement(0)
        removeobject(LOCALID_SKITTYOG)
        applymovement(3, moves(jump_in_place_down * 3))
        waitmovement(0)
        msgbox(format("Skitty! Come back!!!"))
        release
        applymovement(3, moves(face_left))
        waitmovement(0)           
        delay(36)     
        applymovement(3, moves(face_right))
        waitmovement(0)           
        delay(36)
        applymovement(3, moves(emote_exclamation_mark))
        waitmovement(0)           
        delay(36)
        applymovement(3, moves(walk_right * 3 face_up))
        waitmovement(0)           
        msgbox(format("Trainer! Trainer! You saw what happened, right? Oh, you have to bring my SKITTY back, you absolutely have to! I don't want her to get hurt...please find her!"))
        release
        setflag(FLAG_HIDE_GASTREE_SKITTY)
        startquest(QUEST_GASTREE_SKITTY)
        applymovement(3, moves(walk_left * 3 face_right))
        waitmovement(0)           


        //set up the skittys  
        //hide the OG
        setflag(FLAG_HIDE_GASTREE_SKITTY)   
        removeobject(LOCALID_SKITTYOG)

        //Appear skitty 1
        clearflag(FLAG_HIDE_GASTREE_SKITTY1)
        addobject(LOCALID_SKITTY1)

        //Setvars
        setvar(VAR_GASTREE_SKITTY_QUEST, 1)
        releaseall
        end
    }


    script GastreeCity_NPC2{
        lockall
        faceplayer    

        if (flag(FLAG_GASTREE_NPC_SKITTY)){
            msgbox(format("Skitty ran away because she got bored...maybe I should catch a Purrloin or Meowth for her to play with."), MSGBOX_NPC)
        } else {
            if (flag(FLAG_GASTREE_TRIGGER_SKITTY)){
                addobject(LOCALID_SKITTYOG)
                msgbox(format("Thank you trainer - my Skitty is back!"))
                release
                playmoncry(SPECIES_SKITTY, CRY_MODE_NORMAL)
                msgbox(format("SKITTY: skiiii!!!"))
                msgbox(format("Please take this as a reward."))
                giveitem(ITEM_HM_CUT)
                msgbox(format("You do need to have at least one GYM BADGE to use it. Now come here Skitty!"))
                setvar(VAR_GASTREE_SKITTY_QUEST, 5)
                completequest(QUEST_GASTREE_SKITTY)
                setflag(FLAG_GASTREE_NPC_SKITTY)
            } else {
                    msgbox(format("Have you found my SKITTY yet?"))
            }
        }
        releaseall
        end
    }

    script GastreeCity_EventScripts_SkittyTrigger1{
        msgbox(format("There's something in the tree...\pIt's Skitty!"))
        release
        delay(16)
        applymovement(LOCALID_SKITTY1, moves(emote_exclamation_mark, walk_fast_left * 2, walk_fast_down * 3 walk_fast_left *7))
        waitmovement(0)
        removeobject(17)
        msgbox(format("Skitty ran away! Gotta chase it!"))

        //set up the skittys  
        //hide skitty 1
        setflag(FLAG_HIDE_GASTREE_SKITTY1)   
        removeobject(LOCALID_SKITTY1)
        
        //Appear skitty 2
        clearflag(FLAG_HIDE_GASTREE_SKITTY2)
        addobject(LOCALID_SKITTY2)

        //Setvars
        setvar(VAR_GASTREE_SKITTY_QUEST, 2)
        releaseall
        end
    }

    script GastreeCity_EventScripts_SkittyTrigger2{
        msgbox(format("There's something in the tree...\pIt's Skitty!"))
        release
        delay(16)
        applymovement(LOCALID_SKITTY2, moves(emote_exclamation_mark walk_fast_right * 3 walk_fast_down *2 walk_fast_right *3 walk_fast_down *7))
        waitmovement(0)
        removeobject(LOCALID_SKITTY2)
        msgbox(format("Skitty ran away again. It's too slippery!"))
        //set up the skittys  
        //hide skitty 2
        setflag(FLAG_HIDE_GASTREE_SKITTY2)   
        removeobject(LOCALID_SKITTY2)
        
        //Appear skitty 3
        clearflag(FLAG_HIDE_GASTREE_SKITTY3)
        addobject(LOCALID_SKITTY3)

        //Setvars
        setvar(VAR_GASTREE_SKITTY_QUEST, 3)
        releaseall

        end
    }    

    script GastreeCity_EventScripts_SkittyTrigger3{
        msgbox(format("It's Skitty! Okay, this time you'd better come here, Skitty!"))
        release
        delay(16)
        applymovement(LOCALID_SKITTY3, moves(emote_exclamation_mark walk_fast_right * 2 walk_fast_up * 1 walk_fast_right * 4 walk_fast_up * 6))
        waitmovement(0)
        removeobject(LOCALID_SKITTY3)
        msgbox(format("Phew! Finally. Looks like Skitty ran back to its owner!"))
        //set up the skittys  
        //hide skitty 2
        setflag(FLAG_HIDE_GASTREE_SKITTY3)   
        removeobject(LOCALID_SKITTY3)
        
        //Appear skitty og
        clearflag(FLAG_HIDE_GASTREE_SKITTY)
        addobject(LOCALID_SKITTYOG)

        //Setvars
        setvar(VAR_GASTREE_SKITTY_QUEST, 4)

        //Set final flag
        setflag(FLAG_GASTREE_TRIGGER_SKITTY)

        releaseall
        end
    } 

    script GastreeCity_NPC_Skitty{
        lockall
        faceplayer    
        playmoncry(SPECIES_SKITTY, CRY_MODE_NORMAL)        
        msgbox(format("SKITTY: skee~~"))
        releaseall
        end
    }


script GastreeCity_Poltchageist{
    lockall
    playmoncry(SPECIES_POLTCHAGEIST, CRY_MODE_NORMAL)
    faceplayer
    msgbox(format("Poltchageist is picking herbs for the teahouse!"))
    releaseall
    end
}




